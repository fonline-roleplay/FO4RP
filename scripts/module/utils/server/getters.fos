shared Map@ GetLocationMap( uint location_pid )
{
	return GetLocationMap( location_pid, 0 );
}

shared Map@ GetLocationMap( uint location_pid, uint map_index )
{
	Location@ loc = GetLocationByPid( location_pid, 0 );
	if( !valid( loc ) ) 
	{
		Log( "There is no Location pid #" + location_pid + "." );
		return null;
	}
	
	Map@ map = loc.GetMapByIndex( map_index );
	if( !valid( map ) )
	{
		Log( "There is no map # " + map_index + " at location pid #" + location_pid + "." );
	}

	return map;
}

shared Critter@[] getCritters( Map@ map, uint pid, uint find )
{	
	Critter@[] crs;
	int countCr = map.GetCritters( pid, find, crs );
	return crs;	
}

shared string getPlayerInfo( Critter@ cr ) 
{
	return cr.Name + " #" + cr.Id;
}

shared Critter@[] getControlledNPC( Critter& cr )
{
    Critter@[] result();
    cr.GetFollowGroup( FIND_LIFE | FIND_ONLY_NPC, result );
	return result;
}

shared bool sameFaction( Critter& cr1, Critter& cr2 )
{
	if( cr1.ParamBase[ ST_FACTION ] != 0 )
	{
		return cr1.ParamBase[ ST_FACTION ] == cr2.ParamBase[ ST_FACTION ];
	}
	return false;
}

shared bool isOwningNPC( Critter& cr, Critter& npc )
{
	if( npc.ParamBase[ ST_FACTION ] != 0 )
	{
		return npc.ParamBase[ ST_FACTION ] == cr.ParamBase[ ST_FACTION ];
	}

	return npc.ParamBase[ MERC_MASTER_ID ] == int( cr.Id );		
}

shared bool isTesting( Critter& cr, int flag )
{
	return cr.Param[ QST_GAMEMODE ] == GAME_TEST && FLAG( cr.ParamBase[CR_TEST_MODE], flag );
}

shared bool hasItems( Critter& cr, uint16[] pids, int slot = -1 )
{
	return getItems( cr, pids, slot ).length() > 0;
}

shared uint16[] GetFastContaireds()
{
	return array<uint16> = { PID_CHEMTEC_CASE, PID_BLOODY_FIRST_AID_BAG, PID_FIRST_AID_BAG, PID_DOCTORS_BAG, PID_FIRST_AID_KIT, PID_FIELD_MEDIC_KIT, PID_PARAMEDICS_BAG, PID_MEDICAL_SUPPLIES };
}

shared Item@[] getItems( Critter& cr, uint16[] pids, int slot = -1 )
{
	Item@[] result;
	Item@[] items;
	Item@ item = null;
	
	uint count = cr.GetItems( uint(slot), items );
	
	const uint16[] fast_containers = GetFastContaireds();
	
	for( uint i = 0; i < count; i++ )
	{
		@item = items[i];
		if( valid( item ) )
		{
			uint16 itemPID = item.GetProtoId();
			
			if( pids.find( itemPID ) != -1 )
			{
				result.insertLast( item );
				continue;
			}
			
			if( fast_containers.find( itemPID ) != -1 )
			{
				Item@[] subitems;
				Item@ subitem = null;
				uint subcount = item.GetItems( 0, subitems );
				
				for( uint j = 0; j < subcount; j++ )
				{
					@subitem = subitems[j];
					if( valid( subitem ) )
					{
						if( pids.find( subitem.GetProtoId() ) != -1 )
						{
							result.insertLast( subitem );						
						}
					}
				}
			}
		}
	}
	
	return result;
}

shared bool hasItem( Critter& cr, uint16 pid = 0, int slot = -1 )
{
	return valid( getItem( cr, pid, slot ) );
}

shared Item@ getItem( Critter& cr, uint16 pid = 0, int slot = -1 )
{
	Item@ result = cr.GetItem( pid, uint(slot) );
	if( valid( result ) )
	{
		return result;
	}
	Item@[] items;
	uint count = cr.GetItems( uint(slot), items );
	
	if( pid == 0 && count > 0 )
	{
		return items[0];
	}
	
	auto fast_containers = GetFastContaireds();
	
	for( uint i = 0; i < count; i++ )
	{
		uint16 itemPID = items[i].GetProtoId();
		
		if( itemPID == pid )
		{
			return items[i];
		}
		
		if( fast_containers.find( itemPID ) != -1 )
		{
			Item@[] subitems;
			Item@ subitem;
			uint subcount = items[i].GetItems( 0, subitems );
			for( uint j = 0; j < subcount; j++ )
			{
				@ subitem = subitems[j];
				if( valid( subitem ) )
				{
					if( subitem.GetProtoId() == pid )
					{
						return subitem;
					}
				}
			}
		}
	}
	return null;
}

shared int rollLuck( Critter@ cr )
{
	if( !valid( cr ) )
	{
		return 0;
	}
	return rollLuck( cr.Stat[ST_LUCK] );
}

shared int rollLuck( int luck )
{
	if( luck < 6 )
	{
		return luck - 6;
	}	
	return luck - 5;
}

shared string IdName( Critter& cr )
{
	return "#" + cr.Id + ( cr.IsPlayer() ? " " + GetPlayerName( cr.Id ) : "" );
}

shared uint CountItemsByPID( Item@ container, uint PID )
{
	if( !isContainer( container ) )
	{
		return 0;
	}
	
	ProtoItem@ proto = GetProtoItem( PID );
	if( !valid( proto ) )
	{
		return 0;
	}
	
	if( proto.Stackable )
	{
		Item@ item = container.GetItem( PID, 0 );
		if( !valid( item ) )
		{
			return 0;
		}
		
		return item.GetCount();
	}
	
	return CountItemsByPID( getAllItemsFrom( container ), PID );
}

shared ItemMutual@[] getAllItemsFrom( ItemMutual@ container )
{
	Item@[] result;
	if( isContainer( container ) )
	{
		container.GetItems( uint( -1 ), result );
	}
	
	return result;
}

shared Critter@ getTarget( Critter& cr, int crId = 0 )
{
	Critter@ target = null;
	if( crId == 0 )
	{
		crId = cr.Id;
		@target = @cr;
	}
	else
	{
		@target = GetCritter( crId );
	}
	
	return target;
}

shared uint LoadedPercent( Item& item )
{
  return item.GetType() != ITEM_TYPE_CONTAINER ? 0 : ContainmentVolume( item ) * 100 / item.Proto.Container_Volume;
}

shared uint ItemsWeight( Critter& cr )
{
	return 	uint( ItemsWeight( cr, SLOT_INV ) + 
			ItemsWeight( cr, SLOT_HAND1 ) + 
			ItemsWeight( cr, SLOT_HAND2 ) + 
			ItemsWeight( cr, SLOT_HEAD ) + 
			ItemsWeight( cr, SLOT_BACK ) +
			ItemsWeight( cr, SLOT_MISC ) +
			ItemsWeight( cr, SLOT_ARMOR ) * 0.75 );
}

//ContainerWeightEase
shared uint ItemsWeight( Critter& cr, int slot )
{
	if( slot == SLOT_BACK || slot == SLOT_MISC )
	{
		Item@ ItemInSlot = cr.GetItem( 0, slot );
		if( valid( ItemInSlot ) )
		{
			return uint( ItemsWeight( ItemInSlot ) * ContainerWeightEase( ItemInSlot ) );
		}
	}

    Item@[] list;
	Item@ listed = null;
	uint list_length = cr.GetItems( slot, list );
	
    for( uint i = 0, iend = list.length(); i < iend; i++ )
	{
		@ listed = list[i];
		if( valid( listed ) )
		{
			if( listed.GetType() == ITEM_TYPE_CONTAINER )
			{
				list_length += listed.GetItems( 0, list );
			}
		}
    }
	
	auto glass_bottles2 = glass_bottles[2];
    uint result = 0;
    for( uint i = 0; i < list_length; i++ )
	{
		@ listed = list[i];
        if( valid( listed ) )
        {
            result += listed.GetCount() * listed.Proto.Weight;

            if( listed.Proto.Type == 25 )
			{
                result += listed.Val2;
			}
 
			if( glass_bottles2.find( listed.GetProtoId() ) != -1 )
			{
				result -= listed.Proto.Partial_Item * 100 - listed.Val2;
			}
		}
        else
		{
            cr.Say( SAY_NETMSG, "#" + i + " !valid" );
		}
	}
    return result;
}

shared uint ItemsWeight( Item& item )
{ 
	return ItemsWeight( item, 0 ); 
}

shared uint ItemsWeight( Item& item, int count )
{
	if( item.GetType() != ITEM_TYPE_CONTAINER )
	{
		return ( count == 0 ? item.GetCount() : count ) * item.Proto.Weight;
	}
	
    Item@[] list;
	item.GetItems( 0, list );
	Item@ listed = null;
	for( uint i = 0, len = list.length(); i < len; i++ )
	{
		@ listed = list[i];
		if( valid( listed ) )
		{
			if( listed.GetType() == ITEM_TYPE_CONTAINER )
			{
				Item@[] sublist;
				listed.GetItems( 0, sublist );

				for( uint j = 0, jmax = sublist.length(); j < jmax; j++ )
				{
					list.insertLast( sublist[j] );
				}
			}
		}
	}
	
    uint result = 0;
	for( uint i = 0, imax = list.length(); i < imax; i++ )
	{
		@ listed = list[i];
		if( valid( listed ) )
		{
			result += listed.GetCount() * listed.Proto.Weight;
			
			if( listed.GetProtoId() == PID_POT_ALCO || listed.Proto.Type == 25 )
			{
				result += listed.Val2;
			}
			
			if( glass_bottles[2].find( listed.GetProtoId() ) != -1 )
			{
                result += ( listed.Proto.Weight - ( listed.Proto.Partial_Item * 100 - listed.Val2 ) );
			}
		}
    }
    return result + item.GetCount() * item.Proto.Weight;
}

shared float ContainerBonusWeight( Item@ item )
{
	if( !valid( item ) )
	{
		return 0;
	}
	
	switch( item.GetProtoId() )
	{
		case( PID_CR_CART ):
			return 3;
		case( PID_CR_BOX ):
			return 1;
		default:
			return 0.5;
	}
	
	return 0.5;
}

shared float ContainerWeightEase( Item@ item )
{
	if( !valid( item ) )
	{
		return 0;
	}
	
	switch( item.GetProtoId() )
	{
		case( PID_BACKPACK ):
		case( PID_AMMO_PACK ):
		case( PID_PORTABLE_REFRIGERATOR ):
		case( PID_STUFF_SUPPLIES):		
			return 0.5;
		case( PID_BROWN_BAG ):
		case( PID_TRAVEL_BAG ):
		case( PID_CHEMTEC_CASE ):
		case( PID_METAL_CASE ):
		case( PID_BLOODY_FIRST_AID_BAG ):
		case( PID_FIRST_AID_BAG ):
		case( PID_cashbag ):
		case( PID_DOCTORS_BAG ):
		case( PID_FIRST_AID_KIT ):		
		case( PID_FIELD_MEDIC_KIT ):		
		case( PID_PARAMEDICS_BAG ):		
		case( PID_MEDICAL_SUPPLIES ):		
		case( PID_BRIEFCASE ):		
			return 0.8;
		case( PID_WALLET ):
		case( PID_AMMO_BELT ):
			return 0.1;
	}
	return 1.0;
}

shared int SNEAK_TIMEOUT( Critter@ cr )
{
	if( !valid( cr ) )
	{
		return 0;
	}
	return __FullSecond + REAL_SECOND( MAX( 34 - cr.Stat[ ST_SEQUENCE ], 3 ) );
}

shared void setTimeout( Critter@ cr, uint id, int value )
{
	if( !valid( cr ) )
	{
		return;
	}
	if( cr.Timeout[ id ] < int( REAL_SECOND( value ) ) )
	{
		cr.TimeoutBase[ id ] = __FullSecond + REAL_SECOND( value );
	}
}

shared bool Find( Critter@& link, int id )
{
	if( id == 0 )
	{
		return false;
	}
	
	@link = GetCritter( uint( id ) );	
	return valid( link );
}

shared Item@[] getItemsAround( Item& target )
{
	Item@[] empty_result;

	Map@ map = GetMap( target.MapId );
	if( !valid( map ) && Warning( "Can't find items around item #" + target.Id + ", as it's at the global map.", true ) )
	{
		return empty_result;
	}
	
	return getItemsAround( map, target.HexX, target.HexY, 6 );
}

shared Item@[] getItemsAround( Critter& target )
{
	Item@[] empty_result;

	Map@ map = target.GetMap( );
	if( !valid( map ) && Warning( "Can't find items around critter #" + target.Id + ", as it's at the global map.", true ) )
	{
		return empty_result;
	}
	return getItemsAround( map, target.HexX, target.HexY, 6 );
}

shared Item@[] getItemsAround( Map& map, uint16 x, uint16 y, uint8 range )
{
	if( range == 0 )
	{
		range == 1;
	}
	Item@[] result;
	
	for( uint i = 0; i < range; i++ )
	{
		uint16 tx = x, ty = y;
		map.MoveHexByDir( tx, ty, i, 1 );
		map.GetItems( tx, ty, result );
	}
	map.GetItems( x, y, result );
	
	return result;
}

shared string itemInfo( Item@ item )
{
	if( !valid( item ) )
	{
		return "[null]";
	}
	
	return "[" + item.GetProtoId() + ":" + item.GetCount() + "]";
}

shared string itemDesc( Scenery@ target )
{
	return !valid( target ) ? "[null]" : itemName( target.ProtoId ) + " PID " + target.ProtoId;
}

shared string itemDesc( Item@ item ) 
{
	if( !valid( item ) )
	{
		return "[null]";
	}
	
	return itemName( item ) + itemCount(item) + " " + item.GetProtoId() + "#" + item.Id;
}

shared string itemName( Item& item ) 
{ 
	return itemName( item.GetProtoId(), Item_GetLexems( item ) ); 
}

shared string itemName( uint pid, string@ lexems = null )
{
	string@ name = GetMsgStr( 1, TEXTMSG_ITEM, pid * 100 );
	return valid( name ) ? "[ " + name + ( valid( lexems ) ? ", " + lexems : "" ) + " ]" : "";
}

shared string itemNameID( uint id ) 
{ 
	Item@ item = GetItem( id );
	if( !valid( item ) )
	{
		return "[null]";
	}
	
	return itemName( item ); 
}

shared string getPID_name( int pid )
{
	string@ text = GetItemMsgStr( pid );
	return valid( text ) ? text : "[PID:" + pid + "]";
}

shared string itemCount( Item& item )
{
	if( !item.IsStackable() )
	{
		return "";
	}
	
	uint count = item.GetCount();
	if( count > 1 )
	{
		return " x" + count;
	}
	
	return "";
}

shared string ItemState( Item& item, bool fullInfo = false )
{
	string info = "Object " + ( fullInfo ? "¹" + item.Id + ":" + item.GetProtoId() + " " : "" ) + "is ";
	switch( item.Accessory )
	{
		case( ACCESSORY_NONE ):
			info += "nowehere (created with ~ais)";
			break;
		case( ACCESSORY_CRITTER ):
			info += "in critter ¹" + item.CritId + " in slot ¹" + item.CritSlot;
			break;
		case( ACCESSORY_HEX ):
			info += "on map ¹" + item.MapId + " in position (" + item.HexX + ", " + item.HexY + ")";
			break;
		case( ACCESSORY_CONTAINER ):
			info += "inside container ¹" + item.ContainerId;
			break;
		default:
			info += "nowhere (exception, please report in #bugs)";
			break;
	}
	return info + ".";	
}

shared int ContainmentVolume( Item@ item )
{
	if( !valid( item ) )
	{
		return 0;
	}
	
	Item@[] loot;
	Item@ lootItm = null;
	uint volume = 0;
	uint count = item.GetItems( uint(-1), loot );
	for( uint i = 0; i < count; i++ )
	{
		@ lootItm = loot[i];
		if( valid( lootItm ) )
		{
			volume += lootItm.Proto.Volume * lootItm.GetCount();
		}
	}
	return volume;
}

shared string FullItemInfo( Item@ item )
{
	if( !valid( item ) )
	{
		return "[null]";
	}
	uint count = item.GetCount();
	float weight = float( item.Proto.Weight ) / 1000;
	string item_name = itemName( item );
	
	string info = 
		emptySpace( item_name, 60 - int( item_name.length()*0.5 ), "|0x231D18 ", "|0xAAAA00 " ) + item_name + "\n" +
		StyledText( "|", "ProtoID: ", item.GetProtoId() ) + 
		StyledText( "\n", "ID: ", item.Id ) +
		StyledText( "|", "Count: ", count ) + 
		StyledText( "\n", "Flags: ", item.Flags ) +
		StyledText( "|", "Cost: ", OneAll( item.Proto.Cost, count ) ) +
		StyledText( "\n", "Volume: ", OneAll( item.Proto.Volume, count ) ) +
		StyledText( "\n", "Weight: ", OneAll( weight, count ) ) +
		"";
		
	if( item.Proto.Deteriorable )
	{
		info += StyledText( "\n", "Deterioration: ", item.Deterioration );
	}
	
	if( item.Proto.Container_Volume > 0 )
	{
		info += StyledText( "\n", "Used Space: ", ContainmentVolume( item ) + "/" + item.Proto.Container_Volume );
	}
	
	uint8 type = item.GetType();
	if( type == ITEM_TYPE_DOOR || type == ITEM_TYPE_CONTAINER )
	{
		bool always_open = ( type == ITEM_TYPE_CONTAINER && !item.Proto.Container_Changeble );
		string state = "" +
			( FLAG( item.LockerCondition, LOCKER_ISOPEN ) ? "open " : "" ) +
			( FLAG( item.LockerCondition, LOCKER_JAMMED ) ? "jammed " : "" ) +
			( FLAG( item.LockerCondition, LOCKER_BROKEN ) ? "broken " : "" ) +
			( FLAG( item.LockerCondition, LOCKER_NOOPEN ) ? "blocked " : "" ) +
			( FLAG( item.LockerCondition, LOCKER_LOCKED ) ? "locked " : "" ) +
			( FLAG( item.LockerCondition, LOCKER_ELECTRO ) ? "electro " : "" ) +
			"";

		if( state.length() < 2 )
		{
			state = always_open ? "always open" : "closed";
		}

		info += StyledText( "|", "State: ", state );		
		
		if( FLAG( item.LockerCondition, LOCKER_LOCKED ) )
		{
			info += StyledText( "\n", "Id: ", item.LockerId ) + StyledText( "\n", "Complexity: ", item.LockerComplexity );
		}
		
		if( FLAG( item.LockerCondition, LOCKER_ELECTRO ) )
		{			
			info += StyledText( "\n", "Code: ", item.Val4 ) + StyledText( "\n", "Complexity: ", item.LockerComplexity );
		}
	}
	return info;
}

shared string crInfo( int id, bool full = false )
{
	return crInfo( id <= 0 ? null : GetCritter( id ), full );
}

shared string crInfo( Critter@ cr, bool full = false )
{
	if( !valid( cr ) )
	{
		return "[null]";
	}
	
	uint id = cr.Id;
	
	string fullInfo = full ? " (" + cr.HexX + ":" + cr.HexY + ")" : "";
	
	string name = "unknown";
	if( !cr.IsPlayer() )
	{
		string@ name_str = GetDlgMsgStr( cr.GetProtoId() * 10 );
		name = ( valid( name_str ) ? name_str : "mob" );
	}
	else
	{
		name = GetPlayerName( id );
	}
	if( cr.ParamBase[ QST_INVIS ] == 1 )
	{
		name = embed( name, "<", ">" );
	}
	return "#" + id + " " + embed( name, "[ ", " ]" ) + fullInfo;
}

shared string timeInfo()
{
	return __Day + "." + __Month + "." + __Year + " " + MakeTime( __FullSecond );
}

shared string TimeToString()
{
    uint16 year = 0;
    uint16 month = 0;
    uint16 dayOfWeek = 0;
    uint16 day = 0;
    uint16 hour = 0;
    uint16 minute = 0;
    uint16 second = 0;
    uint16 milliseconds = 0;
    GetTime( year, month, day, dayOfWeek, hour, minute, second, milliseconds );
    return "" + ( day > 9 ? ( "" + day ) : ( "0" + day ) ) + "." + ( month > 9 ? ( "" + month ) : ( "0" + month ) ) + "." + year + " " + ( hour > 9 ? ( "" + hour ) : ( "0" + hour ) ) + ":" + ( minute > 9 ? ( "" + minute ) : ( "0" + minute ) );
}

shared string RealDay()
{
    uint16 year = 0;
    uint16 month = 0;
    uint16 dayOfWeek = 0;
    uint16 day = 0;
    uint16 hour = 0;
    uint16 minute = 0;
    uint16 second = 0;
    uint16 milliseconds = 0;
    GetTime( year, month, day, dayOfWeek, hour, minute, second, milliseconds );
	
	string year_text = ( year + 228 ) + ".";
	
    return year_text + smart_two_digits( month, ".", true ) + smart_two_digits( day, "", true );
}

shared string RealTime()
{
    uint16 year = 0;
    uint16 month = 0;
    uint16 dayOfWeek = 0;
    uint16 day = 0;
    uint16 hour = 0;
    uint16 minute = 0;
    uint16 second = 0;
    uint16 milliseconds = 0;
    GetTime( year, month, day, dayOfWeek, hour, minute, second, milliseconds );
    return smart_two_digits( hour, ":", true ) + smart_two_digits( minute, ":", true ) + smart_two_digits( second, "", true );
}

//format: "[pid:count], [pid:count]"
shared string enlist( Item@[] list )
{
	if( !valid( list ) )
	{
		return "[null]";
	}
	
	uint len = list.length();
	if( len == 0 )
	{
		return "[empty]";
	}
	
	string result = itemInfo( list[0] );
	for( uint i = 1; i < len; i++ )
	{
		result += ", " + itemInfo( list[i] );
	}
	return result;
}

shared int GetGvar( uint gvar )
{
    GameVar@ var = GetGlobalVar( gvar );
    if( valid( var ) )
	{
        return var.GetValue();
    }
	else
	{
        Log( "GVar " + gvar + " not valid" );
        return -1;
    }
}

shared void SetGvar( uint gvar, uint value )
{
    GameVar@ var = GetGlobalVar( gvar );
    if( valid( var ) )
	{
        var = value;
    }
	else
	{
        Log("GVar " + gvar + " not valid");
    }
}

shared bool IsHuman( Critter& cr )
{
	uint[] mutants = { BT_MEN, BT_WOMEN, BT_CHILDREN };
	return mutants.find( cr.Stat[ ST_BODY_TYPE ] ) != -1;
}

shared bool IsRobot( Critter& cr )
{
	uint[] robots = { BT_ROBOT };
	return robots.find( cr.Stat[ ST_BODY_TYPE ] ) != -1;
}

shared bool IsMutant( Critter& cr )
{
	uint[] mutants = { BT_SUPER_MUTANT, BT_GHOUL, BT_CENTAUR, BT_FLOATER, BT_ALIEN, BT_SWAMP_LURKER, BT_CROCS, BT_BIG_BAD_BOSS };
	return mutants.find( cr.Stat[ ST_BODY_TYPE ] ) != -1;
}

shared bool IsAnimal( Critter& cr )
{
	uint[] animals = { BT_BRAHMIN, BT_RADSCORPION, BT_RAT, BT_DOG, BT_MANTI, BT_PLANT, BT_GECKO, BT_LAVASH };
	return animals.find( cr.Stat[ ST_BODY_TYPE ] ) != -1;
}

shared bool IsMonster( Critter& cr )
{
	uint[] animals = { BT_DEATHCLAW, BT_YAO };
	return animals.find( cr.Stat[ ST_BODY_TYPE ] ) != -1;
}

shared bool IsInsect( Critter& cr )
{
	uint[] animals = { BT_MANTI, BT_GIANT_ANT, BT_GIANT_BEETLE, BT_GIANT_WASP, BT_MILLEPEDE, BT_SPIDER };
	return animals.find( cr.Stat[ ST_BODY_TYPE ] ) != -1;
}

shared int GetProtoIdByName( string& name ) 
{
    return GetConstantValue( CONSTANTS_ITEM, name );
}

shared uint MovementState( Critter& cr )
{
	uint16 prev_hex_x = 0;
	uint16 prev_hex_y = 0;
	int ticks_since_last_hex = PreviousHex( cr, prev_hex_x, prev_hex_y );
	if( cr.IsRuning && ticks_since_last_hex <= cr.Param[ ST_RUN_TIME ] + 50 )
	{
		return 1;
	}
	else if( !cr.IsRuning && ticks_since_last_hex <= cr.Param[ ST_WALK_TIME ] + 50 )
	{
		return 2;
	}
	else
	{
		return 0;
	}
}

shared bool isBlade( Item@ item )
{
	if( item.Proto.Item_Subtype == ITEM_SUBTYPE_WEAPON_MELEE_KNIFE || item.Proto.Item_Subtype == ITEM_SUBTYPE_WEAPON_MELEE_LONG_KNIFE || item.Proto.Item_Subtype == ITEM_SUBTYPE_WEAPON_GRENADE_KIFE )
	{
		return true;
	}
	return false;
}

shared bool HasBlade( Critter& cr )
{
	Item@[] blades; 
	cr.GetItemsByType( ITEM_TYPE_WEAPON, blades );
	if( blades.length > 0 )
	{
		for( uint i = 0; i < blades.length(); i++ )
		{
			Item@ bladeItem = blades[i];
			if( isBlade( bladeItem ) )
			{
				return true;
			}
		}
	}
	return false;
}

shared bool SamePic( uint PicMap, uint pid )
{
    ProtoItem@ proto = GetProtoItem( pid );
    if( !valid( proto ) )
	{
		return false;
    }
	
    return proto.PicMap == PicMap;
}

shared bool ExoCheck( Critter& cr )
{
	Item@ armor = _CritGetItemArmor( cr );
	if( valid( armor ) )
	{
		switch( armor.Proto.Armor_Perk )
		{
			case( ARMOR_PERK_POWERED ):
			case( ARMOR_PERK_ADVANCED_I ):
			case( ARMOR_PERK_ADVANCED_II ):
			case( ARMOR_PERK_ECZO ):
			case( ARMOR_PERK_ECZO_2 ):
				return true;
			default:
				return false;
		}
	}
	return false;
}