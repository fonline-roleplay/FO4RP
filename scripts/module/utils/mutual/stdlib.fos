shared string itemText( ItemMutual@ item )
{
	return itemText( item, 0 );
}

shared string itemText( ItemMutual@ item, int max_length )
{
	if( !valid( item ) ) return "[null]";
	
	string@ name;
	if( item.Val9 == PID_POPUP || item.GetProtoId() == PID_POPUP )
	{
		#ifdef __SERVER
			@name = Item_GetLexems( item );
		#endif
		
		#ifdef __CLIENT
			@name = "" + item.Lexems;
		#endif
	}
	else
	{
		#ifdef __SERVER
			@name = GetMsgStr( 0, TEXTMSG_ITEM, STR_ITEM_INFO( item ) );
		#endif
		
		#ifdef __CLIENT
			@name = GetMsgStr( TEXTMSG_ITEM, STR_ITEM_INFO( item ) );
		#endif
	}
	
	string result = TRIM_DOTS( name );
	if( max_length > 0 && pixelength( result ) > max_length )
	{
		result = substring( result, 0, MIN( uint( max_length / 7.5f ), result.length() ) ) + "..";
	}
	
	return itemColor(item) + result + COLOR_NETMSG;
}

shared string getItemTextName(string lexems)
{
	string colorSuffix = lexems.length() > COLOR_NETMSG.length() && COLOR_NETMSG == substring(
		lexems, 
		lexems.length() - COLOR_NETMSG.length(), 
		COLOR_NETMSG.length()
	) ? COLOR_NETMSG : "";
			
	return substring( lexems, 0, findFirst( lexems, ". " ) ) + colorSuffix;
}

shared string getItemTextDescription(string lexems)
{
	if( lexems.length() < 1 )
	{
		return "";
	}
	int pos = findFirst( lexems, ". " );
	int off = lexems[ pos + 1 ] == ' ' ? 1 : 0;
	return pos > 0 ? substring( lexems, pos + off + 1, lexems.length() - pos ) : "";
}

shared bool isDisabledSkill( uint skillIndex )
{
	uint[] disabled_skills = { SK_SPEECH, SK_SNEAK, SK_GAMBLING };
	return( disabled_skills.find( skillIndex ) != -1 );
}

shared bool isFarmingSkill( uint skillIndex )
{
	uint[] farming_skills = { SK_MINING, SK_CHOPPING, SK_SCAVENGING, SK_HUNTING, SK_FISHING, SK_FARMING, SK_COOKING, SK_SEX };
	return( farming_skills.find( skillIndex ) != -1 );
}

shared string itemColor( ItemMutual& item )
{
	switch( item.GetType() )
	{
		case( ITEM_TYPE_NONE ):
			return COLOR_NONE; 
		case( ITEM_TYPE_ARMOR ):
			return COLOR_ARMOR;
		case( ITEM_TYPE_DRUG ):
			return COLOR_DRUGS; 
		case( ITEM_TYPE_WEAPON ):
			return COLOR_WEAPON;  
		case( ITEM_TYPE_AMMO ):
			return COLOR_AMMO;  
		case( ITEM_TYPE_MISC ):
			return COLOR_MISC;  
		case( ITEM_TYPE_KEY ):
			return COLOR_KEY;  
		case( ITEM_TYPE_CONTAINER ):
			return COLOR_CONTAINER;  
		case( ITEM_TYPE_DOOR ):
			return COLOR_DOOR;  
		case( ITEM_TYPE_GRID ):
			return COLOR_GRID;  
		case( ITEM_TYPE_GENERIC ):
			return COLOR_GENERIC;  
		case( ITEM_TYPE_WALL ):
			return COLOR_WALL;  
		case( ITEM_TYPE_CAR ):
			return COLOR_CAR;  
		case( ITEM_TYPE_PLANT ):
			return COLOR_PLANT;     
		case( ITEM_TYPE_FOOD ):
			return COLOR_FOOD;  
		case( ITEM_TYPE_DRINK ):
			return COLOR_DRINK;
		case( ITEM_TYPE_SMOKER ):
			return COLOR_SMOKER;
		case( ITEM_TYPE_FLASK ):
			return COLOR_FLASK;
	}
	return COLOR_NETMSG;
}

shared string cutter( string input, int min_line_length = 20, int max_line_length = 30, int max_string_length = 126 )
{
	int len = input.length();
	if( len == 0 || len < min_line_length )
	{
		return input;
	}
	
	if( len > max_string_length )
	{
		input = substring( input, 0, max_string_length - 2 ) + "..";
	}
	
	string@[]@ words = split( input, " " );
	if( !valid( words ) || words.length() == 0 )
	{
		return input;
	}
	
	string result = "";
	int current_length = 0;
	
	for( int i = 0, count = words.length(); i < count; i++ )
	{
		int word_len = words[i].length();
		if( current_length + word_len >= min_line_length )
		{
			current_length = 0;
			result += words[i] + "\n";
			continue;
		}
		
		if( word_len > 3 && current_length + word_len >= max_line_length )
		{
			int hardcut = current_length + word_len - max_line_length;
			string@ word_cut_head = substring( words[i], 0, hardcut );
			string@ word_cut_tail = substring( words[i], hardcut, word_len - hardcut );
			if( valid( word_cut_head ) && valid( word_cut_tail ) && word_cut_head.length() > 2 && word_cut_tail.length() > 2 )
			{
				result += word_cut_head + ".." + "\n" + ".." + word_cut_tail + " ";
				current_length = word_cut_tail.length() + 2;
				continue;
			}
		}

		result += words[i];
		if( i < count - 1 )
		{
			result += " ";
		}
		
		current_length += words[i].length();
	}
	
	return result;
}

shared bool isContainer( ItemMutual@ container )
{
	return valid( container ) && container.GetType() == ITEM_TYPE_CONTAINER;
}

shared uint CountItemsByPID( ItemMutual@[] list, uint PID )
{
	uint len = list.length();
	if( len == 0 ) return 0;

	for( uint i = 0; i < len; i++ )
	{			
		ItemMutual@ item = list[i];
		
		if( valid( item ) && item.GetProtoId() == PID )
		{				
			if( !item.IsStackable() )
			{
				uint count = 1;
				for( uint j = i + 1; j < len; j ++ )
				{
					if( valid( list[j] ) && list[j].GetProtoId() == PID )
					{
						count++;
					}
				}
				return count;
			}
			
			return item.GetCount();
		}
	}
	
	return 0;
}

shared uint CalculateItemCost( ItemMutual& item, CritterMutual& cr, CritterMutual& npc, bool sell )
{
    uint8 itemType = item.GetType();
    uint16 pid = item.GetProtoId();
    uint cost = item.Proto.Cost;

    if( pid == PID_BOTTLE_CAPS || pid == PID_REAL_BOTTLE_CAPS )
	{
        return 1;
	}
	
	if( pid == PID_GOLDEN_COIN )
	{
		return 10;
	}
	
	if( pid == PID_DOLLAR_100 )
	{
		return 100;
	}
	
	if( pid == PID_DOLLAR_20 )
	{
		return 20;
	}
	
	if( pid == PID_DOLLAR_5 )
	{
		return 5;
	}
	
    if( itemType == ITEM_TYPE_ARMOR || itemType == ITEM_TYPE_WEAPON )
    {
        uint8 brokenCount = item.BrokenCount;
        uint8 brokenFlags = item.BrokenFlags;

        if( FLAG( brokenFlags, BI_NOTRESC ) || FLAG( brokenFlags, BI_BROKEN ) )
		{
            cost = cost * 10 / 100;
		}
        else if( FLAG( brokenFlags, BI_HIGHBROKEN ) )
		{
            cost /= 3;
        }
		else if( FLAG( brokenFlags, BI_NORMBROKEN ) )
		{
            cost /= 2;
        }
		else if( FLAG( brokenFlags, BI_LOWBROKEN ) )
		{
            cost = cost * 100 / 140;
        }
		else if( brokenCount > 0 )
		{
            cost = brokenCount != 100 ? ( cost * brokenCount * 10 / 100 ) : cost * 10 / 100;
		}
		
        if( itemType == ITEM_TYPE_WEAPON && !item.IsStackable() )
        {
            cost += GetProtoItem( item.AmmoPid ).Cost * item.AmmoCount;
        }
    }

	//Check for partial pids and NPC price cap
	ProtoItem@ protoItem = GetProtoItem( pid );
	if( protoItem.Partial_Item > 0 && protoItem.Type != ITEM_TYPE_FLASK )
	{
		cost = cost * item.Val0 / protoItem.Partial_Item;
	}
	
	int skill_range = 150;
	float skill = CLAMP( cr.Skill[ SK_BARTER ] - npc.Skill[ SK_BARTER ], -skill_range, skill_range );
	skill = skill / skill_range * 25; //That makes value from [-1..1] to [-25..25]

	if( sell )
	{
		cost = cost * 40 / 100; //40% from cogs is max
	}
	else
	{
		cost *= 75 - int( skill ); //from 50% to 100% of cost
		cost /= 100;
	}
	
	if( cost < 1 )
	{
		cost = 1;
	}
	
	return cost;
}

bool SameFaction( CritterCl& cr1, CritterCl& cr2 )
{
	if( cr1.ParamBase[ ST_FACTION ] != 0 )
	{
		return cr1.ParamBase[ ST_FACTION ] == cr2.ParamBase[ ST_FACTION ];
	}
	
	return false;
}