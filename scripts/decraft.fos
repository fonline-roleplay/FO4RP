#ifndef DECRAFT
#define DECRAFT

#include "_utils.fos"
#include "_ltp.fos"
#include "gathering_h.fos"
#include "handcuffs_h.fos"
#include "repair_h.fos"
#include "decraft.fosh"
#include "craft_recipes.fosh"

dictionary DecraftList;

bool CanDecraft( CraftRecipe@ recipe )
{
	if( !valid( recipe ) )
	{
		return false;
	}
	
	uint ResCount = 0;
	for(uint i = 0, len = recipe.Resources.length(); i < len; i++)
	{
		if( NonDecraftableResources.find( recipe.Resources[i] ) != -1 )
		{
			continue;
		}
		ResCount++;
	}

	if( ResCount <= 1 )
	{
		return false;
	}
	
	if( recipe.Output.length() >= 1 && recipe.OutputCount[0] > 1 )
	{
		return false;
	}
	
	return true;
}

ItemPartArray@ ProcessDecraftRecipe(CraftRecipe@ recipe)
{
	if( !valid( recipe ) )
	{
		return null;
	}
	
	ItemPartArray@ Decraft;

	if( DecraftList.exists( ""+recipe.Output[0] ) )
	{
		DecraftList.get(""+recipe.Output[0], @Decraft);
		return Decraft;
	}
	
	@Decraft = ItemPartArray();
	Decraft.Tier = recipe.Tier;
	Decraft.Tools = recipe.Tools;

	ProtoItem@ proto = GetProtoItem(recipe.Output[0]);

	if( valid( proto ) && ( proto.Type == ITEM_TYPE_ARMOR || proto.Type == ITEM_TYPE_WEAPON ) )
	{
		SETFLAG(Decraft.Tools, TOOL_MULTITOOL);
	}

	for(uint i = 0, len = recipe.Resources.length(); i < len; i++)
	{
		uint16 pid = recipe.Resources[i];
		uint count = recipe.ResourcesCount[i];
		if( NonDecraftableResources.find( pid ) != -1 )
		{
			continue;
		}
		
		CraftRecipe@ ComponentRecipe = GetCraftByItemPID( pid );
		if( !valid( ComponentRecipe ) || !CanDecraft(ComponentRecipe) ) 
		{
			Decraft.Add(pid, count);
			continue;
		}

		ItemPartArray@ ComponentDecraft = ProcessDecraftRecipe(ComponentRecipe);
		if(!valid(ComponentDecraft))
		{
			continue;
		}
		
		for(uint j = 0, jlen = ComponentDecraft.length(); j < jlen; j++)
		{
			ItemPart@ currCompDecraftRes = ComponentDecraft[j];
			if(!valid(currCompDecraftRes))
			{
				continue;
			}
			
			Decraft.Add(currCompDecraftRes.Pid, currCompDecraftRes.Count * count);
		}
	}

	if(Decraft.length() > 0)
	{
		DecraftList.set( ""+recipe.Output[0], Decraft );
	}
	
	return Decraft;
}

void AddManualDecraftList()
{
	ItemPartArray@ Decraft;

	@Decraft = ItemPartArray();
	Decraft.Add( PID_PLANT_FIBERS, 5 );
	Decraft.Tools = TOOL_KNIFE;
	DecraftList.set( "" + PID_PLANT_CORN_DEAD, Decraft );
	DecraftList.set( "" + PID_PLANT_FIVELEAF_DEAD, Decraft );

	@Decraft = ItemPartArray();
	Decraft.Add( PID_PLANT_FIBERS, 1 );
	Decraft.Tools = TOOL_KNIFE;
	DecraftList.set( "" + PID_PLANT_RADCVET_DEAD, Decraft );

	@Decraft = ItemPartArray();
	Decraft.Add( PID_BARREL_GENERIC, 1 );
	Decraft.Add( PID_SAND, 3 );
	DecraftList.set( "" + PID_PLANTBARREL_EMPTY, Decraft );

	@Decraft = ItemPartArray();
	Decraft.Add(PID_BARREL_GENERIC, 1);
	DecraftList.set( "" + PID_SHIT_BAREL_FULL, Decraft );
	DecraftList.set( "" + PID_BAREL_EMPTY, Decraft );

	@Decraft = ItemPartArray();
	Decraft.Add( PID_CLOTH, 5 );
	Decraft.Tools = TOOL_KNIFE;
	DecraftList.set( "" + PID_TOWEL, Decraft );
	DecraftList.set( "" + PID_OLD_TOWEL, Decraft );

	@Decraft = ItemPartArray();
	Decraft.Add( PID_CLOTH, 1 );
	Decraft.Tools = TOOL_KNIFE;
	DecraftList.set( "" + PID_MOUSE_SKIN, Decraft );
	DecraftList.set( "" + PID_PART_OF_ROPE, Decraft );
	
	@Decraft = ItemPartArray();
	Decraft.Add( PID_CLOTH, 2 );
	Decraft.Tools = TOOL_KNIFE;
	DecraftList.set( "" + PID_WOLF_FUR, Decraft );
	DecraftList.set( "" + PID_GECKO_PELT, Decraft );

	@Decraft = ItemPartArray();
	Decraft.Add( PID_LEATHER, 2 );
	Decraft.Tools = TOOL_KNIFE;
	DecraftList.set( "" + PID_PELT3, Decraft );
	DecraftList.set( "" + PID_GOLDEN_GECKO_PELT, Decraft );

	@Decraft = ItemPartArray();
	Decraft.Add(PID_LEATHER, 3);
	Decraft.Tools = TOOL_KNIFE;
	DecraftList.set( "" + PID_FIRE_GECKO_PELT, Decraft );

	@Decraft = ItemPartArray();
	Decraft.Add(PID_LEATHER, 5);
	Decraft.Tools = TOOL_KNIFE;
	DecraftList.set( "" + PID_BRAHMIN_SKIN, Decraft );
	DecraftList.set( "" + PID_DEATHCLAW_HIDE, Decraft );

	@Decraft = ItemPartArray();
	Decraft.Add( PID_HUNTING_RIFLE, 1 );
	Decraft.Add( PID_OPTICAL_SIGHT, 1 );
	Decraft.Add( PID_IRON_PARTS, 2 );
	Decraft.Tools = TOOL_MULTITOOL | TOOL_SCREWDRIVE;
	DecraftList.set( "" + PID_SCOPED_HUNTING_RIFLE, Decraft );

	@Decraft = ItemPartArray();
	Decraft.Add( PID_10MM_PISTOL, 1 );
	Decraft.Add( PID_FLASHLIGHT_OFF, 1 );
	Decraft.Add( PID_IRON_PARTS, 2 );
	Decraft.Tools = TOOL_MULTITOOL | TOOL_SCREWDRIVE;
	DecraftList.set( "" + PID_10MM_PISTOL_FLASHLIGHT, Decraft );

	@Decraft = ItemPartArray();
	Decraft.Add( PID_ASSAULT_RIFLE, 1 );
	Decraft.Add( PID_OPTICAL_SIGHT, 1 );
	Decraft.Add( PID_IRON_PARTS, 2 );
	Decraft.Tools = TOOL_MULTITOOL | TOOL_SCREWDRIVE;
	DecraftList.set( "" + PID_ASSAULT_RIFLE_OPT, Decraft );

	@Decraft = ItemPartArray();
	Decraft.Add( PID_M4, 1 );
	Decraft.Add( PID_OPTICAL_SIGHT, 1 );
	Decraft.Add( PID_IRON_PARTS, 2 );
	Decraft.Tools = TOOL_MULTITOOL | TOOL_SCREWDRIVE;
	DecraftList.set( "" + PID_M4_OPT, Decraft );

	@Decraft = ItemPartArray();
	Decraft.Add( PID_ASSAULT_RIFLE_EXT_MAG, 1 );
	Decraft.Add( PID_OPTICAL_SIGHT, 1 );
	Decraft.Add( PID_BRACING, 4 );
	Decraft.Tools = TOOL_MULTITOOL | TOOL_SCREWDRIVE;
	DecraftList.set( "" + PID_ASSAULT_RIFLE_EXT_MAG_OPT, Decraft );

	@Decraft = ItemPartArray();
	Decraft.Add( PID_ASSAULT_RIFLE_EXT_XXL_MAG, 1 );
	Decraft.Add( PID_OPTICAL_SIGHT, 1 );
	Decraft.Add( PID_IRON_PARTS, 2 );
	Decraft.Tools = TOOL_MULTITOOL | TOOL_SCREWDRIVE;
	DecraftList.set( "" + PID_ASSAULT_RIFLE_EXT_XXL_MAG_OPT, Decraft );
}

void InitDecraft()
{
	AddManualDecraftList();

	Log("Generating decrafts...");
	for(uint i = 0, len = GetMaxCraftsCount(); i < len; i++)
	{
		CraftRecipe@ currRecipe = GetCraftByPID(i);
		if(!valid(currRecipe) || !CanDecraft(currRecipe)) continue;
		ProcessDecraftRecipe(currRecipe);
	}
	Log("Generating decrafts complete.");
}

ItemPartArray@ GetItemParts(uint16 pid)
{
	ItemPartArray@ Decraft;
	if( DecraftList.get(""+pid, @Decraft) )
	{
		return Decraft;
	}
	return null;
}

uint FindFirstTool(uint toolFlag)
{
	for( int i = 0; i < 32; i ++ )
	{			
		if( ISBIT( toolFlag, i ) ) return i;
	}
	
	return 255;
}

bool HasTool(Critter& cr, uint toolFlag)
{
	if(!valid(cr)) return false;
	if(toolFlag == 255) return true;

	Item@[] crInv;
	cr.GetItems( -1, crInv );

	for(uint i = 0, len = crInv.length(); i < len; i++)
	{
		Item@ currItem = crInv[i];
		if(!valid(currItem)) continue;
		
		ProtoItem@ currProto = GetProtoItem(currItem.GetProtoId());
		if(!valid(currProto)) continue;
		
		if( FLAG(1 << toolFlag, currProto.Tool_Perk) ) return true;
	}
	
	return false;
}

void ShowDecraftInfo( Critter& cr, Item& item )
{
	if( !valid( item ) || !valid (cr ) )
	{
		return;
	}

	ItemPartArray@ Decraft = GetItemParts( item.GetProtoId() );
	if( !valid( Decraft ) )
	{
		return;
	}

	ShowDecraftInfo( cr, Decraft );
}

void ShowDecraftInfo( Critter& cr, ItemPartArray@ Decraft )
{
	if(!valid(cr) || !valid(Decraft)) return;

	cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, ( FLAG(Decraft.Tools, TOOL_MULTITOOL) ? STR_NEED_HOLD_TOOL_AND_MULTITOOL : STR_NEED_HOLD_TOOL ) );
	for( int i = 0; i < 32; i ++ )
	{
		if( i == 2 ) // TOOL_MULTITOOL
		{
			continue;
		}
		if( ISBIT( Decraft.Tools, i ) )
		{
			cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_NEED_HOLD_TOOL_PID, "$proto@msg item " + ( ProtoToolPids[i] * 100 ) + "@" );
		}
	}

	cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_GAINED_PARTS );
	for( uint i = 0, len = Decraft.length(); i < len; i++ )
	{
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_GAINED_PARTS_PID, "$proto@msg item " + ( Decraft[i].Pid * 100 ) + "@$amount" + Decraft[i].Count );
	}
}

bool TryDisassembleItem( Critter& cr, Item& item )
{
	if( !valid( item ) || !valid(cr) )
	{
		return false;
	}
	
	if( IsTired( cr ) )
	{
		return false;
	}
	
	if( HandsTied( cr ) )
	{
		return false;
	}
	
	if( cr.IsInjured() )
	{
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_INJURED_TO_DECRAFT );
		return false;
	}

	uint16 pid = item.GetProtoId();
	ItemPartArray@ Decraft = GetItemParts(pid);
	
	if( !valid(Decraft) )
	{
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_NO_IDEA );
		return false;
	}

	uint firstTool = FindFirstTool(Decraft.Tools);
	bool isMultitool = FLAG(Decraft.Tools, TOOL_MULTITOOL) ? HasTool(cr, 2) : true;

	if ( !HasTool(cr, firstTool) || !isMultitool )
	{
		ShowDecraftInfo(cr, Decraft);
		return true;
	}
	
	if( item.GetType() == ITEM_TYPE_WEAPON && item.Proto.Weapon_MaxAmmoCount > 0 )
	{
		uint ammoCount = item.AmmoCount;
		
		if( ammoCount > 0 )
		{
			Item@ ammo = cr.AddItem( item.AmmoPid, ammoCount );
			item.AmmoCount = 0;
			item.Update();
			
			cr.SayMsg( SAY_NETMSG, TEXTMSG_GAME, STR_AMMO_EJECTED, "$proto@msg item " + ( ammo.Proto.ProtoId * 100 + 2 ) + "@$count" + ammoCount );
		}
	}

	start_decraft( cr, item, Decraft );
    return true;
}

bool ltp_decraft_inited = false;
void ltp_decraft_init()
{
	LTPREG( LTP_DECRAFT, process_decraft )
	ltp_decraft_inited = true;
}

bool start_decraft( Critter& cr, Item& item, ItemPartArray@ Decraft )
{
	if( !ltp_decraft_inited )
	{
		ltp_decraft_init();
	}

	uint amount = Decraft.length();
	
	uint hit_pause = ACTION_PAUSE_BASE - (cr.Stat[ST_AGILITY] * ACTION_PAUSE_BONUS_RATE );
	cr.ParamBase[ ST_LTP_SEQUENCE_WAIT ] = CLAMP( hit_pause, ACTION_PAUSE_MIN, ACTION_PAUSE_MAX );
	int decraft_rate = ( cr.Skill[SK_REPAIR] ) + ( cr.Stat[ST_AGILITY] * 10 ) + ( cr.Stat[ST_INTELLECT] * 10 ) + OBJECT_DPA_BASE; 
	cr.ParamBase[ ST_LTP_SEQUENCE ] = int( ceil( float( ( amount * 200 ) / ( CLAMP( decraft_rate, OBJECT_DPA_MIN, OBJECT_DPA_MAX ) ) ) ) );
	
	uint[] values = { cr.Id };
	
	CreateTimeEvent( AFTER( REAL_MS( 700 ) ), "repair@e_RepairSound", values, true);
	CreateTimeEvent( AFTER( REAL_MS( 1000 ) ), "gathering@e_TiredenessTimeout", values, true);	

	_CritAnimateUse( cr );

	StartProcess( cr, LTP_DECRAFT, 0, amount, item.Id, cr.ParamBase[ ST_LTP_SEQUENCE_WAIT ] );
	return true;
}

uint process_decraft( Critter@ cr, int& param0, int& param1, int& param2 )
{
	LTPROCESS( cr, param0, LTP_DECRAFT )
  	
	Item@ item = GetItem( param2 );
	
	if( valid( item ) )
    {
		param0++;
		
		if( param0 > cr.ParamBase[ ST_LTP_SEQUENCE ] )
		{
			cr.SayMsg( SAY_EMOTE_ON_HEAD, TEXTMSG_TEXT, STR_EMOTE_FINISH_DECRAFT );
			DecraftResult( cr, item );
			return 0;
		}
		
		uint[] values = { cr.Id };
		CreateTimeEvent( AFTER( REAL_MS( 700 ) ), "repair@e_RepairSound", values, true);
		CreateTimeEvent( AFTER( REAL_MS( 1000 ) ), "gathering@e_TiredenessTimeout", values, true);

		_CritAnimateUse( cr );
		
		return cr.ParamBase[ ST_LTP_SEQUENCE_WAIT ];
	}
	
	return 0;
}

bool DecraftResult( Critter& cr, Item& item ) 
{
	if(!valid(item)) return false;
	uint16 pid = item.GetProtoId();
	ItemPartArray@ Decraft = GetItemParts(pid);
	
	cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_GAINED_DECRAFT, "$proto@msg item " + ( item.Proto.ProtoId * 100 ) + "@" );
    for( uint8 i = 0; i < Decraft.length(); i++ )
	{
		ItemPart@ currOutput = Decraft[i];
		if(!valid(currOutput)) continue;

		uint count = currOutput.Count;
		int maxSkill = Decraft.Tier * DECRAFT_SK_MUL + DECRAFT_SK_MOD;    
		int successRoll = Random( 1, maxSkill );

		if(cr.Param[SK_SCIENCE] < successRoll)
		{
			count /= 2;
		}

		if( GetDeteriorationProcent( item ) > 40 || (10 - item.BrokenCount) < 4)
		{
			count /= 2;
		}

		count = count < 1 ? 1 : count;

		Item@ gainedItem = cr.AddItem( currOutput.Pid, count );
		if( valid( gainedItem ) )
		{
			if( gainedItem.Proto.Weapon_MaxAmmoCount > 0 )
			{
				gainedItem.AmmoCount = 0;
			}
			
			if( item.IsDeteriorable() && gainedItem.IsDeteriorable() )
			{
				SetDeterioration( gainedItem, GetDeteriorationProcent( item ) );
			}
			
			gainedItem.Update();
		}
		
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_GAINED_DECRAFT_PID, "$proto@msg item " + ( gainedItem.Proto.ProtoId * 100 ) + "@$amount" + currOutput.Count );
	}
	
	if ( item.GetCount() > 1 )
	{
		item.SetCount(item.GetCount() - 1);
		StartMenuAskAction( cr, item );
	}
	else
	{
		DeleteItem( item );
	}
	
	return true;
}

//~run decraft _GetDecraftOutput pid 0 0
void _GetDecraftOutput( Critter& player, int param0, int param1, int param2 )
{
	if(param0 <= 0) return;
	if( !DecraftList.exists( ""+param0 ) ) return;

	ItemPartArray@ Decraft;
	DecraftList.get(""+param0, @Decraft);

	if(!valid(Decraft)) return;

	player.Say(SAY_NETMSG, "Output for pid " + param0 + " is:");
	for(uint i = 0, len = Decraft.length(); i < len; i++)
	{
		ItemPart@ currOut = Decraft[i];
		if(!valid(currOut)) continue;

		player.Say(SAY_NETMSG, "PID " + GetConstantName(CONSTANTS_ITEM, currOut.Pid) + ", Count " + currOut.Count);
	}
}

#endif // DECRAFT