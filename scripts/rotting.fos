#ifndef ROTTING
#define ROTTING

#include "_utils.fos"
#include "rotting_h.fos"

import bool CheckGard( Map& map, uint16 hexX, uint16 hexY ) from "qmap_farm";

//~run rotting go 0 0 0
void go( Critter& player, int repeat, int, int )
{
	uint count = ( repeat < 0 ? 256 : repeat );
	
	Map@ map = player.GetMap();
	if( !valid( map ) )
	{
		return;
	}
	
	for( uint i = 0; i <= count; i++ )
	{
		RottenFood( map );
	}
}

//~run rotting test 0 0 0
void test( Critter& player, int, int, int )
{
	RottenFood( player );
}

//~run rotting setRottenCycle value 0 0
void setRottenCycle( Critter& player, int value, int, int )
{
	GameVar@ cycle = GetGlobalVar( GVAR_rotten_cycle );
	if(valid(cycle)) 
	{
		if( value == 0 )
		{
			if( cycle.GetValue() < 999 )
			{
				cycle.opAddAssign( 1 );
			}
			else
			{
				cycle.opAssign( 0 );
			}
		}
		else
		{
			cycle.opAssign( value );
		}
		player.Say( SAY_NETMSG, "Rotten cycle = " + cycle.GetValue() + "." );
	}
}

void RottenFood( Critter& cr )
{
	if( isGM( cr ) && cr.Param[ QST_GAMEMODE ] != GAME_TEST )
	{
		return;
	}
	
	Map@ map = cr.GetMap();
	if( !valid( map ) )
	{
		return;
	}
	
	GameVar@ cycle = GetGlobalVar( GVAR_rotten_cycle );
	if( !valid( cycle ) )
	{
		return;
	}
	
	int delta = cycle.GetValue() - cr.ParamBase[ CR_LAST_ROTTEN_CYCLE ];
	if( delta > 50 )
	{
		delta = 50;
	}
	
	cr.ParamBase[ CR_LAST_ROTTEN_CYCLE ] = cycle.GetValue();
	
	Item@[] containers, stuff, food;
	food.resize(0);
	cr.GetItems( -1, stuff );
	
	for( uint i = 0; i < stuff.length(); i++ )
	{
		uint pid = stuff[i].GetProtoId();
		if( stuff[i].GetType() == ITEM_TYPE_FOOD || special_pids.find( pid ) != -1  )
		{
			food.insertLast( stuff[i] );
		}
		
		if( stuff[i].GetType() == ITEM_TYPE_CONTAINER )
		{
			if( proper_container_pids.find( pid ) != -1 )
			{
				continue;
			}
			
			bool isShitPit = shit_pit_pids.find( pid ) != -1;
			
			Item@[] substuff;
			stuff[i].GetItems( uint( -1 ), substuff );
			for( uint j = 0; j < substuff.length(); j++ )
			{
				if( substuff[j].GetType() == ITEM_TYPE_FOOD || special_pids.find( substuff[j].GetProtoId() ) != -1 )
				{
					if( isShitPit && ( pid == PID_SHIT_SMALL || pid == PID_SHIT_BIG ) )
					{
						continue;
					}
					else
					{
						food.insertLast( substuff[j] );
					}
				}
			}
		}
	}

	bool badSmell = false;
	Item@ target;
	
	for( int d = 0; d < delta; d++ )
	{
		for( uint i = 0; i < food.length(); i++ ) 
		{
			if( !valid( food ) )
			{
				continue;
			}
			
			@target = food[i];
			for( uint j = 0; j < food_pids_conversion.length(); j++ )
			{
				if( Random( 1, 100 ) < int( 2 * target.GetCount() ) && food_pids_conversion[j][0] == target.GetProtoId() )
				{
					int count = target.GetCount();
					int jdelta = 1;
					if( count > 50 )
					{
						jdelta = count / 50;
					}
					
					uint pid = food_pids_conversion[j][1];
					if( pid != 0 )
					{
						if( target.Accessory == ACCESSORY_CONTAINER )
						{
							Item@ container = GetItem( target.ContainerId );
							if( valid( container ) )
							{
								container.AddItem( pid, jdelta, 0 );
								if( pid == PID_SHIT_SMALL || pid == PID_SHIT_BIG )
								{
									map.SetTextMsg( cr.HexX, cr.HexY, COLOR_LGRAY, TEXTMSG_TEXT, STR_MAPMSG_BAD_SMELL );
								}
							}
						}
						
						if( target.Accessory == ACCESSORY_CRITTER )
						{
							cr.AddItem( pid, jdelta );
							if( pid == PID_SHIT_SMALL || pid == PID_SHIT_BIG )
							{
								badSmell = true;
							}
						}
					}
				
					if( count - jdelta > 0 )
					{
						target.SetCount( count - jdelta );
					}
					else 
					{
						DeleteItem( target );
						break;
					}
				}
			}
		}
	}
	
	cr.ParamBase[ CR_DIRTINESS ] += Random( 0, 3 + ( cr.ParamBase[ST_BLEED] / 2 ) );
	
	if( badSmell )
	{
		cr.SayMsg( SAY_EMOTE, TEXTMSG_TEXT, STR_EMOTE_BAD_SMELL );
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_FOOD_ROTTED );
		cr.ParamBase[ CR_DIRTINESS ] += Random( 0, 5 );
	}
}

void RottenFood( Map& map )
{
	Item@[] containers, stuff, food;
	food.resize(0);
	
	for( uint i = 0; i < special_pids.length(); i++ )
	{
		map.GetItems( special_pids[i], food );
	}
	
    map.GetItemsByType( ITEM_TYPE_CONTAINER, containers );
    if( valid( containers ) )
    {
        for( uint i = 0; i < containers.length(); i++ )
        {
			if( ( proper_container_pids.find( containers[i].GetProtoId() ) != -1 && !FLAG( containers[i].LockerCondition, LOCKER_ISOPEN ) ) 
				|| containers[i].GetProtoId() == PID_VENDING_FRIDGE1 || containers[i].GetProtoId() == PID_VENDING_FRIDGE2 ) //special case for vendings since they are not changable
			{
				continue;
			}
			
			bool isShitPit = shit_pit_pids.find( containers[i].GetProtoId() ) != -1;

			stuff.resize( 0 );
			containers[ i ].GetItems( uint( -1 ), stuff );
			for( uint j = 0; j < stuff.length(); j++ )
			{
				uint pid = stuff[j].GetProtoId();
				if( stuff[j].GetType() == ITEM_TYPE_FOOD || special_pids.find( pid ) != -1 )
				{
					if( isShitPit && ( pid == PID_SHIT_SMALL || pid == PID_SHIT_BIG || pid == PID_SHIT_HUGE ) )
					{
						continue;
					}
					else
					{
						food.insertLast( stuff[j] );
					}
				}
			}
        }
    }
	
	Item@ target;
    map.GetItemsByType( ITEM_TYPE_FOOD, food );
	
	uint count;
	for( uint i = 0; i < food.length(); i++ ) 
	{
		if( !valid( food[i]) )
		{
			continue;
		}
		@target = food[i];
		for( uint j = 0; j < food_pids_conversion.length(); j++ )
		{
			if( Random( 1, 100 ) < int( 10 * target.GetCount() ) && food_pids_conversion[j][0] == target.GetProtoId() )
			{
				if( plants.find( target.GetProtoId() ) != -1 && CheckGard( map, target.HexX, target.HexY ) )
				{
					continue;
				}
				
				count = target.GetCount();
				uint delta = 1;
				if( count > 10 )
				{
					delta = count / 10;
				}
				
				uint pid = food_pids_conversion[j][1];
				if( pid != 0 )
				{
					if( target.Accessory == ACCESSORY_CONTAINER )
					{
						Item@ container = GetItem( target.ContainerId );
						if( valid( container ) )
						{
							container.AddItem( pid, delta, 0 );
							map.SetTextMsg( container.HexX, container.HexY, COLOR_LGRAY, TEXTMSG_TEXT, STR_MAPMSG_BAD_SMELL );
						}
					}
					
					if( target.Accessory == ACCESSORY_HEX )
					{
						map.AddItem( target.HexX, target.HexY, pid, delta );
						map.SetTextMsg( target.HexX, target.HexY, COLOR_LGRAY, TEXTMSG_TEXT, STR_MAPMSG_BAD_SMELL );
					}
				}
			
				if( count - delta > 0 )
				{
					target.SetCount( count - delta );
				}
				else 
				{
					DeleteItem( target );
					break;
				}
			}
		}
	}
}

void RottenCrittersFoodOnMap( Map& map )
{
	Critter@[] critters;
	Critter@ crit = null;
	if( map.GetCritters( 0, FIND_ONLY_PLAYERS, critters ) > 0 )
	{
		for( uint j = 0, len = critters.length(); j < len; j++ )
		{
			@ crit = critters[j];
			if( valid( crit ) )
			{
				RottenFood( crit );
			}
		}
	}
	
	critters.resize(0);
}

uint cte_rotten (Critter& cr, int identifier, uint& rate )
{
	if( cr.StatBase[ST_BODY_TYPE] == BT_ROBOT )
	{
		return 0;
	}
	
	rate++;
	
	bool isPlant = cr.StatBase[ST_BODY_TYPE] == BT_PLANT;

	cr.StatBase[ ST_CURRENT_HP ]--;
	
	if( cr.Stat[ ST_CURRENT_HP ] < __DeadHitPoints - cr.Stat[ ST_MAX_LIFE ]  )
	{
		if( !isPlant )
		{
			Item@ bones;
			Map@ map = cr.GetMap();
			
			if( valid(map) )
			{
				@bones = map.AddItem( cr.HexX, cr.HexY, Random( PID_OLD_BLOOD_BONES_1, PID_OLD_BLOOD_BONES_3 ), 1 );
			}
			
			if( valid( bones) )
			{
				uint[] data = { bones.Id, rate };
				CreateTimeEvent( __FullSecond + REAL_SECOND( 60 ), "e_rotten_bones", data, true );
			}
		}
		DeleteNpc( cr );
		return 0;
		
	}
	
	/*if( cr.Stat[ ST_CURRENT_HP ] < __DeadHitPoints && !isPlant )
	{
		Map@ map = cr.GetMap();
		if( valid(map) )
		{
			uint msgStr = rate < 10 ? STR_MAPMSG_SMELL_BLOOD : STR_MAPMSG_SMELL_DECAY;
			map.SetTextMsg( cr.HexX, cr.HexY, COLOR_SAND, TEXTMSG_TEXT, msgStr );
		}
	}*/
	return REAL_SECOND( cr.Stat[ ST_MAX_LIFE ] );
}

uint e_rotten_bones( uint[] @ values )
{
	if( !valid(values) || values.length() != 2 || values[0] == 0 )
	{
		return 0;
	}
	
	Item@ bones = GetItem( values[0] );
	if( !valid(bones) )
	{
		return 0;
	}
	
	values[1]++;
	
	bones.Val0 += Random( 1, 3 );

	if( bones.Val0 > 10 )
	{
		DeleteItem( bones );
	}
	
	/*Map@ map = GetMap( bones.MapId );
	if( valid(map) && bones.Accessory == ACCESSORY_HEX )
	{
		uint msgStr = values[1] < 10 ? STR_MAPMSG_SMELL_BLOOD : STR_MAPMSG_SMELL_DECAY;
		map.SetTextMsg( bones.HexX, bones.HexY, COLOR_SAND, TEXTMSG_TEXT, msgStr );
	}*/
	return REAL_MINUTE( Random( 5, 15 ) );
}

#endif //ROTTING