#ifndef CHOPPING
#define CHOPPING

#include "_utils.fos"
#include "_ltp.fos"
#include "gathering_h.fos"
#include "critter_skills_h.fos"
#include "combat_h.fos"

bool ltp_chopping_inited = false;
void ltp_chopping_init()
{
	LTPREG( LTP_CHOPPING, process_chopping )
	ltp_chopping_inited = true;
}

bool PrepareChopping( Critter& cr, Item@ tool, Item@ tree ) 
{
	Map@ map = cr.GetMap();
    if( !valid( map ) ) 
    {
    	return false;
    }
	
	if( !valid( tool ) || !valid( tree ) )
	{
		return false;
	}
	
   	uint toolPid = tool.GetProtoId();
	if( ChoppingTools.find( toolPid ) == -1 )
	{
		return false;
	}
	
	if( tool.CritSlot != SLOT_HAND1 )
	{
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_EQUIP_TOOL );
		return false;
	}
	
	if( tree.Proto.Item_Subtype != ITEM_SUBTYPE_TREE && tree.Proto.Item_Subtype != ITEM_SUBTYPE_TREE_SLUMP )
	{
		return false;
	}
	
	if( tree.Proto.Item_Subtype != ITEM_SUBTYPE_TREE_SLUMP )
	{
		if( toolPid == PID_SHOVEL || toolPid == PID_SPADE )
		{
			cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_WRONG_INSTRUMENT );
			return true;
		}
	}
	
	if( IsTired( cr ) )
	{
		return false;
	}
	
	if( cr.IsDmgTwoArm() || cr.IsDmgTwoLeg() )
	{ 
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_INJURED_CANT_WORK );
		return false; 
	}
	
	start_chopping( cr, tool, tree );
	return true;
}

bool start_chopping( Critter& cr, Item& tool, Item& tree )
{
	if(!ltp_chopping_inited)
	{
		ltp_chopping_init();
	}
	
	if( valid( tool ) && valid( tree ) )
	{	
		uint hit_pause = ACTION_PAUSE_BASE - (cr.Stat[ST_AGILITY] * ACTION_PAUSE_BONUS_RATE );
		uint action_pause = CLAMP( hit_pause, ACTION_PAUSE_MIN, ACTION_PAUSE_MAX );
		StartProcess( cr, LTP_CHOPPING, 0, tool.Id, tree.Id, action_pause );
		CreateTimeEvent( AFTER( REAL_MS( 700 ) ), "e_ChoppingSound", cr.Id, false);
		CreateTimeEvent( AFTER( REAL_MS( 1000 ) ), "gathering@e_TiredenessTimeout", cr.Id, true);
		return true;
	}
	return false;
}

uint process_chopping( Critter@ cr, int& param0, int& param1, int& param2 )
{
    LTPROCESS( cr, param0, LTP_CHOPPING )

	uint hit_pause = ACTION_PAUSE_BASE - (cr.Stat[ST_AGILITY] * ACTION_PAUSE_BONUS_RATE );
	uint action_pause = CLAMP( hit_pause, ACTION_PAUSE_MIN, ACTION_PAUSE_MAX );
    Item@ tool = GetItem( param1 );
  	Item@ tree = GetItem( param2 );
  	if( valid( tool ) && valid( tree ) )
    {
      	param0++;
  		uint object_dpa = ( cr.Skill[SK_CHOPPING] ) + ( cr.Stat[ST_STRENGTH] * 10 ) + OBJECT_DPA_BASE; 
		int sequence_length = int( ceil( float( OBJECT_HP_BASE / ( CLAMP( object_dpa, OBJECT_DPA_MIN, OBJECT_DPA_MAX ) ) ) ) );
		if( param0 > sequence_length )
		{
			if( tree.Proto.Item_Subtype == ITEM_SUBTYPE_TREE )
			{
				if( tree.RESOURCE_CAPACITY <= OBJECT_HP_BASE )
				{	
					CutTree( cr, tree );
				}
				else
				{
					if( tree.RESOURCE_CAPACITY > ( OBJECT_HP_BASE * 2 ) )
					{
						ChangeFrame( tree, 1 );
					}
					
					if( tree.RESOURCE_CAPACITY <= ( OBJECT_HP_BASE * 2 ) )
					{
						ChangeFrame( tree, 2 );
					}
				}
			}
			else if( tree.Proto.Item_Subtype == ITEM_SUBTYPE_TREE_SLUMP )
			{
				if( tool.GetProtoId() != PID_SHOVEL && tool.GetProtoId() != PID_SPADE )
				{
					cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_USE_SHOWEL );
					return 0;
				}
				if( tree.RESOURCE_CAPACITY == 0 )
				{
					EraseTimeEvent( tree.EVENT );
					tree.EVENT = 0;
					DeleteItem( tree );
				}
			}
			else
			{
				return 0;
			}
			
			Profit( cr, tool, tree );
			int skillNum = SK_CHOPPING;
			Accident( cr, skillNum );
        }
		else
		{
			_CritAnimateSwing( cr );
			
			CreateTimeEvent( AFTER( REAL_MS( 700 ) ), "e_ChoppingSound", cr.Id, false );
			CreateTimeEvent( AFTER( REAL_MS( 1000 ) ), "gathering@e_TiredenessTimeout", cr.Id, true );
			
			if( cr.IsInjured() )
			{
				uint damage = INJURED_HP_DAMAGE - cr.Stat[ ST_ENDURANCE ];
				InjureCritter( cr, damage, DAMAGE_UNCALLED, cr.Dir, cr.Id, HIT_LOCATION_NONE, false );
			}
			return action_pause;
		}
    }
	return 0;
}
//Action sound delay time event
uint e_ChoppingSound(uint[]@ values)
{
	Critter@cr = GetCritter(values[0]); 
	Map@ map = cr.GetMap();
	PlayGenericSound( map, cr.HexX, cr.HexY, _GetOneRandom( ChoppingSounds ), 30 );
	return 0;
}

//Cut tree pid change, condition to regrow
uint CutTree( Critter& cr, Item@ tree )
{
	tree.ORIGINAL_TREE_PID = tree.GetProtoId();
	uint[] values = { tree.Id };
	tree.TREE_REGROW_TIME = 30 * 4; //time mul
	ChangeFrame( tree, 0 );
	switch( tree.RESOURCE_DNA )
	{
		case( 1 ):
			tree.ChangeProto( PID_TREE_CS1 );
			break;
		case( 2 ):
			tree.ChangeProto( PID_TREE_CS2 );
			break;
		case( 3 ):
			tree.ChangeProto( PID_TREE_CS3 );
			break;
		case( 4 ):
			tree.ChangeProto( PID_TREE_CL1 );	
			break;
		case( 5 ):
			tree.ChangeProto( PID_TREE_CL2 );	
			break;
		case( 6 ):
			tree.ChangeProto( PID_TREE_CL3 );	
			break;
		case( 7 ):
			tree.ChangeProto( PID_TREE_CL4 );	
			break;
		case( 8 ):
			tree.ChangeProto( PID_TREE_CL5 );	
			break;
		case( 9 ):
			tree.ChangeProto( PID_TREE_GL2S );	
			break;
	}
	return 0;
}

//~run chopping _ForceTreeRegrow 0 0 0
void _ForceTreeRegrow( Critter& player, int param0, int param1, int param2 )
{
	Map@[] maps;
    for( int i = 0, iend = GetAllMaps( 0, maps ); i < iend; i++)
	{   
		Map@ map = maps[i];
		Item@[] chopped;
		
		map.GetItemsByType( ITEM_TYPE_PLANT, chopped );
		
		for( uint j = 0; j < chopped.length(); j++ )
		{
			if( chopped[j].Proto.Item_Subtype == ITEM_SUBTYPE_TREE_SLUMP )
			{
				chopped[j].TREE_REGROW_TIME = 0;
				
				if( chopped[j].ORIGINAL_TREE_PID <= 1 ) //error fixing
				{
					chopped[j].ORIGINAL_TREE_PID = PID_TREE_BS1;
				}					
				
				uint[] values = { chopped[j].Id };
				chopped[j].EVENT = CreateTimeEvent( AFTER( REAL_SECOND( 1 ) ), "e_TreeRegrow", values, true );
			}
		}
	}
}

// Regrowth time event
uint e_TreeRegrow( uint[]@ values )
{
	Item@ cutTree = GetItem( values[0] );
	if( valid( cutTree ) )    
	{
		ProtoItem@ treeProto = GetProtoItem( cutTree.ORIGINAL_TREE_PID );
		if( valid( treeProto ) && cutTree.ORIGINAL_TREE_PID != 0 )
		{
			uint16 cutTreePid = cutTree.GetProtoId();
			cutTree.ChangeProto( cutTree.ORIGINAL_TREE_PID );
			cutTree.RESOURCE_CAPACITY = cutTree.Proto.StartValue_1;
			ChangeFrame( cutTree, 0 );
			EraseTimeEvent( cutTree.EVENT );
			cutTree.EVENT = 0;
		}
	}
	return 0;
}

//Loot
bool Profit( Critter& cr, Item@ tool, Item@ tree )
{
	int amount = 0;
	uint toolMod = tool.GetProtoId();
	switch( toolMod )
	{	
		case( PID_REVKNIFE2 ):
		case( PID_MACHETE ):
			amount--;
			break;
		case( PID_AXE2 ):
			amount++;
			break;
	}
	
	uint lootRoll = ( Random( 0, 2 ) + ( cr.ParamBase[ SK_CHOPPING ] / 50 ) );
	amount+= lootRoll;
	if( amount < 1 )
	{
		 cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_NOTHING_USEFULL );
	}
	else
	{
		int skillNum = SK_CHOPPING;
		raiseSkill( cr, skillNum );
		
		cr.AddItem( PID_FIREWOOD, amount );
		
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_YOU_GATHERED, "$proto@msg item " + ( PID_FIREWOOD * 100 ) + "@$amount" + amount );
		
		cr.StatBase[ST_EXPERIENCE] += 25;
		
		tree.RESOURCE_CAPACITY = tree.RESOURCE_CAPACITY - OBJECT_HP_BASE;		
	}
	return true;
}

void CutTreeManager()
{    
    Map@[] maps;
    for( int i = 0, iend = GetAllMaps( 0, maps ); i < iend; i++)
	{   
		CheckCutTrees( maps[i] );
	}
}

void CheckCutTrees( Map& map )
{
	if( valid( map ) )
	{
		Item@[] mapPlants;
		Item@[] cutTrees;
		
		map.GetItemsByType( ITEM_TYPE_PLANT, mapPlants );
		
		for( uint i = 0; i < mapPlants.length(); i++ )
		{
			if( mapPlants[i].Proto.Item_Subtype == ITEM_SUBTYPE_TREE_SLUMP )
			{
				cutTrees.insertLast( mapPlants[i] );
			}
		}
		
		if( cutTrees.length() > 0 )
		{
			Item@ tree = null;
			for( uint j = 0; j < cutTrees.length(); j++ )
			{
				@tree = cutTrees[j];
				
				if( !valid( tree ) )
				{
					continue;
				}
				
				if( tree.ORIGINAL_TREE_PID == 0 )
				{
					continue;
				}
				
				if( tree.TREE_REGROW_TIME > 0 )
				{
					tree.TREE_REGROW_TIME --;
				}
				else
				{
					uint[] values = { tree.Id };
					uint period = Random( 1, 12 );
					tree.EVENT = CreateTimeEvent( AFTER( REAL_HOUR( period ) ), "e_TreeRegrow", values, true );
				}
			}
		}
	}
}

bool TryChopItem( Critter& cr, Item@ tool, Item@ target )
{
	Map@ map = cr.GetMap();
    if( !valid( map ) ) 
    {
    	return false;
    }
	
	if( !valid( tool ) || !valid( target ) )
	{
		return false;
	}
	
   	uint toolPid = tool.GetProtoId();
	if( ChoppingTools.find( toolPid ) == -1 )
	{
		return false;
	}
	
	if( tool.CritSlot != SLOT_HAND1 )
	{
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_EQUIP_TOOL );
		return false;
	}
	
	if( ( target.Proto.Type == ITEM_TYPE_CONTAINER || target.Proto.Type == ITEM_TYPE_DOOR ) && target.Proto.Item_Subtype != ITEM_SUBTYPE_FURNITURE )
	{
		return false;
	}
	
	if( target.Proto.Item_Subtype == ITEM_SUBTYPE_FIREWOOD || ( target.Proto.Material == MATERIAL_WOOD && ( target.Proto.Item_Subtype == ITEM_SUBTYPE_FURNITURE || ( target.Proto.Type == ITEM_TYPE_WEAPON && target.Proto.Item_Subtype == ITEM_SUBTYPE_WEAPON_MELEE_CLUB ) ) ) )
	{
		start_chop_item( cr, tool, target );
		return true;
	}

	return false;
}

//LTP_CHOP_ITEM
bool ltp_chop_item_inited = false;
void ltp_chop_item_init()
{
	LTPREG( LTP_CHOP_ITEM, process_chop_item )
	ltp_chop_item_inited = true;
}

bool start_chop_item( Critter& cr, Item& tool, Item& target )
{
	if( !ltp_chop_item_inited )
	{
		ltp_chop_item_init();
	}
	
	if( valid( tool ) && valid( target ) )
	{	
		uint hit_pause = ACTION_PAUSE_BASE - ( cr.Stat[ ST_STRENGTH ] * ACTION_PAUSE_BONUS_RATE );
		uint action_pause = CLAMP( hit_pause, ACTION_PAUSE_MIN, ACTION_PAUSE_MAX );
		uint[] values = { cr.Id };
		
		_CritAnimateSwing( cr );
		CreateTimeEvent( AFTER( REAL_MS( 700 ) ), "e_ChoppingSound", values, false);
		CreateTimeEvent( AFTER( REAL_MS( 1000 ) ), "gathering@e_TiredenessTimeout", cr.Id, true);	

		StartProcess( cr, LTP_CHOP_ITEM, 0, target.Id, 0, action_pause );
		return true;
	}
	return false;
}

uint process_chop_item( Critter@ cr, int& param0, int& param1, int& param2 )
{
    LTPROCESS( cr, param0, LTP_CHOP_ITEM )
	uint hit_pause = ACTION_PAUSE_BASE - ( cr.Stat[ ST_STRENGTH ] * ACTION_PAUSE_BONUS_RATE );
	uint action_pause = CLAMP( hit_pause, ACTION_PAUSE_MIN, ACTION_PAUSE_MAX );
    Item@ target = GetItem( param1 );
  	if( valid( target ) )
    {
		param0++;
		
		uint smithing_rate = ( cr.Stat[ ST_STRENGTH ] * ACTION_PAUSE_BONUS_RATE ) + OBJECT_DPA_BASE; 
		int sequence_length = int( ceil( float( 3000.0f / ( CLAMP( smithing_rate, OBJECT_DPA_MIN, OBJECT_DPA_MAX ) ) ) ) );
		
		if( param0 > sequence_length )
		{
			ChopItem( target );
			return 0;
		}
		
		uint[] values = { cr.Id };
		CreateTimeEvent( AFTER( REAL_MS( 700 ) ), "e_ChoppingSound", values, true);
		CreateTimeEvent( AFTER( REAL_MS( 1000 ) ), "gathering@e_TiredenessTimeout", values, true);

		_CritAnimateSwing( cr );
		
		return action_pause;
	}
	return 0;
}

void ChopItem( Item& target )
{
	Map@ map = GetMap( target.MapId );
	if( target.Proto.Type == ITEM_TYPE_CONTAINER )
	{
		DropContainerItems( map, target );
	}
	
	map.AddItem( target.HexX, target.HexY, PID_CHOPPED_FIREWOOD, 1 );
	_SubItem( target, 1 );
}

/* Code requires update if ever needed to be used again!!!
//ONE-TIME LIVE SERVER TREE UPGRADE
void _TreeUpgrade( Critter& player, int param0, int param1, int param2 )
{
	for( int ii = 0, jj = TreeList[1].length() ; ii < jj; ii++)
	{
		array<Item@> worldTrees = array<Item@>();
		int count = GetAllItems(TreeList[1][ii], @worldTrees);
		for( int treeIndex = 0; treeIndex < count; treeIndex++ )
		{
			Item@ tree = worldTrees[treeIndex];
			EraseTimeEvent( tree.EVENT );
			tree.EVENT = 0;
			tree.TREE_REGROW_TIME = Random( 4, 24 );
		}
	}
}*/
#endif //CHOPPING