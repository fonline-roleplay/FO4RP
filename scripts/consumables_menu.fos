#ifndef CONSUMABLES_MENU
#define CONSUMABLES_MENU

#include "serializator.fos"

void unsafe_menu( Critter& cr, int id, int, int, string@, int[]@ )// ~drugs
{
	Critter@ target = @cr;
	if( isGM( cr ) && id != 0 )
	{
		@target = GetCritter(id);
		if( !valid( target ) )
			@target = @cr;
	}
	StartConsumablesMenu( cr, target );
}

void StartConsumablesMenu( Critter& cr, Critter& target )//exported
{
    if( isGM( cr ) )
	{
		iDialogBox@ menu = OpenMenu( cr, "Drugs", MenuDrugs( target ) );
	}
	else
	{
		SendPlayerDrugsInfo( cr );
	}
}

class MenuDrugs: CenteredMenuHandler
{
	Critter@ target;
	ActiveDrug@[] drugs;
	bool delayed_refresh;
	
	MenuDrugs( Critter@ target )
	{
		@this.target = @target;
		drugs = enlistAllActiveDrugs(target);
	}

	bool MenuUpdate( Critter& cr, iDialogBox& menu ) 
	{		
		for( uint i = 0, len = drugs.length(); i < len; i++ )
		{
			ActiveDrug@ drug = drugs[i];
			
			string item_name;
			uint pid = drug.pid();
			if( IN_RANGE( pid, ITEM_SUBTYPE_DRUG_BEGIN, ITEM_SUBTYPE_DRUG_END ) )
			{
				uint j = pid - ITEM_SUBTYPE_DRUG_BEGIN;
				item_name = STR_INSERT_TEXT_LINE( STR_DRUG_TYPE_DOPE + j );
			}
			else
			{
				item_name = STR_INSERT_ITEM_LINE( pid * 100 );
			}
			
			string name = drug.event_id() + " [" + drug.phase() + "]" + " '" + item_name + "' ";
			string time = DeltaTime_HMS_short( uint( drug.time_left() / __TimeMultiplier ) );
			
			string lex = "$description" + name + time;
			if( menu.ButtonMsg( STR_ITEMNAME, lex ) )
			{
				if( isGM( cr ) )
				{
					skipStage( target, drug );
				}
				
				drugs = enlistAllActiveDrugs(target);
				delayed_refresh = true;				 
				
				return true;
			}
		}

		return true;
	}

    int getDescriptionFile()
    {
    	return TEXTMSG_TEXT;
    }

	int getDescriptionLine()
	{
		return STR_ACTIVE_DRUGS_DESCRIPTION;
	}
	
	string@ Description( Critter& cr ) 
	{
		string info = "$list " + YELLOW;
		for( uint i = ADDICTION_BEGIN; i <= ADDICTION_END; i++ )
			if( target.Addiction[i] != 0 )
				info += "\n" + STR_INSERT_PARAM_NAME(i);
			
		return info;
	}
	
	bool ShouldRedraw( Critter& cr )
	{
		if( delayed_refresh )
		{
			delayed_refresh = false;
			drugs = enlistAllActiveDrugs(target);
			
			return true;
		}
		
		return false;
    }
}

void SendPlayerDrugsInfo( Critter& player )
{
	if( !valid( player ) )
	{
		return;
	}
	
	Serializator data;

	ActiveDrug@[] drugs = enlistAllActiveDrugs(player);
	int16[] addicts;
	uint addicts_str = 0;
	for( uint i = ADDICTION_BEGIN; i <= ADDICTION_END; i++ )
	{
		if( player.Addiction[i] != 0 )
		{
			addicts.insertLast( i );
			addicts_str++;
		}
	}
	data.Set( addicts );
	
	uint drugs_str = drugs.length();
	for( uint i = 0; i < drugs_str; i++ )
	{
		ActiveDrug@ drug = drugs[i];

		uint8 phase = drug.phase();
		uint16 pid = drug.pid();
		uint32 time = drug.time_left() / __TimeMultiplier;
		
		data.Set( phase ).Set( pid ).Set( time );
	}

	player.RunClientScript( "client_screen_info@ShowDrugScreen", addicts_str, drugs_str, 0, null, data.Buff2Int() );
}

#endif