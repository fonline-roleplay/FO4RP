#include "_client_defines.fos"
#include "client_gui_h.fos"
#include "sprite.fos"
#include "_utils.fos"
#include "craft_recipes.fosh"

#define FIXBOY_PAGE_LEN ( 46 )

// Runtime-Logic, or Runtime-Changed variables
int listOffset = 0;
uint currCraftIndex = 0;
uint currLevel = 0;
int hoverIndex = -1;
dictionary craftLinks;
bool ScreenActive = false;
uint currFilterFlags = 0;
uint currProf = 0;
uint currTier = 0;
bool needProcessSearch = false;

uint[] DisplayIndices;

ScreenMain@ screenPtr = null;

int FindCraftForItem(uint16& pid)
{
    if(craftLinks.exists("" + pid))
    {
        int getVal = -1;
        craftLinks.get("" + pid, getVal);
        return getVal;
    }
    
    int retVal = -1;
    for(uint i = 0, len = GetMaxCraftsCount(); i < len; i++)
    {
        CraftRecipe@ currRecipe = GetCraftByPID(i);
        if(!valid(currRecipe)) continue;

        if(currRecipe.Output.find(pid) != -1)
        {
            retVal = i;
            break;
        }
    }
    craftLinks.set("" + pid, retVal);
    return retVal;
}

void ResetDisplayIndices()
{
    listOffset = 0;
    DisplayIndices.resize(0);
    dictionary craftsSorted;
    for(uint i = 0, len = GetMaxCraftsCount(); i < len; i++)
    {
        CraftRecipe@ currRecipe = GetCraftByPID(i);
        if(!valid(currRecipe)) continue;
        if(currFilterFlags != 0 && !FLAG(currRecipe.FilterFlags, currFilterFlags)) continue;
        if(currProf != 0 && !FLAG(1 << (currRecipe.Profession - PE_CRAFTSMAN), currProf)) continue;
        if(currTier != 0 && !FLAG(1 << currRecipe.Tier, currTier)) continue;
		string craftNameWithoutQuotes = currRecipe.CraftName;
		craftNameWithoutQuotes = ReplaceText(craftNameWithoutQuotes, "\"", "");
        craftsSorted.set(craftNameWithoutQuotes, i);
    }

    string@[] craftsSortedKeys;
    craftsSorted.keys(craftsSortedKeys);
    for(uint i = 0, len = craftsSortedKeys.length(); i < len; i++)
    {
        uint currIndex = uint(-1);
        craftsSorted.get(craftsSortedKeys[i], currIndex);
        if(currIndex == uint(-1)) continue;
        DisplayIndices.insertLast( currIndex );
    }
}

class ScreenMain : IGUIScreenCallbackShow, IGUIScreenCallbackHide
{
    IGUIScreenOpt@ screenOpt;
    FixboyRecipeBut@[] recipeBtns;
    FixboyScrollBut@[] scrBtns;
    FixboyCraftResBut@[] toolsBtns;
    FixboyCraftText@ craftLabel;
    FixboyCraftText@[] sectionLabels;
    FixboyCraftText@ skillsReq;
    FixboyCraftText@ toolsReq;
	FixboyCraftText@ counterScrn;

    void OnShow( int p0, int p1, int p2 )
    {
		ScreenActive = true;

        currFilterFlags = 0;
        currProf = 0;
        currTier = 0;
        ResetDisplayIndices();

        sectionLabels[0].SetText(GetMsgStr(TEXTMSG_GAME, STR_FIX_PARAMS), COLOR_GREEN, FT_BORDERED);
        sectionLabels[1].SetText(GetMsgStr(TEXTMSG_GAME, STR_FIX_TOOLS), COLOR_GREEN, FT_BORDERED);
        sectionLabels[2].SetText(GetMsgStr(TEXTMSG_GAME, STR_FIX_ITEMS), COLOR_GREEN, FT_BORDERED);
        ChangeLevel(0);
        UpdateElements(currLevel);
    }

    void OnHide( int p0, int p1, int p2 )
    {
		ScreenActive = false;
        hoverIndex = -1;
	}

    void UpdateElements(uint level)
    {
        if(level == 0)
        {
            for(uint i = listOffset, len = FIXBOY_PAGE_LEN + listOffset; i < len; i++)
            {
                FixboyRecipeBut@ currBtn = recipeBtns[i - listOffset];

                uint index = (i < DisplayIndices.length()) ? DisplayIndices[i] : GetMaxCraftsCount();
                if(index > GetMaxCraftsCount() - 1)
                {
                    currBtn.index = -1;
                    currBtn.SetName("");
                }
                else
                {
                    CraftRecipe@ currRecipe = GetCraftByPID(index);
                    if(!valid(currRecipe)) continue;

                    currBtn.index = index;
                    currBtn.SetName(currRecipe.CraftName);
                }
            }
            counterScrn.SetText("Page: " + ((listOffset / FIXBOY_PAGE_LEN) + 1) + "/" + ((DisplayIndices.length() / FIXBOY_PAGE_LEN) + 1), COLOR_GREEN, FT_BORDERED|FT_CENTERXY);
        }
    }

    void ChangeLevel(uint level)
    {
        UpdateElements(level);
        if(level == 0)
        {
            for(uint i = 0; i < FIXBOY_PAGE_LEN; i++)
            {
                recipeBtns[i].Options.SetVisible(true);
            }
            for(uint i = 0, len = toolsBtns.length(); i < len; i++)
            {
                toolsBtns[i].Options.SetVisible(false);
            }

            for(uint i = 0, len = sectionLabels.length(); i < len; i++)
            {
                sectionLabels[i].Options.SetVisible(false);
            }

            skillsReq.Options.SetVisible(false);
            toolsReq.Options.SetVisible(false);

            craftLabel.Options.SetVisible(false);
            currCraftIndex = 0;
        }
        else if(level == 1)
        {
            ActualizeCraftInfo();
            for(uint i = 0; i < FIXBOY_PAGE_LEN; i++)
            {
                recipeBtns[i].Options.SetVisible(false);
            }
            for(uint i = 0, len = toolsBtns.length(); i < len; i++)
            {
                toolsBtns[i].Options.SetVisible(true);
            }
            for(uint i = 0, len = sectionLabels.length(); i < len; i++)
            {
                sectionLabels[i].Options.SetVisible(true);
            }

            skillsReq.Options.SetVisible(true);
            toolsReq.Options.SetVisible(true);

            craftLabel.Options.SetVisible(true);
        }
        currLevel = level;
    }

    void ActualizeCraftInfo()
    {
        CraftRecipe@ currRecipe = GetCraftByPID(currCraftIndex);
        if( !valid( currRecipe ) )
		{
			return;
		}
		
		ItemPartArray@ currDecraft = GetItemParts(currRecipe.Output[0]);
		
		if( !valid(currDecraft) )
		{
			return;
		}
		
		for(uint i = 0, len = toolsBtns.length(); i < len; i++)
        {
            toolsBtns[i].index = -1;
            toolsBtns[i].Options.SetSprite(null);
		}
		
		string toolsText;		
		toolsReq.SetText(toolsText, COLOR_GREEN, FT_BORDERED);
        craftLabel.GenLabel();

        string skillsText;
        skillsText += "\t|0xADFF2F � ���� �� ���������:" + "|0x00C800 " + currDecraft.ProtoCost + "\n";
		skillsText += "\t|0xADFF2F � ���� �����������:" + "|0x00C800 " + currDecraft.CalcCost + "\n";
		skillsReq.SetText(skillsText, COLOR_GREEN, FT_BORDERED);
    }
}

class TestScreenButtonShow : IGUIElementCallbackMouseClick
{
    void OnMouseClick( int click )
    {
        ::ShowScreen( CLIENT_SCREEN_ECONOMY, 30, 3, 3 );
    }
}

class FixboyDoneBut : IGUIElementCallbackInit, IGUIElementCallbackMouseClick
{
    IGUIElementOpt@ Options;
    ScreenMain@ Instance;

    FixboyDoneBut( ScreenMain & instance )
    {
        @Instance = instance;
    }

    void OnInit()
    {
        @Options = GUI_GetElementOptions();
    }

    void OnMouseClick( int click )
    {
        if(currLevel == 0)
        {
            ::HideScreen( 0, 0, 0, 0 );
		    ChangeCursor( GetLastCursor() );
        }
        else if(currLevel == 1)
        {
            Instance.ChangeLevel(0);
        }
    }
}

class FixboyScrollBut : IGUIElementCallbackInit, IGUIElementCallbackMouseClick
{
    IGUIElementOpt@ Options;
    ScreenMain@ Instance;
    bool isUp = false;

    FixboyScrollBut( ScreenMain & instance, bool mode )
    {
        @Instance = instance;
        isUp = mode;
    }

    void OnInit()
    {
        @Options = GUI_GetElementOptions();
        Instance.scrBtns.insertLast(this);
    }

    void OnMouseClick( int click )
    {
        if(currLevel == 0)
        {
            if(isUp && listOffset - FIXBOY_PAGE_LEN < 0)
            {
                return;
            }
            else if(!isUp && listOffset + FIXBOY_PAGE_LEN >= int(DisplayIndices.length()))
            {
                return;
            }

            listOffset += (isUp ? -FIXBOY_PAGE_LEN : FIXBOY_PAGE_LEN);
        }
        Instance.UpdateElements(currLevel);
    }
}

class FixboyRecipeBut : IGUIElementCallbackInit, IGUIElementCallbackMouseClick, IGUIElementCallbackDraw
{
    IGUIElementOpt@ Options;
    ScreenMain@ Instance;
    int index = -1;
    string name = "";

    FixboyRecipeBut( ScreenMain & instance )
    {
        @Instance = instance;
    }

    void OnInit()
    {
        @Options = GUI_GetElementOptions();
        Instance.recipeBtns.insertLast(this);
    }

    void OnMouseClick( int click )
    {
        if(index != -1)
        {
            currCraftIndex = index;
            Instance.ChangeLevel(1);
        }
    }

    void OnDraw( int posX, int posY, int w, int h )
    {
        
        if( (__MouseX >= posX && __MouseX <= posX + w) &&
            (__MouseY >= posY && __MouseY <= posY + h) )
        {
            Options.Text( "" + name, FONT_FALLOUT, COLOR_LGRAY, 0, FT_BORDERED );
            hoverIndex = index;
        }
        else
        {
            Options.Text( "" + name, FONT_FALLOUT, COLOR_GRAY, 0, FT_BORDERED );
        }
    }

    void SetName( string name )
    {
		this.name = name;
    }
}

class FixboyCraftResBut : IGUIElementCallbackInit, IGUIElementCallbackMouseClick
{
    IGUIElementOpt@ Options;
    ScreenMain@ Instance;
    int index = -1;

    FixboyCraftResBut( ScreenMain & instance )
    {
        @Instance = instance;
    }

    void OnInit()
    {
        @Options = GUI_GetElementOptions();
    }

    void OnMouseClick( int click )
    {
        if(index != -1)
        {
            currCraftIndex = index;
			hoverIndex = index;
            Instance.ChangeLevel(1);
        }
    }

    void SetPicByPid(uint16 pid)
    {
        ProtoItem@ proto = GetProtoItem(pid);
        if(!valid(proto))
        {
            return;
        }

        int w = 40, h = 40;
        Options.GetSize(w, h);

        if( w == 0 ) w = 40;
        if( h == 0 ) h = 40;

        Sprite@ invPic = Sprite();
        invPic.Load(proto.PicInv, 0);
        Options.SetSprite(invPic);
        Options.SetSize(w, h);
    }
}

class FixboyCraftText : IGUIElementCallbackInit
{
    IGUIElementOpt@ Options;
    ScreenMain@ Instance;

    FixboyCraftText( ScreenMain & instance )
    {
        @Instance = instance;
    }

    void OnInit()
    {
        @Options = GUI_GetElementOptions();
    }

    void SetText(string text, uint color, int flags)
    {
        Options.Text("" + text, FONT_FALLOUT, color, 0, flags);
    }

    void GenLabel()
    {
        CraftRecipe@ currRecipe = GetCraftByPID(currCraftIndex);
        if(!valid(currRecipe)) return;

		Options.Text( currRecipe.CraftName, FONT_FALLOUT, COLOR_GREEN, 0, FT_BORDERED|FT_CENTERX );
    }
}

class FixboyHoverInfo : IGUIElementCallbackInit, IGUIElementCallbackDraw
{
    IGUIElementOpt@ Options;
    ScreenMain@ Instance;
    FixboyCraftResBut@ ItemIcon;
    int lastIndex = -1;

    FixboyHoverInfo( ScreenMain& instance )
    {
        @Instance = instance;
    }

    void OnInit()
    {
        @Options = GUI_GetElementOptions();
    }

    void OnDraw( int posX, int posY, int w, int h )
    {
        if(lastIndex == hoverIndex || hoverIndex == -1) return;

        Options.Text("" + GetHoverText(), FONT_FALLOUT, COLOR_GREEN, 0, 0);
        
        lastIndex = hoverIndex;
    }

    string GetHoverText()
    {
        CraftRecipe@ currRecipe = GetCraftByPID(hoverIndex);
        if(!valid(currRecipe)) return "";

        ProtoItem@ item = GetProtoItem(currRecipe.Output[0]);
        int itemCount = currRecipe.OutputCount[0];
        string itemName = checkNull( GetMsgStr( TEXTMSG_ITEM, item.ProtoId * 100 ) );
        int textSizeX = 0, textSizeY = 0;
        Options.GetSize(textSizeX, textSizeY);
        int textWidth = 0, textHeight = 0, lines = 0;
        GetTextInfo(itemName, textSizeX, textSizeY, FONT_FALLOUT, 0, textWidth, textHeight, lines);

        ItemIcon.Options.Position(580, 49 + (13 * lines));
        ItemIcon.SetPicByPid(currRecipe.Output[0]);

        string result = protoColor( item ) + itemName + COLOR_NETMSG;
		result += "\n-----------------------\n";

        result += "\n\n\n\n\n\n";

        if( item.Type == ITEM_TYPE_WEAPON )
        {
            // Strength Requirement
            result += "\n-----------------------\n";
            string str;
            str = GetMsgStr( TEXTMSG_GAME, STR_INV_STR_REQ );
            str = ReplaceText( str, "VALUE", item.Weapon_MinStrength  );
            result += str;
            
            //Weapon Skill
            int skillNum = _WeaponSkill( item, 0 );
            if( skillNum != 0 )
            {
                str = GetMsgStr( TEXTMSG_TEXT, STR_SKILL_USED );
                string value = GetMsgStr( TEXTMSG_GAME, STR_SKILL_NAME(skillNum) );
                result += "\n" + ReplaceText( str, "VALUE", value );
            }
            
            // Weapon Perk
            result += "\n" + GetMsgStr( TEXTMSG_TEXT, STR_WPN_PERK_TITLE );
            string perksInfo = "";
            str = GetMsgStr( TEXTMSG_TEXT, STR_WPN_PERK );
            int count = 0;
            for( int i = 0; i < 32; i ++ )
            {
                if( ISBIT( item.Weapon_Perk, i ) )
                {
                    string perkStr = GetMsgStr( TEXTMSG_TEXT, STR_WPN_PERK_NAME( i + 1 ) );
                    count ++;
                    if( perksInfo != "" )
                    {
                        perksInfo += GetMsgStr( TEXTMSG_TEXT, STR_WPN_PERK_SPACING );
                    }
                    
                    perksInfo += perkStr;
                }
            }
            
            if( perksInfo != "" )
            {
                result += ReplaceText( str, "VALUE", perksInfo );
            }
            else
            {
                result += GetMsgStr( TEXTMSG_TEXT, STR_WPN_PERK_NAME( 0 ) );
            }
            
            //  Custom object speed
            if( FLAG( item.Flags, ITEM_CAN_USE ) || FLAG( item.Flags, ITEM_CAN_USE_ON_SMTH ) || item.Type == ITEM_TYPE_WEAPON )
            {
                result += "\n";
                string str2 = GetMsgStr( TEXTMSG_GAME, STR_INV_WAIT_TIME );
                uint val = _GetProtoWindupTime( item );

                str2 = ReplaceText( str2, "VALUE", val );
                result += str2;
            }
            
            //  Custom AP cost
            if( FLAG(item.Flags, ITEM_CAN_USE) || FLAG(item.Flags, ITEM_CAN_USE_ON_SMTH ) || item.Type == ITEM_TYPE_WEAPON )
            {
                result += "\n";
                uint val = _WeaponApCost( item, 0);

                string str_a = GetMsgStr( TEXTMSG_GAME, STR_INV_AP_COST );
                str_a = ReplaceText( str_a, "VALUE", val );
                result += str_a;
            }
            
            // Ammo & Reload info
            if( item.Weapon_MaxAmmoCount > 0 )
            {
                // Custom Reload AP cost
                result += "\n";
                string str_b = GetMsgStr( TEXTMSG_GAME, STR_INV_RELOAD_COST );
                str_b = ReplaceText( str_b, "VALUE", item.Weapon_ReloadAp );
                
                // Ammo load
                string str2 = GetMsgStr( TEXTMSG_GAME, STR_INV_HAS_SHOTS );
                str2 = ReplaceText( str2, "VALUE",     item.Weapon_MaxAmmoCount );
                str2 = ReplaceText( str2, "MAX_VALUE", item.Weapon_MaxAmmoCount );
                
                // Caliber
                string str3 = GetMsgStr( TEXTMSG_GAME, STR_INV_CALIBER_MSG );
                str3 = ReplaceText( str3, "AMMO", GetMsgStr( TEXTMSG_GAME, STR_CALIBER( item.Weapon_Caliber ) ) );

                string modMin = GetMsgStr( TEXTMSG_GAME, STR_INV_MIN_DMG_MOD );
                int mod1 = item.Weapon_MinDmgMod - 100;
                modMin = ReplaceText( modMin, "MINMOD", mod1 );
                string modMax = GetMsgStr( TEXTMSG_GAME, STR_INV_MAX_DMG_MOD );
                int mod2 = item.Weapon_MaxDmgMod - 100;
                modMax = ReplaceText( modMax, "MAXMOD", mod2 );
                
                result += str_b + "\n" + str2 + "\n" + str3 + "\n" + modMin + "\n" + modMax;
            }
            else
            {
                string DtMod = GetMsgStr( TEXTMSG_GAME, STR_INV_DTMOD );
                DtMod = ReplaceText( DtMod, "DTMOD", item.Item_DtMod );
                result += "\n" + DtMod;
            }
            
        }
        else if( item.Type == ITEM_TYPE_ARMOR )
        {
            result += "\n\n-----------------------";
            string str;
            
            // Armor Perk
            result += "\n" + GetMsgStr( TEXTMSG_TEXT, STR_WPN_PERK_TITLE );
            string perksInfo = "";
            str = GetMsgStr( TEXTMSG_TEXT, STR_WPN_PERK );
            int count = 0;
            for( int i = 0; i < 32; i ++ )
            {
                if( ISBIT( item.Armor_Perk, i ) )
                {
                    string perkStr = GetMsgStr( TEXTMSG_TEXT, STR_ARMOR_PERK_NAME( i + 1 ) );
                    count ++;
                    if( perksInfo != "" )
                    {
                        perksInfo += GetMsgStr( TEXTMSG_TEXT, STR_WPN_PERK_SPACING );
                    }
                    
                    perksInfo += perkStr;
                }
            }
            
            if( perksInfo != "" )
            {
                result += ReplaceText( str, "VALUE", perksInfo );
            }
            else
            {
                result += GetMsgStr( TEXTMSG_TEXT, STR_ARMOR_PERK_NAME( 0 ) );
            }
        }
        else if( item.Type == ITEM_TYPE_AMMO )
        {
            // Ammo stats
            result += "\n-----------------------";
            string ammoACMod = GetMsgStr( TEXTMSG_GAME, STR_INV_AMMO_ACMOD );
            ammoACMod = ReplaceText( ammoACMod, "ACMOD", item.Ammo_AcMod);
            string ammoMult  = GetMsgStr( TEXTMSG_GAME, STR_INV_AMMO_DAMAGE  );
            ammoMult  = ReplaceText( ammoMult,  "MINDMG", item.Ammo_DmgMin );
            ammoMult  = ReplaceText( ammoMult,  "MAXDMG", item.Ammo_DmgMax );
            string ammoDRMod = GetMsgStr( TEXTMSG_GAME, STR_INV_AMMO_DRMOD );
            ammoDRMod = ReplaceText( ammoDRMod, "DRMOD", item.Ammo_DrMod );
            string ammoDTMod = GetMsgStr( TEXTMSG_GAME, STR_INV_AMMO_DTMOD );
            ammoDTMod = ReplaceText( ammoDTMod, "DTMOD", item.Item_DtMod );
            
            string perk = GetMsgStr( TEXTMSG_TEXT, STR_WPN_PERK_TITLE );
            string perksInfo = "";
            string str = GetMsgStr( TEXTMSG_TEXT, STR_WPN_PERK );
            int count = 0;
            for( int i = 0; i < 32; i ++ )
            {
                if( ISBIT( item.Ammo_Perk, i ) )
                {
                    string perkStr = GetMsgStr( TEXTMSG_TEXT, STR_AMMO_PERK_NAME( i + 1 ) );
                    count ++;
                    if( perksInfo != "" )
                    {
                        perksInfo += GetMsgStr( TEXTMSG_TEXT, STR_WPN_PERK_SPACING );
                    }
                    
                    perksInfo += perkStr;
                }
            }
            
            if( perksInfo != "" )
            {
                perk += ReplaceText( str, "VALUE", perksInfo );
            }
            else
            {
                perk += GetMsgStr( TEXTMSG_TEXT, STR_AMMO_PERK_NAME( 0 ) );
            }
            
            ammoDRMod = ReplaceText( ammoDRMod, "DRMOD", item.Ammo_DrMod );
            string ammoCal = GetMsgStr( TEXTMSG_GAME, STR_INV_CALIBER_MSG );
            ammoCal = ReplaceText( ammoCal, "AMMO", GetMsgStr( TEXTMSG_GAME, STR_CALIBER( item.Ammo_Caliber ) ) );

            result += "\n" + ammoACMod + "\n" + ammoMult + "\n" + ammoDRMod + "\n" + ammoDTMod + "\n" + perk + "\n" + ammoCal;
        }
        else if( item.Type == ITEM_TYPE_DRUG )
        {
            result += "\n-----------------------";
            string str = GetMsgStr( TEXTMSG_TEXT, STR_DRUG_TYPE ) + "|0xFFFF00  " + GetMsgStr( TEXTMSG_TEXT, item.Item_Subtype ) + "|0x3CF800 .";
            result += str;
        }
        else if( item.Slot == SLOT_HEAD )
        {
            result += "\n\n-----------------------";
            string str;
            
            // Headgear Perk
            result += "\n" + GetMsgStr( TEXTMSG_TEXT, STR_WPN_PERK_TITLE );
            string perksInfo = "";
            str = GetMsgStr( TEXTMSG_TEXT, STR_WPN_PERK );
            int count = 0;
            for( int i = 0; i < 32; i ++ )
            {
                if( ISBIT( item.HeadItem_Perk, i ) )
                {
                    string perkStr = GetMsgStr( TEXTMSG_TEXT, STR_HEADGEAR_PERK_NAME( i + 1 ) );
                    count ++;
                    if( perksInfo != "" )
                    {
                        perksInfo += GetMsgStr( TEXTMSG_TEXT, STR_WPN_PERK_SPACING );
                    }
                    
                    perksInfo += perkStr;
                }
            }
            
            if( perksInfo != "" )
            {
                result += ReplaceText( str, "VALUE", perksInfo );
            }
            else
            {
                result += GetMsgStr( TEXTMSG_TEXT, STR_WPN_PERK_NAME( 0 ) );
            }
        }
		
		result += "\n-----------------------";
		
		if(item.Tool_Perk != 0)
		{
			string str;

			// Tool Perk
			result += "\n" + GetMsgStr( TEXTMSG_TEXT, STR_CAN_BE_USED_AS );
			string toolPerksInfo = "";
			str = GetMsgStr( TEXTMSG_TEXT, STR_WPN_PERK );
			int toolCount = 0;
			for( int i = 0; i < 32; i ++ )
			{
				if( ISBIT( item.Tool_Perk, i ) )
				{
					string perkStr = GetMsgStr( TEXTMSG_TEXT, STR_TOOL_PERK_NAME( i + 1 ) );
					toolCount++;
					if( toolPerksInfo != "" )
					{
						toolPerksInfo += GetMsgStr( TEXTMSG_TEXT, STR_WPN_PERK_SPACING );
					}
					
					toolPerksInfo += perkStr;
				}
			}
			
			if( toolPerksInfo != "" )
			{
				result += ReplaceText( str, "VALUE", toolPerksInfo );
			}
			else
			{
				result += GetMsgStr( TEXTMSG_TEXT, STR_TOOL_PERK_NAME( 0 ) );
			}
		}
        
        // Weight
        result += "\n" + ReplaceText( GetMsgStr( TEXTMSG_GAME, STR_ITEM_WEIGHT_GRAMM ), "VALUE", item.Weight * itemCount );

        // Volume
        result += "\n" + ReplaceText( GetMsgStr( TEXTMSG_GAME, STR_INV_VOLUME ), "VALUE", item.Volume * itemCount );
        
        // Material
        result += "\n" + ReplaceText( GetMsgStr( TEXTMSG_GAME, STR_INV_MATERIAL ), "VALUE", GetMsgStr( TEXTMSG_GAME, item.Material + 170 ) );

        return result;
    }
}

class FixboySearch : IGUIElementCallbackInit, IGUIElementCallbackDraw, IGUIElementCallbackKeyPress, IGUIElementCallbackMouseClick
{
    IGUIElementOpt@ Options;
    ScreenMain@ Instance;

    FixboySearch( ScreenMain& instance )
    {
        @Instance = instance;
    }

    void OnInit()
    {
        @Options = GUI_GetElementOptions();
    }

    void OnDraw( int posX, int posY, int w, int h )
    {
        if(!needProcessSearch) return;
        ProcessSearch();
    }

    void OnKeyPress( uint8 key, uint8 letter )
    {
        if( key == DIK_ESCAPE || __ConsoleActive ) return;
		if( key == DIK_BACK && Options.GetText() == "" )
		{
			needProcessSearch = true;
			return;
		}
		if( key == DIK_RETURN )
		{
			needProcessSearch = true;
			return;
		}
    }

    void OnMouseClick( int click )
    {
        if(Options.GetText() == "Search")
        {
            Options.Text( null, FONT_FALLOUT, COLOR_DGREEN, COLOR_RED, FT_CENTERY );
        }
    }

    void ProcessSearch()
    {
        uint[] result;

        string searchQuery = Options.GetText();
        if(!valid(searchQuery) || searchQuery.length() < 3 || searchQuery == "Search")
        {
            ResetDisplayIndices();
            Instance.UpdateElements(currLevel);
            needProcessSearch = false;
            return;
        }
		
		searchQuery = ReplaceText(searchQuery, "\n", "");
        searchQuery = strlwr(searchQuery);
        searchQuery = ReplaceText(searchQuery, "�", "�");
		

        for(uint i = 0, len = GetMaxCraftsCount(); i < len; i++)
        {
            CraftRecipe@ currRecipe = GetCraftByPID(i);
            if(!valid(currRecipe)) continue;

            if(currProf != 0 && !FLAG(1 << (currRecipe.Profession - PE_CRAFTSMAN), currProf)) continue;
            if(currTier != 0 && !FLAG(1 << currRecipe.Tier, currTier)) continue;
            if(currFilterFlags != 0 && !FLAG(currRecipe.FilterFlags, currFilterFlags)) continue;

            string currName = strlwr(currRecipe.CraftName);
            currName = ReplaceText(currName, "�", "�");
            if(findFirst(currName, searchQuery) != -1)
            {
                result.insertLast(i);
            }
        }

        DisplayIndices = result;

        needProcessSearch = false;
        Instance.UpdateElements(currLevel);
    }
}

class FixboyFilterBut : IGUIElementCallbackInit, IGUIElementCallbackMouseClick
{
    IGUIElementOpt@ Options;
    ScreenMain@ Instance;
    uint Flag = 0;

    FixboyFilterBut( ScreenMain & instance )
    {
        @Instance = instance;
    }

    void OnInit()
    {
        @Options = GUI_GetElementOptions();
    }

    void OnMouseClick( int click )
    {
		Options.JammingState = !Options.JammingState;

        if(Options.JammingState)
        {
            SETFLAG(currFilterFlags, Flag);
        }
        else
        {
            UNSETFLAG(currFilterFlags, Flag);
        }

        needProcessSearch = true;
    }
}

class FixboyProfBut : IGUIElementCallbackInit, IGUIElementCallbackMouseClick
{
    IGUIElementOpt@ Options;
    ScreenMain@ Instance;
    uint Prof = 0;

    FixboyProfBut( ScreenMain & instance )
    {
        @Instance = instance;
    }

    void OnInit()
    {
        @Options = GUI_GetElementOptions();
    }

    void OnMouseClick( int click )
    {
        Options.JammingState = !Options.JammingState;
        if(Options.JammingState)
        {
            SETFLAG(currProf, 1 << (Prof - PE_CRAFTSMAN));
        }
        else
        {
            UNSETFLAG(currProf, 1 << (Prof - PE_CRAFTSMAN));
        }

        needProcessSearch = true;
    }
}

class FixboyTierBut : IGUIElementCallbackInit, IGUIElementCallbackMouseClick
{
    IGUIElementOpt@ Options;
    ScreenMain@ Instance;
    uint Tier = 0;

    FixboyTierBut( ScreenMain & instance )
    {
        @Instance = instance;
    }

    void OnInit()
    {
        @Options = GUI_GetElementOptions();
    }

    void OnMouseClick( int click )
    {
        Options.JammingState = !Options.JammingState;
        if(Options.JammingState)
        {
            SETFLAG(currTier, 1 << Tier);
        }
        else
        {
            UNSETFLAG(currTier, 1 << Tier);
        }

        needProcessSearch = true;
    }
}

void ShowScreen( int p0, int p1, int p2, string@ p3, int[]@ p4 )
{
	ScreenActive = true;
    ShowScreen( CLIENT_SCREEN_ECONOMY, 0, p1, p2 );
}

void HideScreen( int p0, int p1, int p2, string@ p3, int[]@ p4 )
{
	ScreenActive = false;
    GUI_HideScreen( CLIENT_SCREEN_ECONOMY, 0, 0, 0 );
}

void SwitchEconomyScreen()
{
    if(!ScreenActive) ShowScreen( CLIENT_SCREEN_ECONOMY, 0, 0, 0 );
//    else HideScreen( CLIENT_SCREEN_ECONOMY, 0, 0, 0 );
}

string[] FilterPicNames = {"parts", "sg", "clothes", "melee", "tools", "chem", "mg", "armor", "traps", "back", "ammo", "bg", "head", "thrown", "furniture"};
string[] ProfPicNames = {"craft", "mechanic", "chemist", "electric"};

void InitEconomyScreen()
{
    InitEconomyParser();
    ScreenMain screen();
	@screenPtr = screen;
    GUI_CreateScreen( CLIENT_SCREEN_ECONOMY, "fixboy/fixboy_TLJ.png" )
    .CallbackHide( screen )
    .CallbackShow( screen )
    .AutoCursor( true, CURSOR_DEFAULT );
	
	FixboyDoneBut OkButton( screen );
    GUI_AddScreenElement( CLIENT_SCREEN_ECONOMY, "", 413, 536 )
    .CallbackInit( OkButton )
    .CallbackMouseClick( OkButton )
    .SetSize(82, 46)
    .DownPic( "done_dn_en.png" );

    for(uint i = 0; i < 2; i++)
    {
        FixboyScrollBut scrollButton( screen, i > 0 );
        GUI_AddScreenElement( CLIENT_SCREEN_ECONOMY, "", (i > 0 ? 86 : 187), 538 )
        .CallbackInit( scrollButton )
        .CallbackMouseClick( scrollButton )
        .SetSize(36, 36)
        .DownPic( ( i > 0 ? "fixboy/fixboy_up.png" : "fixboy/fixboy_down.png" ) );
    }

    FixboyCraftText craftLabel(screen);
    GUI_AddScreenElement( CLIENT_SCREEN_ECONOMY, "", 73, 14 )
    .CallbackInit( craftLabel )
    .SetSize(440, 22);
    @screen.craftLabel = craftLabel;
    
    FixboyCraftText skillLabel(screen);
    GUI_AddScreenElement( CLIENT_SCREEN_ECONOMY, "", 73, 72 )
    .CallbackInit( skillLabel )
    .SetSize(82, 16)
    .SetVisible(false);
    screen.sectionLabels.insertLast(skillLabel);

    FixboyCraftText toolsLabel(screen);
    GUI_AddScreenElement( CLIENT_SCREEN_ECONOMY, "", 73, 172 )
    .CallbackInit( toolsLabel )
    .SetSize(82, 16)
    .SetVisible(false);
    screen.sectionLabels.insertLast(toolsLabel);

    FixboyCraftText resLabel(screen);
    GUI_AddScreenElement( CLIENT_SCREEN_ECONOMY, "", 73, 312 )
    .CallbackInit( resLabel )
    .SetSize(84, 16)
    .SetVisible(false);
    screen.sectionLabels.insertLast(resLabel);

    FixboyCraftText skillsReq(screen);
    GUI_AddScreenElement( CLIENT_SCREEN_ECONOMY, "", 73, 82 )
    .CallbackInit( skillsReq )
    .SetSize(460, 192)
    .SetVisible(false);
    @screen.skillsReq = skillsReq;

    FixboyCraftText toolsReq(screen);
    GUI_AddScreenElement( CLIENT_SCREEN_ECONOMY, "", 73, 181 )
    .CallbackInit( toolsReq )
    .SetSize(272, 192)
    .SetVisible(false);
    @screen.toolsReq = toolsReq;
	
	FixboyCraftText counterScrn(screen);
	GUI_AddScreenElement( CLIENT_SCREEN_ECONOMY, "", 125, 538 )
    .CallbackInit( counterScrn )
    .SetSize(60, 36)
    .SetVisible(true);
    @screen.counterScrn = counterScrn;

    for(int i = 0; i < FIXBOY_PAGE_LEN; i++)
    {
        FixboyRecipeBut currButton( screen );
        GUI_AddScreenElement( CLIENT_SCREEN_ECONOMY, "", 68, 17 + (10 * i) )
        .CallbackInit( currButton )
        .CallbackMouseClick( currButton )
        .CallbackDraw( currButton )
        .SetSize(455, 7);
    }
    
    int col = 0, row = 0;
    for(uint i = 0; i < 12; i++)
    {
        FixboyCraftResBut currButton( screen );
        GUI_AddScreenElement( CLIENT_SCREEN_ECONOMY, "", 343 + (45 * col), 172 + (45 * row) )
        .CallbackInit( currButton )
        .CallbackMouseClick( currButton )
        .SetSize(40, 40)
        .SetVisible(false);
        screen.toolsBtns.insertLast(currButton);

        col++;
        if( col > 3 )
        {
            col = 0;
            row++;
        }
    }

    FixboyHoverInfo hoverInfo( screen );
    GUI_AddScreenElement( CLIENT_SCREEN_ECONOMY, "", 564, 39 )
    .CallbackInit( hoverInfo )
    .CallbackDraw( hoverInfo )
    .SetSize(164, 280);
    
    FixboyCraftResBut hoverIcon( screen );
    GUI_AddScreenElement( CLIENT_SCREEN_ECONOMY, "", 580, 62 )
    .CallbackInit( hoverIcon )
    .CallbackMouseClick( hoverIcon )
    .SetSize(128, 70);

    @hoverInfo.ItemIcon = hoverIcon;

    FixboySearch search( screen );
    GUI_AddScreenElement( CLIENT_SCREEN_ECONOMY, "", 562, 352 )
    .CallbackInit( search )
    .CallbackDraw( search )
    .CallbackKeyPress( search )
    .CallbackMouseClick( search )
    .SetSize(154, 14)
    .Text( "Search", FONT_FALLOUT, COLOR_GRAY, COLOR_RED, FT_CENTERY )
    .TextInput(true, 40, COLOR_LGREEN);

    for(uint i = 0; i < 15; i++)
    {
        int x = i % 5;
        int y = i / 5;

        FixboyFilterBut currFilter( screen );
        currFilter.Flag = 1 << i;
        GUI_AddScreenElement( CLIENT_SCREEN_ECONOMY, "", 544 + (38 * x), 378 + (34 * y))
        .CallbackInit( currFilter )
        .CallbackMouseClick( currFilter )
        .SetSize(34, 33)
        .SetJamming(true)
        .DownPic( "fixboy/filter_" + FilterPicNames[i] + ".png" );
    }

    for(uint i = 0; i < 4; i++)
    {
        FixboyProfBut currProfBtn( screen );
        currProfBtn.Prof = PE_CRAFTSMAN + i;
        GUI_AddScreenElement( CLIENT_SCREEN_ECONOMY, "", 7, 307 + (39 * i))
        .CallbackInit( currProfBtn )
        .CallbackMouseClick( currProfBtn )
        .SetSize(26, 35)
        .SetJamming(true)
        .DownPic( "fixboy/job_" + ProfPicNames[i] + ".png" );
    }

    for(uint i = 1; i <= 6; i++)
    {
        FixboyTierBut currTierBtn( screen );
        currTierBtn.Tier = i;
        GUI_AddScreenElement( CLIENT_SCREEN_ECONOMY, "", 6, 36 + (39 * (i - 1)))
        .CallbackInit( currTierBtn )
        .CallbackMouseClick( currTierBtn )
        .SetSize(26, 35)
        .SetJamming(true)
        .DownPic( "fixboy/tech_" + i + "_down.png" );
    }
}

class ItemPart
{
    uint16 Pid = 0;
    uint Count = 0;

    ItemPart(uint16 pid, uint count)
    {
        if( pid == 0 || count == 0 )
        {
            return;
        }
        
        this.Pid = pid;
        this.Count = count;
    }
}

class ItemPartArray
{
    ItemPart@[] ItemParts;
    dictionary pidAssosiateList;
    uint Tier = 1;
    uint Tools = 0;
    uint CalcCost = 0;
    uint ProtoCost = 0;
    
    void Add(uint16 pid, uint count)
    {
        if( pid == 0 || count == 0 )
        {
            return;
        }
        
        uint index;
        if( pidAssosiateList.get( ""+pid, index ) )
        {
            ItemParts[index].Count += count;
            return;
        }

        ItemPart@ newElem = ItemPart(pid, count);
        Add(newElem);
        pidAssosiateList.set(""+pid, this.length() - 1);
    }

    void Add(ItemPart@ newElem)
    {
        ItemParts.insertLast(newElem);
    }

    uint length()
    {
        return ItemParts.length();
    }

    ItemPart@ get_opIndex(int index) 
    {
        if( index >= int( this.length() ) )
        {
            return null;
        }
        
        return ItemParts[index];
    }
}

dictionary EconomyList;

ItemPartArray@ ProcessDecraftRecipe(CraftRecipe@ recipe)
{
	if( !valid( recipe ) )
	{
		return null;
	}
	
	ItemPartArray@ Decraft;

	if( EconomyList.exists( ""+recipe.Output[0] ) )
	{
		EconomyList.get(""+recipe.Output[0], @Decraft);
		return Decraft;
	}
	
	@Decraft = ItemPartArray();
	Decraft.Tier = recipe.Tier;
	Decraft.Tools = recipe.Tools;

	ProtoItem@ proto = GetProtoItem(recipe.Output[0]);

    if( valid( proto ) )
    {
        Decraft.ProtoCost = proto.Cost;
        if( ( proto.Type == ITEM_TYPE_ARMOR || proto.Type == ITEM_TYPE_WEAPON ) )
        {
            SETFLAG(Decraft.Tools, TOOL_MULTITOOL);
        }
    }

	for(uint i = 0, len = recipe.Resources.length(); i < len; i++)
	{
		uint16 pid = recipe.Resources[i];
		uint count = recipe.ResourcesCount[i];
		ProtoItem@ ResourceProto = GetProtoItem(pid);
		if(!valid(ResourceProto))
		{
			continue;
		}

		CraftRecipe@ ResourceRecipe = GetCraftByItemPID(pid);
		
		CraftRecipe@ ComponentRecipe = GetCraftByItemPID( pid );
		if( !valid( ComponentRecipe ) ) 
		{
            Decraft.CalcCost += ResourceProto.Cost * count;
			Decraft.Add(pid, count);
			continue;
		}

		ItemPartArray@ ComponentDecraft = ProcessDecraftRecipe(ComponentRecipe);
		if(!valid(ComponentDecraft))
		{
			continue;
		}

        Decraft.CalcCost += ComponentDecraft.CalcCost;

		for(uint j = 0, jlen = ComponentDecraft.length(); j < jlen; j++)
		{
			ItemPart@ currCompDecraftRes = ComponentDecraft[j];
			if(!valid(currCompDecraftRes))
			{
				continue;
			}
			
			Decraft.Add(currCompDecraftRes.Pid, currCompDecraftRes.Count * count);
		}
	}

	if(Decraft.length() > 0)
	{
		EconomyList.set( ""+recipe.Output[0], Decraft );
	}
	
	return Decraft;
}

void InitEconomyParser()
{
	Log("Generating economy list...");
	for(uint i = 0, len = GetMaxCraftsCount(); i < len; i++)
	{
		CraftRecipe@ currRecipe = GetCraftByPID(i);
		if(!valid(currRecipe)) continue;
		ProcessDecraftRecipe(currRecipe);
	}
	Log("Generating economy list complete.");
}

ItemPartArray@ GetItemParts(uint16 pid)
{
	ItemPartArray@ Decraft;
	if( EconomyList.get(""+pid, @Decraft) )
	{
		return Decraft;
	}
	return null;
}