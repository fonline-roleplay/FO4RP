#ifndef SMITHING
#define SMITHING

#include "_utils.fos"
#include "smithing_h.fos"
#include "_ltp.fos"
#include "gathering_h.fos"
#include "speed_h.fos"

class MenuAnvil: CenteredMenuHandler {
    uint anvil_id;
    uint map_id;
	
    MenuAnvil( Item& anvil, Map& map ) {
        anvil_id = anvil.Id;
        map_id = map.Id;
    }

    bool MenuUpdate( Critter& cr, iDialogBox& menu ) {
        Map@ map = GetMap( map_id );
        Item@ anvil = GetItem( anvil_id );
	
		bool locked_in_place = !FLAG( anvil.Flags, ITEM_CAN_PICKUP );
	
        if( map is null || anvil is null ) {
            return false;
        }

		if( locked_in_place ) {
			if( hasItems( cr, SmithingTools, SLOT_HAND1 ) && menu.Button( "Ковать" ) ) {
				Item@ tool = _CritGetItemHand( cr );
				PrepareSmithing( cr, tool, anvil );
				return false;
			}

			if( menu.Button( "Осмотреть" ) ) {
				ShowContainer( cr, anvil, TRANSFER_HEX_CONT_UP );
				return false;
			}
			
			if( menu.Button( "Демонтировать" ) ) {
				LockInPlace( cr, anvil );
				return true;
			}
			
		} else {
			if( menu.Button( "Установить" ) ) {
				LockInPlace( cr, anvil );
				return true;
			}

			if( menu.Button( "Поднять" ) ) {
				PickItem( cr, anvil );
				ChangeCritterSpeed( cr );
				return false;
			}			
		}
		return true;
    }
	
	// UI менюхи
    string@ Description( Critter& cr ) {
	
		string info;
		Item@ anvil = GetItem( anvil_id );
		
		Item@[] MaterialsCheck;
		anvil.GetItems( uint( -1 ), MaterialsCheck );
		Item@ material = null;
		
		if( MaterialsCheck.length() == 0 ) { 
			info = "|0x3CF800 Наковальня пуста."; 
		} else {
			for( uint i = 0; i < MaterialsCheck.length(); i ++ ) {
				uint16 materialPid = MaterialsCheck[i].GetProtoId();
				if( MaterialList.find( materialPid ) != -1 ) {
					@material = MaterialsCheck[i];
					break;
				}
			}
		}
		if( !valid( material ) ) { 
			info = "|0x3CF800 На наковальне нет подходящего бруса.";
        } else {
			uint16 materialPid = material.GetProtoId();
			info = "|0x3CF800 Поверх наковальни лежит: ";
			info += "|0xFFFF00 " + _GetProtoName( materialPid );
			
			string amount = 0;
			amount = "|0xFFFF00 " + material.GetCount();
			info += "|0x3CF800  - ";
			info += "|0xFFFF00 " + amount;
			info += "|0x3CF800  шт.";
		}
		return info;
    }
	
    string@ ButtonCancel() {
        return ButtonDecorator( "Скрыть меню", null );
    }
}

void StartMenuAnvil( Critter& cr, Item& anvil )
{
    Map@ map = cr.GetMap();
    if( map is null ) {
        return;
    }

    iMenuHandler@ handler = MenuAnvil( anvil, map );
    iDialogBox@ menu = OpenMenu( cr, "Наковальня", handler );
}

// LTP (long time process)
bool ltp_smithing_inited = false;
void ltp_smithing_init()
{
	LTPREG( LTP_SMITHING, process_Smithing )
	ltp_smithing_inited = true;
}
//exported to main.fos - checks before start
bool PrepareSmithing( Critter& cr, Item@ tool, Item@ anvil ) 
{
	Map@ map = cr.GetMap();
    if( !valid( map ) ) { return false; }
	if( !valid( tool ) || !valid( anvil ) || anvil.GetProtoId() != PID_ANVIL ) { return false; }
	if( !hasItems( cr, SmithingTools, SLOT_HAND1 ) ) { return false; }

	if( IsTired( cr ) ) { return false; }
	if( cr.IsInjured() ) { 
		cr.Say( SAY_NETMSG, "|0xFF0000 Вы травмированы и не в состоянии заниматься кузнечным делом." );
		return false; 
	}
	
	Item@[] MaterialsCheck;
	anvil.GetItems( uint( -1 ), MaterialsCheck );
	Item@ material = null;
	
	if( MaterialsCheck.length() == 0 ) { 
		cr.Say( SAY_NETMSG, "|0xFFFF00 Наковальня пуста." ); 
		return false; 
	} else {
		for( uint i = 0; i < MaterialsCheck.length(); i ++ ) {
			uint16 materialPid = MaterialsCheck[i].GetProtoId();
			if( MaterialList.find( materialPid ) != -1 ) {
				@material = MaterialsCheck[i];
				break;
			}
		}
		
		if( !valid( material ) ) { 
			cr.Say( SAY_NETMSG, "|0xFFFF00 На наковальне нет подходящего бруса." ); 
			return false; 
		}
	}
	
	start_Smithing( cr, tool, anvil, material );
	return true;
}

bool start_Smithing( Critter& cr, Item& tool, Item& anvil, Item@ material )
{
	if(!ltp_smithing_inited) 
		ltp_smithing_init();

	if( valid( tool ) && valid( anvil ) )
	{	
		uint hit_pause = ACTION_PAUSE_BASE - ( cr.Stat[ ST_STRENGTH ] * ACTION_PAUSE_BONUS_RATE );
		uint action_pause = CLAMP( hit_pause, ACTION_PAUSE_MIN, ACTION_PAUSE_MAX );
		uint[] values = { cr.Id };
		
		_CritAnimateSwing( cr );
		CreateTimeEvent( AFTER( REAL_MS( 700 ) ), "e_SmithingSound", values, false);
		CreateTimeEvent( AFTER( REAL_MS( 1000 ) ), "gathering@e_TiredenessTimeout", cr.Id, true);	

		StartProcess( cr, LTP_SMITHING, 0, anvil.Id, material.Id, action_pause );
		return true;
	}
	return false;
}

uint process_Smithing( Critter@ cr, int& param0, int& param1, int& param2 )
{
    LTPROCESS( cr, param0, LTP_SMITHING )
	uint hit_pause = ACTION_PAUSE_BASE - ( cr.Stat[ ST_STRENGTH ] * ACTION_PAUSE_BONUS_RATE );
	uint action_pause = CLAMP( hit_pause, ACTION_PAUSE_MIN, ACTION_PAUSE_MAX );
    Item@ anvil = GetItem( param1 );
  	Item@ material = GetItem( param2 );
  	if( valid( anvil ) && valid( material ) )
    {
		param0++;
		
		uint smithing_rate = ( cr.Stat[ ST_STRENGTH ] * ACTION_PAUSE_BONUS_RATE ) + OBJECT_DPA_BASE; 
		int sequence_length = ceil( float( 3000.0f / ( CLAMP( smithing_rate, OBJECT_DPA_MIN, OBJECT_DPA_MAX ) ) ) );
		
		if( param0 > sequence_length )
		{
			cr.Say( SAY_EMOTE, "завершает работу" );
			Profit( cr, anvil, material );
			int skillNum = SK_REPAIR;
			Accident( cr, skillNum );
			return 0;
		}
		
		uint[] values = { cr.Id };
		CreateTimeEvent( AFTER( REAL_MS( 700 ) ), "e_SmithingSound", values, true);
		CreateTimeEvent( AFTER( REAL_MS( 1000 ) ), "gathering@e_TiredenessTimeout", values, true);

		_CritAnimateSwing( cr );
		
		return action_pause;
	}
	return 0;
}

uint e_SmithingSound( uint[]@ values )
{
	Critter@ cr = GetCritter(values[0]);
	Map@ map = cr.GetMap();
	map.PlaySound( SmithingSounds[ Random( 0, SmithingSounds.length() -1 ) ], cr.HexX, cr.HexY, 5 );
	return 0;
}

void Profit(  Critter& cr, Item& anvil, Item& material )
{
	if( !valid( anvil) && !valid( material ) ) { return ; }
	
	uint16 materialPid = material.GetProtoId();
	uint8 index = MaterialList.find( materialPid );
	uint16 resultPid = ResultList[ index ];
	uint8 amount = Amount[index];
	cr.AddItem( resultPid, amount );
	_SubItem( material, 1 );
	
	string inform = "Вы получили: \n";
	inform += "- ";
	inform += "|0xFFFF00 " + _GetProtoName( resultPid );
	inform += "|0x3CF800  - ";
	inform += "|0xFFFF00 " + amount;
	inform += "|0x3CF800  шт.";
	cr.Say( SAY_NETMSG, inform );
	
	cr.StatBase[ST_EXPERIENCE] += 25;
}

#endif //SMITHING