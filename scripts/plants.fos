#ifndef PLANTS_MODULE
#define PLANTS_MODULE

#include "_utils.fos"
#include "plants_h.fos"
#include "critter_skills_h.fos"
#include "flask_h.fos"
#include "furniture_h.fos"
#include "speed_h.fos"

import bool CheckGard( Map& map, uint16 hexX, uint16 hexY ) from "qmap_farm";

class Plant
{
	uint16 pid;
	uint8 type;
	uint8 seedStage;
	uint8 gatheringStage;
	uint8 deathStage;
	int[] seed;
	int[] loot;
	
	Plant( uint16 pid, uint8 type, uint8 seedStage, uint8 gatheringStage, uint8 deathStage )
	{
		this.pid = pid;
        this.type = type;
        this.seedStage = seedStage;
		this.gatheringStage = gatheringStage;
		this.deathStage = deathStage;
	}
}

class PlantCollection
{
	Plant@[] plants;
	PlantCollection()
	{
	}
	
	PlantCollection@ cplant( uint16 pid, uint8 type, uint8 seedStage, uint8 gatheringStage, uint8 deathStage ) 
	{
		this.plants.insertLast( Plant( pid, type, seedStage, gatheringStage, deathStage ) );
		return this;
	}
	
	PlantCollection@ seed( int seed ) 
	{
		this.plants[ this.plants.length() - 1 ].seed.insertLast( seed );
		return this;
	}
	
	PlantCollection@ loot( int loot ) 
	{
		this.plants[ this.plants.length() - 1 ].loot.insertLast( loot );
		return this;
	}
  
  	PlantCollection@ get_by_type( uint8 type ) 
	{
		PlantCollection result;
		for( uint i=0, len = this.plants.length(); i < len; i++ )
		{
			if( this.plants[i].type == type )
			{
				result.plants.insertLast( this.plants[i] );
			}
		}
		return result;
	}

	Plant@ get_by_seed( uint16 seedPid ) 
	{
		for( uint i = 0, len = this.plants.length(); i < len; i++ )
		{
			if( this.plants[i].seed.find( seedPid ) != -1 )
			{
				return this.plants[i];
			}
		}
		return null;
	}
	
	Plant@ get_by_pid( uint16 pid ) 
	{
		for( uint i = 0, len = this.plants.length(); i < len; i++ )
		{
			if( this.plants[i].pid == pid )
			{
				return this.plants[i];
			}
		}
		return null;
	}	
}

PlantCollection@ plants = PlantCollection()
//					pid,							type, 			seedStage, gatheringStage, deathStage
    .cplant( PID_PLANT_RADCVET,					PLANT_TYPE_FARM, 		3,	 		4,			  5		)
		.seed( PID_PLANT_RADCVET_SEED )
		.loot( PID_RADCVET )
		.loot( PID_PLANT_RADCVET_DEAD )
    .cplant( PID_PLANT_CABBAGE,					PLANT_TYPE_FARM, 		1,	 		3,			  4		)
		.seed( PID_PLANT_CABBAGE_SEED )
		.loot( PID_CABBAGE )
		.loot( PID_PLANT_CABBAGE_DEAD )
    .cplant( PID_PLANT_FIVELEAF,				PLANT_TYPE_FARM, 		3,	 		4,			  5		)
		.seed( PID_PLANT_FIVELEAF_SEED )
		.loot( PID_FIVELEAF )
		.loot( PID_PLANT_FIVELEAF_DEAD )
	.cplant( PID_PLANT_CORN,					PLANT_TYPE_FARM, 		2,	 		3,			  4		)
		.seed( PID_PLANT_CORN_SEED )
		.loot( PID_CORN )
		.loot( PID_PLANT_CORN_DEAD )
	.cplant( PID_PLANTBARREL_RAD,				PLANT_TYPE_BARREL, 		4,	 		5,			  6		)
		.seed( PID_PLANT_RADCVET_SEED )
		.loot( PID_RADCVET )
		.loot( PID_PLANT_RADCVET_DEAD )
    .cplant( PID_PLANTBARREL_CAB,				PLANT_TYPE_BARREL, 		2,	 		4,			  5		)
		.seed( PID_PLANT_CABBAGE_SEED )
		.loot( PID_CABBAGE )
		.loot( PID_PLANT_CABBAGE_DEAD )
    .cplant( PID_PLANTBARREL_FIVELEAF,			PLANT_TYPE_BARREL, 		4,	 		5,			  6		)
		.seed( PID_PLANT_FIVELEAF_SEED )
		.loot( PID_FIVELEAF )
		.loot( PID_PLANT_FIVELEAF_DEAD )
	.cplant( PID_PLANTBARREL_CORN,				PLANT_TYPE_BARREL, 		3,	 		4,			  5		)
		.seed( PID_PLANT_CORN_SEED )
		.loot( PID_CORN )
		.loot( PID_PLANT_CORN_DEAD )	
	.cplant( PID_HYDRO_EW_BROC,					PLANT_TYPE_HYDRO, 		9,	 		7,			  8		)
		.seed( PID_BROC_FLOWER )
		.loot( PID_BROC_FLOWER )
	.cplant( PID_HYDRO_EW_XDR,					PLANT_TYPE_HYDRO, 		7,	 		5,			  6		)
		.seed( PID_XANDER_ROOT )
		.loot( PID_XANDER_ROOT )
	.cplant( PID_HYDRO_EW_RAD,					PLANT_TYPE_HYDRO, 		4,	 		5,			  6		)
		.seed( PID_PLANT_RADCVET_SEED )
		.loot( PID_RADCVET )
		.loot( PID_PLANT_RADCVET_DEAD )
	.cplant( PID_HYDRO_EW_CORN,					PLANT_TYPE_HYDRO, 		3,	 		4,			  5		)
		.seed( PID_PLANT_CORN_SEED )
		.loot( PID_CORN )
		.loot( PID_PLANT_CORN_DEAD )
	.cplant( PID_HYDRO_NS_CAB,					PLANT_TYPE_HYDRO, 		1,	 		3,			  4		)
		.seed( PID_PLANT_CABBAGE_SEED )
		.loot( PID_CABBAGE )
		.loot( PID_PLANT_CABBAGE_DEAD )	
	.cplant( PID_HYDRO_EW_CAB,					PLANT_TYPE_HYDRO, 		1,	 		3,			  4		)
		.seed( PID_PLANT_CABBAGE_SEED )
		.loot( PID_CABBAGE )
		.loot( PID_PLANT_CABBAGE_DEAD )	
	.cplant( PID_MUSHBED_PALE,					PLANT_TYPE_MUSHBED, 	0,	 		0,			  4		)
		.seed( PID_SEEDLING_PALE_MUSHROOM )
		.seed( PID_RISING_PALE_MUSHROOM )
		.seed( PID_PALE_MUSHROOM )
	.cplant( PID_MUSHBED_BLACK,					PLANT_TYPE_MUSHBED, 	0,	 		0,			  4		)
		.seed( PID_SEEDLING_BLACK_MUSHROOMS )
		.seed( PID_RISING_BLACK_MUSHROOMS )
		.seed( PID_BLACK_MUSHROOMS )
	.cplant( PID_MUSHBED_GECKO,					PLANT_TYPE_MUSHBED, 	0,	 		0,			  4		)
		.seed( PID_SEEDLING_TONGUE_GECKO )
		.seed( PID_RISING_TONGUE_GECKO )
		.seed( PID_TONGUE_GECKO )
	.cplant( PID_MUSHBED_ANT,					PLANT_TYPE_MUSHBED, 	0,	 		0,			  4		)
		.seed( PID_SEEDLING_MURAVNIKTAR )
		.seed( PID_RISING_MURAVNIKTAR )
		.seed( PID_MURAVNIKTAR )
	.cplant( PID_MUSHBED_MUT,					PLANT_TYPE_MUSHBED, 	0,	 		0,			  4		)
		.seed( PID_SEEDLING_MUTMUSH )
		.seed( PID_RISING_MUTMUSH )
		.seed( PID_MUTMUSH )
	;

Plant@ GetPlantByPid( Item@ item )
{
    uint16 Pid = item.GetProtoId();
    return plants.get_by_pid( Pid );
}

Plant@ GetPlant( Item@ seed, Item@ plantbed ) 
{
	if( valid( seed ) )
	{
		uint16 seedPid = seed.GetProtoId();
		bool isMushroom = ( mushbed_seeds.find( seedPid ) != -1 );
		bool isBarrel = ( farm_seeds.find( seedPid ) != -1 );
		uint8 type = 0;
		if( !valid( plantbed ) )
		{
			type = PLANT_TYPE_FARM;
		}
		else
		{
			uint16 bedPid = plantbed.GetProtoId();
			if( bedPid == PID_PLANTBARREL_EMPTY )
			{
				type = PLANT_TYPE_BARREL;
			}
			else if( bedPid == PID_MUSHBED_EMPTY )
			{
				type = PLANT_TYPE_MUSHBED;
			}
			else if( bedPid == PID_HYDRO_NS_EMPTY || bedPid == PID_HYDRO_EW_EMPTY )
			{
				type = PLANT_TYPE_HYDRO;
			}
		}
		return plants.get_by_type( type ).get_by_seed( seedPid );
	}
	return null;
}

void TryPlantSeed( Critter& cr, Item& seed, int hexX, int hexY, Item@ plant_bed ) 
{
	Map@ map = cr.GetMap();
	if( !valid( map ) )
	{
		return;
	}

	Plant@ plant = GetPlant( seed, plant_bed );

	if( !valid( plant ) ) 
	{
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_PLANTS_YOU_CANT_PLANT_THIS );
		return;
	}

	if( plant.type == PLANT_TYPE_FARM )
	{
		if( GetDistantion( cr.HexX, cr.HexY, hexX, hexY ) > 1 )
		{
			cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_PLANTS_YOU_ARE_TOO_FAR  );
			return;
		}

		if( CheckGard( map, hexX, hexY ) )
		{
			Item@[] items;
			if( map.GetItems( hexX, hexY, items ) == 0 )
			{
				PlantSeed( cr, seed, hexX, hexY );
			}
			else
			{
				cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_PLANTS_NEED_EMPTY_SOIL );
			}
		}
		else
		{
			cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_PLANTS_PLANT_IT_ELSEWHERE );
		}
	}
	else if( plant.type == PLANT_TYPE_BARREL )
	{
		if( plant_bed.GetProtoId() == PID_PLANTBARREL_EMPTY )
		{
			if( plant_bed.SOIL_FERTILITY <= 0 )
			{
				cr.Say( SAY_NETMSG, "|0xFFFF00 The soil lacks mineral fertilizer" );
			}
			else if( plant_bed.PLANT_WATERING > 0 && plant_bed.PLANT_FERTILITY > 0 )
			{
				PlantBarrel( cr, seed, plant_bed );
				plant_bed.SOIL_FERTILITY -= Random( 1, 3 );
				cr.SayMsg( SAY_EMOTE_ON_HEAD, TEXTMSG_TEXT, STR_PLANTS_PLANTING_MUSHROOM );
			}
			else
			{
				cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_PLANTS_NEED_WATERED_AND_FERTILE );
			}
		}
		else
		{
			cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_PLANTS_NEED_HYDROPONICS_FARM );
		}
	}
	else if( plant.type == PLANT_TYPE_HYDRO )
	{
		if( plant_bed.GetProtoId() == PID_HYDRO_NS_EMPTY || plant_bed.GetProtoId() == PID_HYDRO_EW_EMPTY )
		{
			if( plant_bed.PLANT_WATERING > 0 && plant_bed.PLANT_FERTILITY > 0 )
			{
				PlantHydro( cr, seed, plant_bed );
				cr.SayMsg( SAY_EMOTE_ON_HEAD, TEXTMSG_TEXT, STR_PLANTS_PLANTING_HYDRO );
			}
			else
			{
				cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_PLANTS_NEED_WATERED_AND_FERTILE );
			}
		}
		else
		{
			cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_PLANTS_NEED_HYDROPONICS_FARM );
		}
	}
	else if( plant.type == PLANT_TYPE_MUSHBED )
	{
		if( plant_bed.GetProtoId() == PID_MUSHBED_EMPTY )
		{
			if( plant_bed.PLANT_WATERING > 0 && plant_bed.PLANT_FERTILITY > 0 )
			{
				PlantMushroom( cr, seed, plant_bed );
				cr.SayMsg( SAY_EMOTE_ON_HEAD, TEXTMSG_TEXT, STR_PLANTS_PLANTING_MUSHROOM );
			}
			else
			{
				cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_PLANTS_NEED_WATERED_AND_FERTILE );
			}
		}
		else
		{
			cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_PLANTS_NEED_MUSHROOM_BED );
		}
	}
}

void PlantSeed( Critter& cr, Item& seed, int hexX, int hexY )
{
	Map@ map = cr.GetMap();	
	Item@ plantedSeed = map.AddItem( hexX, hexY, seed.GetProtoId(), 1 );
	cr.SayMsg( SAY_EMOTE_ON_HEAD, TEXTMSG_TEXT, STR_PLANTS_PLANTING_SEED );
	uint[] values = { plantedSeed.Id };
	plantedSeed.PLANT_EVENT = CreateTimeEvent( AFTER( REAL_MINUTE( Random( MIN_GROWTH_TIME, MAX_GROWTH_TIME ) ) ), "e_SeedEvent", values, true );
	_SubItem( seed, 1 );
	cr.Animate( 0, ANIM2_PICKUP, null, false, true );
}

uint e_SeedEvent( uint[]@ values )
{
	Item@ plantedSeed = GetItem( values[0] );
	if( !valid( plantedSeed ) )
	{
		return 0;
	}
	
	uint16 hx = 0, hy = 0;
	Map@ map = plantedSeed.GetMapPosition( hx, hy );
	if( !valid( map ) )
	{
		EraseTimeEvent( plantedSeed.PLANT_EVENT );
		return 0;
	}
	
	if( !CheckGard( map, plantedSeed.HexX, plantedSeed.HexY ) )
	{
		EraseTimeEvent( plantedSeed.PLANT_EVENT );
		return 0;
	}
	
	Plant@ plant = GetPlant( plantedSeed, null );
	if( !valid( plant ) )
	{
		EraseTimeEvent( plantedSeed.PLANT_EVENT );
		return 0;
	}
	
	plantedSeed.PLANT_GROW_STAGE ++;
	plantedSeed.ChangeProto( plant.pid );
	IncreaseCrop( plantedSeed, 1 );

	EraseTimeEvent( plantedSeed.PLANT_EVENT );
	plantedSeed.PLANT_EVENT = CreateTimeEvent( AFTER( REAL_MINUTE( Random( MIN_GROWTH_TIME, MAX_GROWTH_TIME ) ) ), "e_PlantGrowth", values, true );
	return 0;
}

void PlantMushroom( Critter& cr, Item& seed, Item& plant_bed )
{
	if( valid( seed ) && valid( plant_bed ) )
	{
		Plant@ plant = GetPlant( seed, plant_bed );
		if( valid( plant ) )
		{
			plant_bed.PLANT_BED_DNA = plant_bed.GetProtoId();
			plant_bed.ChangeProto( plant.pid );
			
			/*Map@ map = cr.GetMap();
			uint16 hx = plant_bed.HexX, hy = plant_bed.HexY;
			map.MoveHexByDir( hx, hy, 0, 1 );
			Item@ blocker0 = map.AddItem( hx, hy, PID_WOOD_BLOCKER, 1 );
			if( valid( blocker0 ) )
			{
				blocker0.PARENT_ITM_ID = plant_bed.Id;
			}
			
			map.MoveHexByDir( hx, hy, 2, 1 );
			Item@ blocker1 = map.AddItem( hx, hy, PID_WOOD_BLOCKER, 1 );
			if( valid( blocker1 ) )
			{
				blocker1.PARENT_ITM_ID = plant_bed.Id;
			}
			
			map.MoveHexByDir( hx, hy, 3, 1 );
			map.MoveHexByDir( hx, hy, 4, 1 );
			Item@ blocker2 = map.AddItem( hx, hy, PID_WOOD_BLOCKER, 1 );
			if( valid( blocker2 ) )
			{
				blocker2.PARENT_ITM_ID = plant_bed.Id;
			}
			
			map.MoveHexByDir( hx, hy, 5, 1 );
			Item@ blocker3 = map.AddItem( hx, hy, PID_WOOD_BLOCKER, 1 );
			if( valid( blocker3 ) )
			{
				blocker3.PARENT_ITM_ID = plant_bed.Id;
			}*/
			
			uint[] values = { plant_bed.Id };
			plant_bed.PLANT_EVENT = CreateTimeEvent( AFTER( REAL_SECOND( 1 ) ), "e_PlantGrowth", values, true );
			_SubItem( seed, 1 );
			cr.Animate( 0, ANIM2_PICKUP, null, false, true );
			TryRaiseSkill( cr );
			
			//plant_bed.SetEvent( ITEM_EVENT_FINISH, "furniture@_DeleteBlockers" );
		}
	}
}

void PlantHydro( Critter& cr, Item& seed, Item& plant_bed )
{
	if( valid( seed ) && valid( plant_bed ) )
	{
		Plant@ plant = GetPlant( seed, plant_bed );
		if( valid( plant ) )
		{
			plant_bed.PLANT_BED_DNA = plant_bed.GetProtoId();
			plant_bed.ChangeProto( plant.pid );
			
			/*Map@ map = cr.GetMap();
			uint16 hx = plant_bed.HexX, hy = plant_bed.HexY;
			map.MoveHexByDir( hx, hy, 0, 1 );
			Item@ blocker0 = map.AddItem( hx, hy, PID_METAL_BLOCKER, 1 );
			if( valid( blocker0 ) )
			{
				blocker0.PARENT_ITM_ID = plant_bed.Id;
			}
			
			map.MoveHexByDir( hx, hy, 2, 1 );
			Item@ blocker1 = map.AddItem( hx, hy, PID_METAL_BLOCKER, 1 );
			if( valid( blocker1 ) )
			{
				blocker1.PARENT_ITM_ID = plant_bed.Id;
			}
			
			map.MoveHexByDir( hx, hy, 3, 1 );
			map.MoveHexByDir( hx, hy, 4, 1 );
			Item@ blocker2 = map.AddItem( hx, hy, PID_METAL_BLOCKER, 1 );
			if( valid( blocker2 ) )
			{
				blocker2.PARENT_ITM_ID = plant_bed.Id;
			}
			
			map.MoveHexByDir( hx, hy, 5, 1 );
			Item@ blocker3 = map.AddItem( hx, hy, PID_METAL_BLOCKER, 1 );
			if( valid( blocker3 ) )
			{
				blocker3.PARENT_ITM_ID = plant_bed.Id;
			}*/
			
			uint[] values = { plant_bed.Id };
			plant_bed.PLANT_EVENT = CreateTimeEvent( AFTER( REAL_SECOND( 1 ) ), "e_PlantGrowth", values, true );
			_SubItem( seed, 1 );
			cr.Animate( 0, ANIM2_PICKUP, null, false, true );
			TryRaiseSkill( cr );
			
			//plant_bed.SetEvent( ITEM_EVENT_FINISH, "furniture@_DeleteBlockers" );
		}
	}
}

void PlantBarrel( Critter& cr, Item& seed, Item& plant_bed )
{
	if( valid( seed ) && valid( plant_bed ) )
	{
		Plant@ plant = GetPlant( seed, plant_bed );
		if( valid( plant ) )
		{
			plant_bed.PLANT_BED_DNA = plant_bed.GetProtoId();
			plant_bed.ChangeProto( plant.pid );
			uint[] values = { plant_bed.Id };
			plant_bed.PLANT_EVENT = CreateTimeEvent( AFTER( REAL_SECOND( 1 ) ), "e_PlantGrowth", values, true );
			_SubItem( seed, 1 );
			cr.Animate( 0, ANIM2_PICKUP, null, false, true );
			TryRaiseSkill( cr );
		}
	}
}

uint e_PlantGrowth( uint[]@ values )
{
	Item@ seed = GetItem( values[0] );
	if( valid( seed ) )
	{
		Plant@ plant = GetPlantByPid( seed );
		if( !valid( plant ) )
		{
			return 0;
		}
		
		IncreaseCrop( seed, 1 );
		
		if( plant.type != PLANT_TYPE_MUSHBED )
		{ 
			if( seed.PLANT_GROW_STAGE == 1 && seed.PLANT_WATERING == 0 )
			{
				seed.PLANT_GROW_STAGE = plant.deathStage - 1;
			}
		}
		
		if( uint( seed.PLANT_GROW_STAGE ) < plant.deathStage )
		{
			seed.PLANT_GROW_STAGE ++;
			ChangeFrame( seed, seed.PLANT_GROW_STAGE - 1 );
			uint time = uint( seed.PLANT_GROW_STAGE ) == plant.gatheringStage ? 24 : 6;
			
			if( plant.type == PLANT_TYPE_MUSHBED )
			{
				if( seed.PLANT_WATERING == 0 )
				{
					time *= 2;
				}
				seed.PLANT_WATERING = 0;
				seed.PLANT_FERTILITY = 0;
			}
			
			uint time2 = ( time - 1 ) * 60;
			time *= 60;
			return REAL_MINUTE( Random( time, time2 ) );
		}
	}

	return 0;
}

bool StartMenuPlant( Critter& cr, Item& plant )
{
    Map@ map = cr.GetMap();
    if( !valid( map ) )
	{
        return false;
    }

    iMenuHandler@ handler = MenuPlant( plant, map );
    iDialogBox@ menu = OpenMenu( cr, "Farming", handler );
	return true;
}

class MenuPlant: CenteredMenuHandler 
{
    uint plant_id;
    uint map_id;
	
    MenuPlant ( Item& plant, Map& map )
	{
        plant_id = plant.Id;
        map_id = map.Id;
    }

    bool MenuUpdate( Critter& cr, iDialogBox& menu )
	{
        Map@ map = GetMap( map_id );
        Item@ plant = GetItem( plant_id );
		Plant@ cplant = GetPlantByPid( plant );
		if( !valid( cplant ) )
		{
			return false;
		}
		
		if( plant.PLANT_WATERING <= 0 )
		{
			if( menu.ButtonMsg( STR_PLANTS_MENU_WATER ) )
			{
				Item@ hand = _CritGetItemHand( cr );
				if( valid( hand ) )
				{
					TryWaterPlant( cr, hand, plant );
					return true;
				}
				else
				{
					cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_PLANTS_HOLD_A_WATER_CONTAINER );
					return false;
				}
			}
		}
		
		if( plant.PLANT_FERTILITY <= 0 )
		{	
			if( menu.ButtonMsg( STR_PLANTS_MENU_FERTILIZE ) )
			{
				if( hasItems( cr, fertilizers ) )
				{
					Item@[] fertilizer = getItems( cr, fertilizers );
					FurtilezePlant( cr, fertilizer[0], plant );
					return true;
				}
				
				cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_PLANTS_YOU_REQUIRE_FERTILIZERS );
				return true;
			}	
		}
		
		uint16 lootPid;
		uint amount = plant.PLANT_RESOURCE_CAPACITY;
		
		if( menu.ButtonMsg( STR_PLANTS_MENU_GATHER ) )
		{
			switch( cplant.type )
			{
				case( PLANT_TYPE_FARM ):
				{
					if( uint( plant.PLANT_GROW_STAGE ) == cplant.seedStage )
					{
						lootPid = cplant.seed[ 0 ];
						if( cplant.pid == PID_PLANT_CORN || cplant.pid == PID_PLANT_FIVELEAF )
						{
							cr.AddItem( cplant.loot[ cplant.loot.length() - 1 ], 1 );
							cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_YOU_GATHERED, "$proto@msg item " + ( cplant.loot[ cplant.loot.length() - 1 ] * 100 ) + "@$amount" + 1 );
						}
					}
					else if( uint( plant.PLANT_GROW_STAGE ) == cplant.gatheringStage )
					{
						lootPid = cplant.loot[ 0 ];
						if( cplant.pid == PID_PLANT_CORN || cplant.pid == PID_PLANT_FIVELEAF )
						{
							cr.AddItem( cplant.loot[ cplant.loot.length() - 1 ], 1 );
							cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_YOU_GATHERED, "$proto@msg item " + ( cplant.loot[ cplant.loot.length() - 1 ] * 100 ) + "@$amount" + 1 );
						}
					}
					else
					{
						lootPid = cplant.loot[ cplant.loot.length() - 1 ];
						amount = 1;
					}
					
					DeleteItem( plant );
					break;
				}
				case( PLANT_TYPE_MUSHBED ):
				{
					if( plant.PLANT_GROW_STAGE > 1 )
					{
						lootPid = cplant.seed[ plant.PLANT_GROW_STAGE - 2 ];
						CleanBed( plant );
					}
					else
					{
						cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_PLANTS_THERE_IS_NOTHING_TO_GATHER );
						return true;
					}
					break;
				}
				case( PLANT_TYPE_HYDRO ):
				{
					if( plant.PLANT_GROW_STAGE > 1 )
					{
						if( uint( plant.PLANT_GROW_STAGE ) == cplant.seedStage )
						{
							lootPid = cplant.seed[ 0 ];
							if( cplant.pid == PID_HYDRO_NS_CORN || cplant.pid == PID_HYDRO_EW_CORN )
							{
								cr.AddItem( cplant.loot[ cplant.loot.length() - 1 ], 1 );
								cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_YOU_GATHERED, "$proto@msg item " + ( cplant.loot[ cplant.loot.length() - 1 ] * 100 ) + "@$amount" + 1 );
							}
						}
						else if( uint( plant.PLANT_GROW_STAGE ) == cplant.gatheringStage )
						{
							lootPid = cplant.loot[ 0 ];
							if( cplant.pid == PID_HYDRO_NS_CORN || cplant.pid == PID_HYDRO_EW_CORN )
							{
								cr.AddItem( cplant.loot[ cplant.loot.length() - 1 ], 1 );
								cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_YOU_GATHERED, "$proto@msg item " + ( cplant.loot[ cplant.loot.length() - 1 ] * 100 ) + "@$amount" + 1 );
							}
						}
						else if( cplant.pid == PID_HYDRO_NS_CORN || cplant.pid == PID_HYDRO_EW_CORN || cplant.pid == PID_HYDRO_EW_CAB || cplant.pid == PID_HYDRO_NS_CAB )
						{
							lootPid = cplant.loot[ cplant.loot.length() - 1 ];
							amount = 1;
						}
						else
						{
							cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_WRONG_PLANT_STAGE );
							amount = 0;
						}
					}
					else
					{
						cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_PLANTS_THERE_IS_NOTHING_TO_GATHER );
						return true;
					}
					
					break;
				}
				case( PLANT_TYPE_BARREL ):
				{
					if( plant.PLANT_GROW_STAGE > 1 )
					{
						if( uint( plant.PLANT_GROW_STAGE ) == cplant.seedStage )
						{
							lootPid = cplant.seed[ 0 ];
							if( cplant.pid == PID_PLANTBARREL_CORN || cplant.pid == PID_PLANTBARREL_FIVELEAF )
							{
								cr.AddItem( cplant.loot[ cplant.loot.length() - 1 ], 1 );
								cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_YOU_GATHERED, "$proto@msg item " + ( cplant.loot[ cplant.loot.length() - 1 ] * 100 ) + "@$amount" + 1 );
							}
						}
						else if( uint( plant.PLANT_GROW_STAGE ) == cplant.gatheringStage )
						{
							lootPid = cplant.loot[ 0 ];
							if( cplant.pid == PID_PLANTBARREL_CORN || cplant.pid == PID_PLANTBARREL_FIVELEAF )
							{
								cr.AddItem( cplant.loot[ cplant.loot.length() - 1 ], 1 );
								cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_YOU_GATHERED, "$proto@msg item " + ( cplant.loot[ cplant.loot.length() - 1 ] * 100 ) + "@$amount" + 1 );
							}
						}
						else if( cplant.pid == PID_PLANTBARREL_CORN || cplant.pid == PID_PLANTBARREL_CAB || cplant.pid == PID_PLANTBARREL_RAD || cplant.pid == PID_PLANTBARREL_FIVELEAF )
						{
							lootPid = cplant.loot[ cplant.loot.length() - 1 ];
							amount = 1;
						}
						else
						{
							cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_WRONG_PLANT_STAGE );
							amount = 0;
						}
					}
					else
					{
						cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_PLANTS_THERE_IS_NOTHING_TO_GATHER );
						return true;
					}
					
					break;
				}
				default:
					return false;
			}

			if( amount > 0 )
			{
				Item@ loot = cr.AddItem( lootPid, amount );
				cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_YOU_GATHERED, "$proto@msg item " + ( lootPid * 100 ) + "@$amount" + amount );
				ChangeCritterSpeed( cr );
			}
		}
		
		return true;
    }
	
    string@ Description( Critter& cr )
	{
		Item@ plant = GetItem( plant_id );
		Plant@ cplant = GetPlantByPid( plant );

		string stage = "";
		if( uint( plant.PLANT_GROW_STAGE ) == cplant.seedStage )
		{
			stage = STR_INSERT_TEXT_LINE( STR_PLANTS_MENU_DESC_STAGE_SEED );
		}
		else if( uint( plant.PLANT_GROW_STAGE ) == cplant.gatheringStage || ( cplant.type == PLANT_TYPE_MUSHBED && uint( plant.PLANT_GROW_STAGE ) == cplant.deathStage ) )
		{
			stage = STR_INSERT_TEXT_LINE( STR_PLANTS_MENU_DESC_STAGE_GATHERING );
		}
		else if( uint( plant.PLANT_GROW_STAGE ) == cplant.deathStage )
		{
			stage = STR_INSERT_TEXT_LINE( STR_PLANTS_MENU_DESC_STAGE_DEAD );
		}
		else
		{
			stage = STR_INSERT_TEXT_LINE( STR_PLANTS_MENU_DESC_STAGE_GROWING );
		}
		
	   return "$notWaterinedPlant" + ( plant.PLANT_WATERING > 0 ? "" : STR_INSERT_TEXT_LINE( STR_NOT ) )
			  + "$notFertilisedPlant" + ( plant.PLANT_FERTILITY > 0 ? "" : STR_INSERT_TEXT_LINE( STR_NOT ) )
			  + "$stagePlant" + stage 
			  +" $capacityPlant" + plant.PLANT_RESOURCE_CAPACITY;
    }
	
	int getDescriptionFile()
    {
    	return TEXTMSG_TEXT;
    }

	int getDescriptionLine()
	{
		return STR_PLANTS_MENUPLANT_DESC;
	}
}

bool FurtilezePlant( Critter& cr, Item& fertilizer, Item& plant )
{
	if( plant.PLANT_FERTILITY > 0 )
	{
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_PLANTS_ENOUGH_FERTILIZER );
		return false;
	}
	
	plant.PLANT_FERTILITY ++;
	cr.SayMsg( SAY_EMOTE_ON_HEAD, TEXTMSG_TEXT, STR_PLANTS_FERTILIZES_THE_PLANT );
	cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_PLANTS_YOU_FERTILIZED_THE_PLANT );
	_SubItem( fertilizer, 1 );
	TryRaiseSkill( cr );
	int amount = 0;
	amount += ( cr.Skill[ SK_FARMING ] / 50 );
	IncreaseCrop( plant, amount );
	return true;
}

bool TryWaterPlant( Critter& cr, Item& water, Item& plant )
{
	if( !valid( water ) )
	{
		return false;
	}
	
	if( !valid( plant ) )
	{
		return false;
	}
	
	if( plant.PLANT_WATERING > 0 )
	{
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_PLANTS_ENOUGH_WATER );
		return false;
	}
	
	uint16 flaskPid = water.GetProtoId();
	if( FindFlask( water ) && ( water.DRINK_DNA == PID_GLASS_BOTTLE_FULL_PRT || water.DRINK_DNA == PID_DISTIL_WATER_PRT || water.DRINK_DNA == PID_GLASS_BOTTLE_DIRTY_FULL_PRT ) 
		|| flaskPid == PID_GLASS_BOTTLE_FULL_PRT || flaskPid == PID_DISTIL_WATER_PRT || flaskPid == PID_GLASS_BOTTLE_DIRTY_FULL_PRT
		&& water.REMAINING_USES > 0 )
	{
		WaterPlant( cr, water, plant );
		return true;
	}
	else
	{
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_PLANTS_HOLD_A_WATER_CONTAINER );
	}
	
	return false;
}

void WaterPlant( Critter& cr, Item& water, Item& plant )
{
	water.REMAINING_USES --;
	water.EXTRA_WEIGHT -= 100;
	water.Update();
	plant.PLANT_WATERING ++;
	cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_PLANTS_YOU_HAVE_WATERED_THE_PLANT );
	cr.SayMsg( SAY_EMOTE_ON_HEAD, TEXTMSG_TEXT, STR_PLANTS_WATERS_THE_PLANT );
	
	if( water.REMAINING_USES < 1 )
	{
		if( FindFlask( water ) )
		{
			water.DRINK_DNA = 0;
			water.REMAINING_USES = 0;
			water.EXTRA_WEIGHT = 0;
			ChangeFlaskProto( water );
		}
		else
		{
			_SubItem( water, 1 );
			cr.AddItem( PID_BOTTLE_GLASS, 1 );
		}
	}
	TryRaiseSkill( cr );
}

bool StartMenuPlantBed( Critter& cr, Item& plantbed )
{
    Map@ map = cr.GetMap();
    if( !valid( map ) )
	{
        return false;
    }

    iMenuHandler@ handler = MenuPlantBed( plantbed, map );
    iDialogBox@ menu = OpenMenu( cr, "Plant bed", handler );
	return true;
}

class MenuPlantBed: CenteredMenuHandler 
{
    uint plantbed_id;
    uint map_id;
	
    MenuPlantBed ( Item& plantbed, Map& map )
	{
        plantbed_id = plantbed.Id;
        map_id = map.Id;
    }

    bool MenuUpdate( Critter& cr, iDialogBox& menu )
	{
        Map@ map = GetMap( map_id );
        Item@ plantbed = GetItem( plantbed_id );
		
		if( plantbed.GetProtoId() == PID_PLANTBARREL_EMPTY && plantbed.SOIL_FERTILITY < 10 )
		{
			if( menu.Button( "Fertilize soil" ) )
			{
				if( hasItem( cr, PID_CRAFT_FERTILIZER ) )
				{
					Item@ soil_fertilizer = getItem( cr, PID_CRAFT_FERTILIZER );
					if( valid( soil_fertilizer ) )
					{
						plantbed.SOIL_FERTILITY = 10;
						_SubItem( soil_fertilizer, 1 );
					}
					return true;
				}
				else
				{
					cr.Say( SAY_NETMSG, "|0xFFFF00 You require a mineral fertilizer for the soil." );
					return false;
				}
			}
		}
		
		if( plantbed.PLANT_WATERING <= 0 && menu.ButtonMsg( STR_PLANTS_MENU_WATER ) )
		{
			Item@ hand = _CritGetItemHand( cr );
			if( valid( hand ) )
			{
				TryWaterPlant( cr, hand, plantbed );
				return true;
			}
			else
			{
				cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_PLANTS_HOLD_A_WATER_CONTAINER );
				return false;
			}
		}
		
		if( plantbed.PLANT_FERTILITY <= 0 && menu.ButtonMsg( STR_PLANTS_MENU_FERTILIZE ) )
		{
			if( hasItems( cr, fertilizers ) )
			{
				Item@[] fertilizer = getItems( cr, fertilizers );
				FurtilezePlant( cr, fertilizer[0], plantbed );
				return true;
			}
			cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_PLANTS_YOU_REQUIRE_FERTILIZERS );
			return true;
		}
		
		if( plantbed.PLANT_GROW_STAGE == 0 && plantbed.GetProtoId() != PID_HYDRO_EW_EMPTY && menu.ButtonMsg( STR_FURNITURE_MENU_BUTTON ) )
		{
			_InitFurniture( plantbed, true );
			plantbed.Update();
			StartMenuFuriture( cr, plantbed );
			return false;
		}
		
		return true;
    }
	
    string@ Description( Critter& cr )
	{
		Item@ plantbed = GetItem( plantbed_id );
		
        return "$soil_status" + ( plantbed.GetProtoId() == PID_PLANTBARREL_EMPTY ? ( plantbed.SOIL_FERTILITY > 0 ? STR_INSERT_TEXT_LINE( STR_PLANTS_FERTILE ) : STR_INSERT_TEXT_LINE( STR_PLANTS_BARREN ) ) :  STR_INSERT_TEXT_LINE( STR_PLANTS_FERTILE ) )
				+ "$notWatered" + ( plantbed.PLANT_WATERING > 0 ? "" : STR_INSERT_TEXT_LINE( STR_NOT ) )
				+ "$notFertilised" + ( plantbed.PLANT_FERTILITY > 0 ? "" : STR_INSERT_TEXT_LINE( STR_NOT ) );
    }
	
	int getDescriptionFile()
    {
    	return TEXTMSG_TEXT;
    }

	int getDescriptionLine()
	{
		return STR_PLANTS_MENUPLANTBED_DESC;
	}
}

void TryRaiseSkill( Critter& cr )
{
	if( Random( 1, 10 ) == 10 )
	{
		int skillNum = SK_FARMING;
		raiseSkill( cr, skillNum );
	}
}

void IncreaseCrop( Item& plant, int amount )
{
	for( int i = 0; i < amount ; i++ )
	{
		if( Random( 0, 1 ) == 1 )
		{
			plant.PLANT_RESOURCE_CAPACITY ++;
		}
	}
}

void CleanBed( Item& plant )
{
	ChangeFrame( plant, 0 );
	plant.ChangeProto( plant.PLANT_BED_DNA );

	plant.PLANT_GROW_STAGE = 0;
	plant.PLANT_BED_DNA = 0;
	if( plant.PLANT_EVENT != 0 )
	{
		EraseTimeEvent( plant.PLANT_EVENT );
		plant.PLANT_EVENT = 0;
	}
	
	plant.PLANT_RESOURCE_CAPACITY = 0;
	plant.PLANT_WATERING = 0;
	plant.PLANT_FERTILITY = 0;
	
	//CleanBlockers( plant );
}
/*
void CleanBlockers( Item& plant )
{
	if( !valid( plant ) )
	{
		return;
	}
	
	uint16 hx = 0, hy = 0;
	Map@ map = plant.GetMapPosition( hx, hy );
	Item@ blocker = null;
	
	if( plant.GetProtoId() == PID_MUSHBED_EMPTY )
	{
		map.MoveHexByDir( hx, hy, 0, 1 );
		@ blocker = map.GetItem( hx, hy, PID_WOOD_BLOCKER );
		if( valid( blocker ) )
		{
			DeleteItem( blocker );
		}
		
		map.MoveHexByDir( hx, hy, 2, 1 );
		@ blocker = map.GetItem( hx, hy, PID_WOOD_BLOCKER );
		if( valid( blocker ) )
		{
			DeleteItem( blocker );
		}
		
		map.MoveHexByDir( hx, hy, 3, 1 );
		map.MoveHexByDir( hx, hy, 4, 1 );
		@ blocker = map.GetItem( hx, hy, PID_WOOD_BLOCKER );
		if( valid( blocker ) )
		{
			DeleteItem( blocker );
		}
		
		map.MoveHexByDir( hx, hy, 5, 1 );
		@ blocker = map.GetItem( hx, hy, PID_WOOD_BLOCKER );
		if( valid( blocker ) )
		{
			DeleteItem( blocker );
		}
	}
}*/

#endif // PLANTS_MODULE