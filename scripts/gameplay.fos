#ifndef GAMEPLAY
#define GAMEPLAY

#include "_utils.fos"
#include "_global_events.fos"
#include "gathering_h.fos"
#include "wildplant_h.fos"
#include "waterworks_h.fos"
#include "local_population_h.fos"
#include "map_loot_h.fos"
#include "radiation_h.fos"
#include "drugs_h.fos"
#include "poison_h.fos"
#include "effects_h.fos"
#include "speed_h.fos"
#include "gm_h.fos"
#include "rotting_h.fos"
#include "time_h.fos"
#include "globalmap_group_h.fos"
#include "weather_h.fos"
#include "entire_h.fos"
#include "gasses_h.fos"

import uint CountMapPlayers( Map@ map ) from "manager";
import void _DoorsInit( Critter& player, int param0, int param1, int param2 ) from "gateway";
import void _ForceArenaInit( Critter& player, int param0, int param1, int param2 ) from "map_arena";

bool SwitchGameMode( Critter& cr, int mode )
{
    if( cr.Param[ QST_GAMEMODE ] != GAME_START )
    {
        Log( "Cant change game mode for " + GetPlayerName( cr.Id ) + " (" + cr.Id + ")" );
        return false;
    }
    if( mode <= GAME_START || mode >= GAME_MAX )
    {
        Log( "Wrong target game mode: " + GetPlayerName( cr.Id ) + " (" + cr.Id + ")" );
        return false;
    }

    cr.ParamBase[ QST_GAMEMODE ] = mode;
    return true;
}

bool IsInGame( Critter& cr )
{
    return ( cr.WorldX < 350 || cr.WorldY < 600 );
}

class Gameplay : iManagerModule, iManager_loop,
      iManager_critter_init, iManager_critter_finish,
      iManager_critter_idle, iManager_critter_dead, iManager_critter_respawn,
      iManager_map_critter_in, iManager_map_critter_out,
      iManager_world_save, iManager_time  // , iManager_player_registration, iManager_player_login, iManager_time
{
    Gameplay()
    {
        AddStartCallback( "gameplay", "start" );
    }
    bool manager_init()
    {
        // called in priority
        return true;
    }
    uint global_loop()
    {
        return uint( -1 );
    }
	
    bool global_critter_init( Critter& cr, bool firstTime )
    {
        if( cr.IsNpc() )
		{
            return true;
		}
		
        if( firstTime )
        {
            cr.ParamBase[ QST_GAMEMODE ] = GAME_INVALID;

			for( uint y = 0; y < __GlobalMapHeight; y++ )
			{
				for( uint x = 0; x < __GlobalMapWidth; x++ )
				{
					cr.SetFog(x, y, FOG_NONE);
				}
			}
		}

        if( cr.Param[ QST_GAMEMODE ] == GAME_INVALID )
        {
            if( TeleportToStart( cr ) )
			{
				cr.ParamBase[ QST_GAMEMODE ] = GAME_START;
            }
			else
            {
				cr.Disconnect();
				return false;
            }
        }

        Map@ map = cr.GetMap();

        if( !IsInGame( cr ) )
        {
            if( cr.ParamBase[ QST_GAMEMODE ] > GAME_START && !valid(map) )
            {
                CreateModeStart( cr );
            }
        }

        AfterLogin( cr );

        return true;
    }
	
    bool global_critter_finish( Critter& cr, bool toDelete )
    {
        return true;
    }
	
    bool global_critter_idle( Critter& cr )
    {
		Map@ map = cr.GetMap();
		if( !valid( map ) )
		{
			return true;
		}
		
		if( cr.StatBase[ST_GLOBAL_OVERDOSE] > 0 && cr.GetTimeEvents( CTE_GLOBAL_OVERDOSE, null, null, null ) < 1 )
		{
			cr.AddTimeEvent( "cte_Global_Overdose", REAL_MS( cr.Param[ CR_GLOBAL_OVERDOSE_SPEED ] ), CTE_GLOBAL_OVERDOSE, 0 );				
		}

		uint16 mapPid = map.GetProtoId();
		if( cr.IsPlayer() )
		{
			if( cr.ParamBase[ QST_GAMEMODE ] == GAME_TEST )
			{
				cr.RunClientScript( "client_main@_testmode", cr.ParamBase[ CR_TEST_MODE ], 0, 0, null, null );
			}
			
			if( cr.ParamBase[ QST_GAMEMODE ] == GAME_START )
			{
				if( !valid( map ) || ( mapPid != MAP_UTILITY_START && mapPid != MAP_TLJ_ARENA1 && mapPid != MAP_TLJ_ARENA2 ) )
				{
					cr.ParamBase[ QST_GAMEMODE ] = GAME_SURVIVAL;
					if( !isGM( cr ) )
					{
						cr.SayMsg( SAY_EMOTE, TEXTMSG_TEXT, STR_EMOTE_NO_ACTIVATION );
						cr.ToDead( ANIM2_DEAD_EXPLODE, null );
					}
				}
			}
		}
		
		if( cr.IsDead() && cr.IsPlayer() )
		{
			if( valid( map ) )
			{
				switch( mapPid )
				{
					case( MAP_HELL ):	
					{
						GameVar@ canPray = GetGlobalVar( GVAR_Pray_Battleground );
						if( canPray.GetValue() != 0 )
						{
							Location@ location = GetLocationByPid( LOCATION_Deadloc, 0 );
							if( valid( location ) )
							{
								@map = location.GetMapByIndex( 0 );
								if( valid( map ) )
								{
									cr.TransitToMap( map.Id, 241 );
								}
							}
							
							cr.ToLife();
							FullHeal( cr );
							return true;
						}
						break;
					}
					default:
						break;
				}
			}
		}
		
        if( cr.IsDead() && cr.Stat[ ST_REPLICATION_TIME ] >= 0 && cr.Timeout[ TO_REPLICATION ] == 0 )
        {
            if( cr.IsPlayer() && !RespawnCritter( cr ) )
			{
                cr.TimeoutBase[ TO_REPLICATION ] = __FullSecond + REAL_MINUTE( 10 );
			}
		}

		if( cr.Stat[ ST_DYSPNEA ] < 1000 && cr.GetTimeEvents( CTE_DYSPNEA, null, null, null ) < 1 )
		{
			DypsneaCr( cr, 0 );
		}
		
		if( cr.Stat[ ST_POISONING_LEVEL ] > 0 && cr.GetTimeEvents( CTE_POISON, null, null, null ) < 1 )
		{
			cr.AddTimeEvent( "poison@cte_Poison", REAL_SECOND( 5 ), CTE_POISON );
		}
		
        return true;
    }
	
    bool global_critter_dead( Critter& cr, Critter@ killer )
    {
        Map@ map = cr.GetMap();
        if( !valid( map ) )
        {
            Log( "Error! Death map is null" );
            return false;
        }
		
		if( cr.IsNpc() )
		{
			if( cr.ParamBase[ MERC_MASTER_ID ] != 0 )
			{
				Critter@ owner = GetCritter( cr.ParamBase[ MERC_MASTER_ID ] );
				if( valid( owner ) )
				{
					owner.RunClientScript( "client_gui@_LostCritterControl", cr.Id, 0, 0, null, null );
					cr.ParamBase[ MERC_MASTER_ID ] = 0;
					cr.ParamBase[ CR_CONTROLLED_GROUP ] = 0;
				}
			}
		}

		if( cr.Stat[ ST_DYSPNEA ] == 0 )
		{
			CrimeLog( cr, crInfo( cr ) + " suffocated." );
			Log_Killings( cr, cr.Id, 0, 0, null, null );
		}

		cr.ParamBase[ CR_IS_WINDUPED ] = 0;
		cr.ParamBase[ CR_IS_RELAXING ] = 0;
		cr.ParamBase[ CR_IS_RELOADING ] = 0;

		old_drop_stuff_if_bluddy( cr, map );

        int replTime = cr.Stat[ ST_REPLICATION_TIME ];
        if( cr.Stat[ ST_BODY_TYPE ] == BT_ROBOT && cr.IsPlayer() )
		{
            replTime = -1;
		}
		
        if( replTime < 0 )
        {
            if( cr.StatBase[ ST_BODY_TYPE ] > 0 )
            {
                ChangeMonsterCount( cr, 1, false );
            }
            return true;
        } // No respawn

        if( replTime == 0 ) // Take default values
        {
            if( cr.IsPlayer() )
            {
                if( cr.Anim2Dead <= ANIM2_DEAD_BURST )
				{
                    replTime = REAL_MINUTE( 60 );
				}
                else
				{
                    replTime = REAL_MINUTE( 60 );
				}
			}
            else
            {
				replTime = Random( 30, 60 );
				replTime *= REAL_MINUTE( 1 );
            }
        }
        else
        {
            replTime *= REAL_MINUTE( 1 );
        }

		if( cr.IsPlayer() )
		{
			if( map.GetProtoId() == MAP_TLJ_ARENA1 || map.GetProtoId() == MAP_TLJ_ARENA2 )
			{
				cr.TimeoutBase[ TO_REPLICATION ] = __FullSecond + REAL_MINUTE( 1 );
			}
			else
			{
				cr.TimeoutBase[ TO_REPLICATION ] = __FullSecond + replTime;
			}
		}
		
		if( cr.IsNpc() )
		{
			cr.AddTimeEvent( "rotting@cte_rotten", 0, CTE_ROTTEN, 0 );
		}
		
        return true;
    }

    bool RespawnCritter( Critter& cr )
    {
		return _RespawnCritter( cr );
    }

    bool global_critter_respawn( Critter& cr )
    {
        return true;
    }
	
    bool global_map_critter_in( Map& map, Critter& cr )
    {
		if( cr.IsNpc() )
		{
			return true;
		}
		cr.RunClientScript( "client_main@updateMapId", map.Id, 0, 0, null, null );
		
        return true;
    }
	
    bool global_map_critter_out( Map& map, Critter& cr )
    {
		if( cr.IsNpc() )
		{
			return true;
		}		
        return true;
    }
	
    bool global_world_save()
    {
        return true;
    }
	
    bool global_player_registration( uint ip, string& name, uint& textMsg, uint& strNum )
    {
        return true;
    }
	
    bool global_player_login( uint ip, string& name, uint id, uint& textMsg, uint& strNum )
    {
        Critter@ cr = GetCritter( id );
        if( valid(cr) )
		{
            AfterLogin( cr );
        }
		return true;
    }
	
    void AfterLogin( Critter& cr )
	{
		if( !cr.IsPlayer() )
		{
			return;
		}
		
		Item@[] shadows;
		Map@ crMap = cr.GetMap();
		if( valid( crMap ) )
		{
			crMap.GetItems( PID_SHADOW, shadows );
			if( shadows.length() > 0 )
			{
				for( uint i = 0; i < shadows.length(); i ++ )
				{
					Item@ shadow = shadows[i];
					if( valid( shadow ) && shadow.Val0 == int( cr.Id ) )
					{
						DeleteItem( shadow );
					}
				}
			}
		}
		
		if( cr.StatBase[ST_CARRY_WEIGHT_EXT] != 0 )
		{
			LogInfoTime( "logs\\debug.txt", crInfo( cr ) + " extra waight bonus is 0: " + cr.StatBase[ST_CARRY_WEIGHT_EXT] + " / " +  cr.Stat[ST_CARRY_WEIGHT] + "." );
			cr.StatBase[ST_CARRY_WEIGHT_EXT] = 0;
		}

		//Skills trained by use
		if( cr.SkillBase[ SK_MINING ] == 0 )
		{
			cr.SkillBase[ SK_MINING ] += 0 + 5 * cr.StatBase[ ST_STRENGTH ] + 4 * cr.StatBase[ ST_AGILITY ] + 3 * cr.StatBase[ ST_PERCEPTION ] + 2 * cr.StatBase[ ST_INTELLECT ];
		}
		
		if( cr.SkillBase[ SK_CHOPPING ] == 0 )
		{
			cr.SkillBase[ SK_CHOPPING ] += 0 + 5 * cr.StatBase[ ST_STRENGTH ] + 4 * cr.StatBase[ ST_AGILITY ] + 3 * cr.StatBase[ ST_PERCEPTION ] + 2 * cr.StatBase[ ST_INTELLECT ];
		}
		
		if( cr.SkillBase[ SK_SCAVENGING ] == 0 )
		{
			cr.SkillBase[ SK_SCAVENGING ] += 0 + 6 *cr.StatBase[ ST_LUCK ] + 5 * cr.StatBase[ ST_INTELLECT ] + 4 * cr.StatBase[ ST_PERCEPTION ] + 3 * cr.StatBase[ ST_AGILITY ] + 2 * cr.StatBase[ ST_STRENGTH ];
		}
		
		if( cr.SkillBase[ SK_HUNTING ] == 0 )
		{
			cr.SkillBase[ SK_HUNTING ] += 0 + 5 * cr.StatBase[ ST_AGILITY ] + 4 * cr.StatBase[ ST_INTELLECT ] + 3 * cr.StatBase[ ST_PERCEPTION ] + 2 * cr.StatBase[ ST_STRENGTH ];
		}

		if( cr.SkillBase[ SK_FISHING ] == 0 )
		{
			cr.SkillBase[ SK_FISHING ] += 0 + 6 *cr.StatBase[ ST_LUCK ] + 5 * cr.StatBase[ ST_PERCEPTION ] + 4 * cr.StatBase[ ST_AGILITY ] + 3 * cr.StatBase[ ST_INTELLECT ] + 2 * cr.StatBase[ ST_STRENGTH ];
		}
		
		if( cr.SkillBase[ SK_FARMING ] == 0 )
		{
			cr.SkillBase[ SK_FARMING ] += 0 + 5 * cr.StatBase[ ST_INTELLECT ] + 4 * cr.StatBase[ ST_PERCEPTION ] + 3 * cr.StatBase[ ST_STRENGTH ] + 2 * cr.StatBase[ ST_AGILITY ];
		}
		
		if( cr.SkillBase[ SK_COOKING ] == 0 )
		{
			cr.SkillBase[ SK_COOKING ] += 0 + 5 * cr.StatBase[ ST_INTELLECT ] + 4 * cr.StatBase[ ST_PERCEPTION ] + 3 * cr.StatBase[ ST_AGILITY ];
		}
		
		if( cr.SkillBase[ SK_HERBALISM ] == 0 )
		{
			cr.SkillBase[ SK_HERBALISM ] += 0 + 5 * cr.StatBase[ ST_INTELLECT ] + 4 * cr.StatBase[ ST_PERCEPTION ] + 3 * cr.StatBase[ ST_AGILITY ];
		}
		
		if( isGM( cr ) )
		{
			GameVar@ autoOffOldGMs = GetGlobalVar( GVAR_autoOffOldGMs );		
			if( autoOffOldGMs.GetValue() != 0 )
			{
				cr.StatBase[ ST_ACCESS_LEVEL ] = 0;
			}
		}
		
		if( cr.StatBase[ ST_BODY_TYPE ] != BT_ROBOT )
		{
			if( cr.GetTimeEvents( CTE_THIRST, null, null, null ) < 1 )
			{
				cr.AddTimeEvent( "cte_Thirst", REAL_SECOND( cr.Param[ CR_THIRST_SPEED ] ), CTE_THIRST, 0 );
			}
			
			if( cr.GetTimeEvents( CTE_HUNGER, null, null, null ) < 1 )
			{
				cr.AddTimeEvent( "cte_Hunger", REAL_SECOND( cr.Param[ CR_HUNGER_SPEED ] ), CTE_HUNGER, 0 );
			}
			
			if( cr.StatBase[ ST_DRUNK ] > 0 && cr.GetTimeEvents( CTE_DRUNK, null, null, null ) < 1 )
			{
				cr.AddTimeEvent( "cte_Drunk", REAL_SECOND( cr.Param[ CR_DRUNK_SPEED ] ), CTE_DRUNK, 0 );
			}
		}		
		
		if( cr.GetTimeEvents( CTE_DIRT, null, null, null ) < 1 )
		{
			cr.AddTimeEvent( "cte_Dirt", 0, CTE_DIRT, 0 );			
		}

		if( cr.StatBase [ ST_WETNESS ] > 0 && cr.GetTimeEvents( CTE_WETNESS, null, null, null ) < 1 )
		{
			cr.AddTimeEvent( "cte_Wetness", REAL_SECOND( cr.Param[ CR_WETNESS_SPEED ] ), CTE_WETNESS, 0);
		}
		
		RottenFood( cr );
		
		if( cr.StatBase[ ST_UNSPENT_PERKS ] > 0 )
		{
			cr.StatBase[ ST_UNSPENT_PERKS ] = 0;
		}
		
		uint[] durations;
		if( cr.GetTimeEvents( PID_PILLS, null, durations, null ) > 0 )
		{
			cr.TimeoutBase[ TO_PILLS ] = __FullSecond + durations[0];
		}

	}

    bool global_time( int8 type )
    {
        Map@[] maps;
		Map@ mapM = null;
		Map@ mapH = null;
        uint mapsLength = GetAllMaps( 0, maps );
		uint[] ignored_map_pids = { MAP_UTILITY_START, MAP_DREAM, MAP_HEAVEN, MAP_HELL };
		
		if( type == TIME_NEW_MINUTE )
		{
			for( uint i = 0; i < mapsLength; i++ )
			{
				@ mapM = maps[i];
				if( !valid( mapM ) )
				{
					continue;
				}
			
				if( ignored_map_pids.find( mapM.GetProtoId() ) != -1 )
				{
					continue;
				}

				if( CountMapPlayers( mapM ) == 0 )
				{
					continue;
				}
				
				/*uint8 rainIntensity = mapM.GetRain();
				if( rainIntensity > 0 )
				{
					Critter@[] critters;
					Critter@ crit = null;
					if( mapM.GetCritters( 0, FIND_LIFE | FIND_ONLY_PLAYERS, critters ) > 0 )
					{
						for( uint j = 0, len = critters.length(); j < len; j++ )
						{
							@ crit = critters[j];
							if( valid( crit ) )
							{
								if( !mapM.HasRoof( crit.HexX, crit.HexY ) )
								{
									applyWetness( crit );

									if( crit.Param[ CR_DIRTINESS ] >= 30 )
									{
										uint8 dirt = crit.Param[ CR_DIRTINESS ];
										crit.ParamBase[ CR_DIRTINESS ] -= MIN( ( rainIntensity / 17 ), dirt );
									}
								}
							}
						}
					}
					critters.resize(0);
				}*/
			}
		}

        if( type == TIME_NEW_HOUR )
        {
           	GameVar@ cycle = GetGlobalVar( GVAR_rotten_cycle );
			if( valid( cycle ) ) 
			{
				if( cycle.GetValue() < 999 )
				{ 
					cycle.opAddAssign( 1 );
				}
				else
				{
					cycle.opAssign( 0 );
				}
			}

			for( uint i = 0; i < mapsLength; i++ )
			{
				@ mapH = maps[i];
				if( !valid( mapH ) )
				{
					continue;
				}

				if( ignored_map_pids.find( mapH.GetProtoId() ) != -1 )
				{
					continue;
				}

				RottenFood( mapH );
				RottenCrittersFoodOnMap( mapH );
				MapHourTimeLoop( mapH );
			}
		}

        if( type == TIME_NEW_DAY )       // 142
        {
			MapLootInit();
			OreInit();
			JunkInit();
			WildplantInit();
			MobsSpawnInit();
			CutTreeManager();
        }
        return true;
    }
}

bool _RespawnCritter( Critter& cr )
{
	Map@ dieMap = cr.GetMap();

	if( cr.IsNpc() )
	{
		return respawn_npc( cr, dieMap );
	}

	if( cr.Stat[ ST_BODY_TYPE ] == BT_ROBOT )
	{
		return false;
	}
	
	if( dieMap.GetProtoId() == MAP_TLJ_ARENA1 || dieMap.GetProtoId() == MAP_TLJ_ARENA2 )
	{
		int entires = dieMap.CountEntire( ARENA_PLAYER_SPAWN );
		if( entires <= 0 )
		{
			return false;
		}
		else
		{
			uint16 hx = 0, hy = 0;
			dieMap.GetEntireCoords( ARENA_PLAYER_SPAWN, Random( 0, entires - 1 ), hx, hy );
			cr.TransitToHex( hx, hy, cr.Dir );
			cr.ToLife();
			FullHeal( cr );
			ChangeCritterSpeed( cr );
			cr.RunClientScript( "client_main@__ChosenRefreshMap", 0, 0, 0, null, null );
			
			if( !cr.IsNpc() && cr.Stat[ ST_LEVEL ] != 12 )
			{
				cr.StatBase[ ST_EXPERIENCE ] += 100000;
			}
			
			cr.ParamBase[ CR_DEATH_STAGE ] = 0;
			RefreshClientsMap( dieMap );
			return true;
		}
	}
	
	int GM = cr.Param[ QST_GAMEMODE ];

	uint16 hx0 = cr.HexX, hy0 = cr.HexY, hx = 0, hy = 0;
	uint8 condition = cr.Anim2Dead;
	uint dir = condition == ANIM2_DEAD_FRONT ? ( cr.Dir + 3 ) % 6 : cr.Dir;
	Map@ map = null;
	
	if( GM == GAME_SURVIVAL || GM == GAME_ADVENTURE )
	{
		Location@ loc = GetLocationByPid( LOCATION_Deadloc, 0 );

		if( !valid(loc) )
		{
			uint locid = CreateLocation( LOCATION_Deadloc, cr.WorldX, cr.WorldY, null );
			@loc = GetLocation( locid );

			if( not valid( loc ) )
			{
				Log( "can't create dead loc" );
				return false;
			}
		}

		@map = loc.GetMapByIndex( 0 );

		if( not valid( map ) )
		{
			Log( "can't get dead map" );
			return false;
		}

		if( !cr.TransitToMap( map.Id, 241 ) )
		{	// Continue dead
			cr.TimeoutBase[ TO_REPLICATION ] = __FullSecond + REAL_MINUTE( 1 );
			return true;
		}
	}
	else
	{
		@map = @dieMap;
		hx = cr.HexX;
		hy = cr.HexY;
		if( not map.IsHexPassed( hx, hy ) )
		{
			bool found = false;
			for( int x = -1; x <= 1; x++ )
			{
				for( int y = -1; y <= 1; y++ )
				{
					if( x == 0 && y == 0 )
					{
						continue;	// Skip direct position
					}
					if( ( hx % 2 ) == 1 && ( ( x == -1 && y == 1 ) || ( x == 1 && y == 1 ) ) )
					{
						continue;
					}
					if( ( hx % 2 ) == 0 && ( ( x == -1 && y == -1 ) || ( x == 1 && y == -1 ) ) )
					{
						continue;
					}
					
					if( map.IsHexPassed( hx + x, hy + y ) )
					{
						hx += x;
						hy += y;
						found = true;
						break;
					}
				}
				if( found )
				{
					break;
				}
			}

			if( not found )
			{
				// Continue dead
				cr.TimeoutBase[ TO_REPLICATION ] = __FullSecond + REAL_MINUTE( 1 );
				return true;
			}
		}
		cr.TransitToMap( map.Id, hx, hy, Random( 0, 5 ) );
	}

	if( _CritCanDropItemsOnDead( cr ) )
	{
		Item@[] items;
		cr.GetItems( -1, items );
		// Disable drop of hidden items
		for( uint i = 0, j = items.length(); i < j; i++ )
		{
			if( FLAG( items[ i ].Flags, ITEM_HIDDEN ) )
			{
				@items[ i ] = null;
			}
		}
		
		Item@ skeleton;
		if( condition < ANIM2_DEAD_PULSE )
		{
			@skeleton = dieMap.AddItem( hx0, hy0, PID_VAULT_DWELLER_BONES, 1 );
		}
		
		if( valid( skeleton ) )
		{
			skeleton.Val0 = cr.StatBase[ ST_BODY_TYPE ];
			skeleton.Val1 = cr.Id;
			skeleton.Val2 = cr.StatBase[ ST_AGE ];
			skeleton.Val3 = cr.StatBase[ ST_GENDER ];
			skeleton.Val4 = cr.StatBase[ ST_CHARISMA ];
			skeleton.Val5 = cr.StatBase[ ST_STRENGTH ];
			skeleton.Val6 = cr.StatBase[ ST_ENDURANCE ];
			skeleton.Val7 = cr.StatBase[ ST_AGILITY ];
			
			string @crLex = cr.GetLexems();
			if( valid( crLex ) ) 
			{
				crLex = crLex;
				setLexDesc( skeleton, crLex );
				setOverridenDesc( skeleton, true );
			}
			
			MoveItems( items, skeleton, 0 );
			uint[] values = { dieMap.Id, skeleton.Id };
			CreateTimeEvent( __FullSecond + REAL_MINUTE( 180 ), "e_deleteBody", values, true );
			
			setCorpsePicMap( skeleton, dir );
		}
		else
		{
			MoveItems( items, dieMap, hx0, hy0 );
		}
	}

	cr.ToLife();
	cr.StatBase[ ST_CURRENT_HP ] = ( cr.Stat[ ST_MAX_LIFE ] + cr.Stat[ ST_MAX_LIFE_EXT ] );

	return true;
}

//~run gameplay RespawnSelfAtCorpse 0 0 0
void RespawnSelfAtCorpse( Critter& cr, int, int, int )
{
	cr.ToDead( 0, null );
	
	Map@ map = cr.GetMap();
	uint x = cr.HexX;
	uint y = cr.HexY;
	uint dir = cr.Dir;
	_RespawnCritter( cr );
	cr.TransitToMap( map.Id, x, y, dir );
}

bool setCorpsePicMap( Item& item, uint dir )
{
	if( item.GetProtoId() != PID_VAULT_DWELLER_BONES )
	{
		return false;
	}
	
	item.Val0 = dir;
	item.PicMap = GetStrHash( "art\\items\\misc\\corpse\\corpse" + dir + ".png" );
	item.Update();
	
	return true;
}

//~run gameplay StartServer 0 0 0
void StartServer( Critter& cr, int p0, int p1, int p2 )
{
	RespawnWorld( cr, p0, p1, p2 );
	_DoorsInit( cr, p0, p1, p2 );
	_ForceArenaInit( cr, p0, p1, p2 );
	///smoke global timer
	///fire global timer
	cr.Say( SAY_NETMSG, "|0xFFFF00 Server global functions initiated" );
}

//~run gameplay RespawnWorld 0 0 0
void RespawnWorld( Critter& cr, int p0, int p1, int p2 )
{
	MapLootInit();
	OreInit();
	JunkInit();
	WildplantInit();
	MobsSpawnInit();
	FoceWellsCharge();
	FoceWaterPumpsCharge();
	
	cr.Say( SAY_NETMSG, "|0xFFFF00 Global timers reset" );
}

uint cte_ban( Critter& player, int identifier, uint& rate )
{
    player.RunClientScript("client_main@CheckHardBan", 0, 0, 0, null, null);
    return 0;
}

//DIRT
uint cte_Dirt( Critter& cr, int identifier, uint& rate )
{
	int8 Dirtiness = cr.Param[ CR_DIRTINESS ];

	if( isGM( cr ) || cr.IsDead() || cr.IsKnockout() || cr.Param[ QST_GAMEMODE ] == GAME_START )
	{
		return REAL_MINUTE( 10 );
	}

	uint8 Dirt = 1;
	uint16 Chance = 1;
	uint moved = MovementState( cr );
	if( moved > MOVE_STATE_STILL )
	{
		Chance += ( 100 * cr.Param[ CR_ITEMS_WEIGHT ] / cr.Stat[ ST_CARRY_WEIGHT ] );
		if( moved == MOVE_STATE_RUN )
		{
			Dirt += 1;
			Chance += 100;
		}
	}

	Chance = CLAMP( Chance, 0, 200 );
	if( Random( Chance, 200 ) == 200 )
	{
		Dirtiness += Dirt;
	}

	if( cr.Param[ CR_DIRTINESS ] > 100 )
	{
		cr.ParamBase[ CR_DIRTINESS ] = 100;
	}
	else if( cr.Param[ CR_DIRTINESS ] < 0 )
	{
		cr.ParamBase[ CR_DIRTINESS ] = 0;
	}
	
	if( cr.Param[ CR_DIRTINESS ] >= 85 )
	{
		DirtCharDebuff( cr );
	}
	else
	{
		DirtCharRestore( cr );
	}
	
	return REAL_SECOND( 10 );
}

void DirtCharDebuff( Critter& cr ) 
{
	if( cr.ParamBase[ CR_DIRTY ] <= 0 )
	{
		cr.ParamBase[ CR_DIRTY ] = 1;
		cr.StatBase[ ST_CHARISMA_EXT ] -= 1;
		
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_YOU_DIRTY );
	} 
}

void DirtCharRestore( Critter& cr )
{
	if( cr.ParamBase[ CR_DIRTY ] > 0 )
	{
		cr.ParamBase[ CR_DIRTY ] = 0;
		cr.StatBase[ ST_CHARISMA_EXT ] += 1;
	
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_YOU_WASHED );
	}
}

// THIRST 
uint cte_Thirst( Critter& cr, int identifier, uint& rate )
{
	if( cr.StatBase[ ST_BODY_TYPE ] == BT_ROBOT )
	{
		return 0;
	}
	
	if( isGM( cr ) || cr.IsDead() || cr.Param[ QST_GAMEMODE ] == GAME_START )
	{
		return REAL_MINUTE( 10 );
	}
	
	if( cr.IsKnockout() )
	{
		return REAL_MINUTE( 1 );
	}

	cr.StatBase[ ST_THIRST ] -= Random( 0, 2 ) * 10;

	if( cr.StatBase[ ST_THIRST ] <= 0 )
	{
		ThirstCharDebuff( cr );
	}
	else
	{
		ThirstCharRestore( cr );
	}

	return REAL_SECOND( cr.Param[ CR_THIRST_SPEED ] );
}

void ThirstCharDebuff( Critter& cr ) 
{
	if( !cr.IsNpc() && cr.ParamBase[ CR_THIRSTY ] <= 0 )
	{
		cr.ParamBase[ CR_THIRSTY ] = 1;
		cr.StatBase[ ST_ACTION_POINTS ] -= 20;
		
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_AP_LOSS_THIRST );
	} 
}

void ThirstCharRestore( Critter& cr )
{
	if( !cr.IsNpc() && cr.ParamBase[ CR_THIRSTY ] > 0 )
	{
		cr.ParamBase[ CR_THIRSTY ] = 0;
		cr.StatBase[ ST_ACTION_POINTS ] += 20;
	
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_AP_RESTORED );
	}
}

// HUNGER
uint cte_Hunger( Critter& cr, int identifier, uint& rate )
{
	if( cr.StatBase[ ST_BODY_TYPE ] == BT_ROBOT )
	{
		return 0;
	}
	
	if( cr.IsNpc() && cr.GetProtoId() == NPC_PID_PET_DOG )
	{
		if( cr.Stat[ ST_LEVEL ] < 12 && cr.Param[ PET_HUNGER ] < 24 )
		{
			cr.ParamBase[ PET_HUNGER ] ++;
			return REAL_MINUTE( Random( 50, 70 ) );
		}
		else if( cr.Param[ PET_HUNGER ] >= 24 )
		{
			cr.ParamBase[ PET_LOYALTY ] -- ;
			Log( "Pet: " + cr.Id + " Has dropped loyalty!!! Current loyalty: " + cr.Param[ PET_LOYALTY ] );
 			if( cr.Param[ PET_LOYALTY ] <= -100 )
			{
				cr.ClearEnemyStack();
				cr.DropPlanes();
				cr.EraseTimeEvents( CTE_NPC_FOLLOW );
				cr.ModeBase[ MODE_NO_HOME ] = 1;
				cr.ParamBase[ PET_PROTECT_ID ] = 0;
				cr.ParamBase[ PET_GUARD_ID ] = 0;
				cr.SetScript( "mob@_MobInit" );
				Map@ map = cr.GetMap();
				if( valid( map ) )
				{
					PlayGenericSound( map, cr.HexX, cr.HexY, "MADDOGGR.mp3", 10 );
				}
				
				return 0;		
			}
			
			return REAL_MINUTE( Random( 50, 70 ) );
		}
	}
	
	if( isGM( cr ) || cr.IsDead() || cr.Param[ QST_GAMEMODE ] == GAME_START )
	{
		return REAL_MINUTE( 10 );
	}
	
	if( cr.IsKnockout() )
	{
		return REAL_MINUTE( 1 );
	}

	cr.StatBase[ ST_HUNGER ] -= Random( 0, 2 ) * 10;

	if( cr.StatBase[ ST_HUNGER ] <= 0 )
	{
		HungerCharDebuff( cr );
	}
	else
	{
		HungerCharRestore( cr );
	}
	
	return REAL_SECOND( cr.Param[ CR_HUNGER_SPEED ] );
}

void HungerCharDebuff( Critter& cr ) 
{
	if( !cr.IsNpc() && cr.ParamBase[ CR_HUNGRY ] <= 0 )
	{
		cr.ParamBase[ CR_HUNGRY ] = 1;
		cr.StatBase[ ST_ACTION_POINTS ] -= 20;
		
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_AP_LOSS_HUNGER );
	} 
}

void HungerCharRestore( Critter& cr )
{
	if( !cr.IsNpc() && cr.ParamBase[ CR_HUNGRY ] > 0 )
	{
		cr.ParamBase[ CR_HUNGRY ] = 0;
		cr.StatBase[ ST_ACTION_POINTS ] += 20;
	
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_AP_RESTORED );
	}
}

//Getting Drunk
uint cte_Drunk( Critter& cr, int identifier, uint& rate )
{
    if( cr.IsDead() )
	{
        return REAL_MINUTE( 1 );
	}
	
    int levelVal = cr.Stat[ ST_DRUNK ];
    if( levelVal > 0 )
    {
        int modifier = ( ( cr.ParamBase[ ST_ENDURANCE ] + cr.ParamBase[ ST_STRENGTH ] ) / 2 );
		cr.StatBase[ ST_DRUNK ] = CLAMP( levelVal - modifier, 0, 2000 );
		drunkVFX( cr );
        if( levelVal >= 80 && Random( 1, 100 ) >= ( cr.ParamBase[ ST_ENDURANCE ] * 10 - 5 ) )
		{
			uint[] durations;
			uint[] indexes;
			uint amount = cr.GetTimeEvents( CTE_VOMIT, indexes, durations, null );
			if( amount < 1 )
			{
				cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_FEEL_SICK );
				uint vomitTime = Random( 5, 15 );
				cr.AddTimeEvent( "vomit@cte_vomit", REAL_MINUTE( vomitTime ), CTE_VOMIT, 0 );
				vomitTime --;
				cr.AddTimeEvent( "vomit@cte_vomit_warn", REAL_MINUTE( vomitTime ), CTE_VOMIT_WARN, 0 );
			}
		}
	}

    if( cr.Stat[ ST_DRUNK ] == 0 )
    {
        return 0;
    }

    return REAL_SECOND( cr.Param[ CR_DRUNK_SPEED ] );
}

void drunkVFX( Critter& cr )
{
	int levelVal = cr.Stat[ ST_DRUNK ];
	int roll = Random( 1, 100 ) + levelVal; 
	if( roll >= Random( 1, 300 ) )
	{
		uint rollFX = Random( 0, 3 );
		switch( rollFX )
		{
			case( 0 ):
				FlushScreen( cr, COLOR_BLACK, 0, 3000 );
				break;
			case( 1 ):
				FlushScreen( cr, COLOR_BLACK, 0, 6000 );
				break;
			case( 2 ):
				FlushScreen( cr, COLOR_BLACK, 0, 9000 );
				break;
			case( 3 ):
				QuakeScreen( cr, 1, 7000 );
				break;
			default:
				break;
		}
	}
}

//Overdose with healing items and drugs
uint cte_Global_Overdose( Critter& cr, int identifier, uint& rate )
{
	cr.StatBase[ ST_GLOBAL_OVERDOSE ] --;
	
    if( cr.Stat[ ST_GLOBAL_OVERDOSE ] == 0 )
    {
        return 0;
    }

    return REAL_MS( cr.Param[ CR_GLOBAL_OVERDOSE_SPEED ] );
}

/* Process of drying up passively */
uint cte_Wetness( Critter& cr, int identifier, uint& rate )
{
	int amount = 10;
	Map@ map = cr.GetMap();
	if( !valid( map ) )
	{
		return 0;
	}
	
	uint8 rainIntensity = map.GetRain();
	Item@ armor = cr.GetItem( 0, SLOT_ARMOR );
	Item@ helmet = cr.GetItem( 0, SLOT_HEAD );

	if( cr.StatBase[ ST_WETNESS ] > 0 )
	{
		if( !map.HasRoof( cr.HexX, cr.HexY ) && rainIntensity > 0 )
		{
			return REAL_MS( cr.Param[ CR_WETNESS_SPEED ] );
		}
		
		if( map.HasRoof( cr.HexX, cr.HexY ) )
		{
			amount += 5;
		}
		
		cr.StatBase[ ST_WETNESS ] -= MIN( cr.StatBase[ ST_WETNESS ], amount );
		//Log("Proc Wetness");
	}
	
	return REAL_MS( cr.Param[ CR_WETNESS_SPEED ] );
}

Gameplay gameplay;

void start()
{
    iManagerModule@  module = gameplay;
    iManagerElement@ manager = manager_add_module( module, "Gameplay", 40 );
    if( !valid(manager) )
	{
        return;
	}
}

bool CreateModeStart( Critter& cr )
{
    return TeleportToStart( cr );
}

bool TeleportToStart( Critter& cr )
{
    Map@ start = GetMapByPid( MAP_UTILITY_START, 0 );

    if( valid( start ) && cr.TransitToMap( start.Id, 0 ) )
    {
		cr.AddTimeEvent( "cte_ban", 2 * __TimeMultiplier, 0, 0 );
        return true;
    }
    Log( "Transit to start error! PlayerId: " + cr.Id );
    return false;
}

bool respawn_npc( Critter& cr, Map@ map )
{
    if( not valid( map ) ) // On global, delete
    {
        DeleteNpc( cr );
        return true;
    }

    uint16 hx = cr.HexX,
           hy = cr.HexY;

    if( not map.IsHexPassed( hx, hy ) )
    {
        bool founded = false;
        for( int x = -1; x <= 1; x++ )
        {
            for( int y = -1; y <= 1; y++ )
            {
                if( x == 0 && y == 0 )
				{
                    continue;	// Skip direct position
				}
				
                if( __MapHexagonal )
                {
                    if( ( hx % 2 ) == 1 && ( ( x == -1 && y == 1 ) || ( x == 1 && y == 1 ) ) )
					{
                        continue;
                    }
					if( ( hx % 2 ) == 0 && ( ( x == -1 && y == -1 ) || ( x == 1 && y == -1 ) ) )
					{
                        continue;
					}
				}

                if( map.IsHexPassed( hx + x, hy + y ) )
                {
                    hx += x;
                    hy += y;
                    founded = true;
                    break;
                }
            }
            if( founded )
			{
                break;
			}
		}

        if( not founded )
        {
            // Continue dead
            cr.TimeoutBase[ TO_REPLICATION ] = __FullSecond + REAL_MINUTE( 1 );
            return true;
        }
    }

    cr.TransitToMap( map.Id, hx, hy, Random( 0, 5 ) );
    cr.ToLife();

    cr.DropPlanes();
    cr.ClearEnemyStackNpc();
    cr.StatBase[ ST_LAST_WEAPON_ID ] = 0;
	
    cr.DamageBase[ DAMAGE_EYE ] = 0;
    cr.DamageBase[ DAMAGE_RIGHT_ARM ] = 0;
    cr.DamageBase[ DAMAGE_LEFT_ARM ] = 0;
    cr.DamageBase[ DAMAGE_RIGHT_LEG ] = 0;
    cr.DamageBase[ DAMAGE_LEFT_LEG ] = 0;

    cr.StatBase[ ST_CURRENT_HP ] = ( cr.Stat[ ST_MAX_LIFE ] + cr.Stat[ ST_MAX_LIFE_EXT ] );
    cr.Stat[ ST_ACTION_POINTS ] * 100;

    DropPoison( cr );
    DropRadiation( cr );
    clearDrugs( cr );
    cr.DropTimers();
    for( uint i = TIMEOUT_BEGIN; i <= TIMEOUT_END; i++ )
	{
        if( i != TO_TIREDNESS )
		{
            cr.TimeoutBase[ i ] = 0;
		}
	}
    return true;
}

void old_drop_stuff_if_bluddy( Critter& cr, Map& map )
{
    if( ( cr.Anim2Dead == ANIM2_DEAD_PULSE_DUST || cr.Anim2Dead == ANIM2_DEAD_EXPLODE ) && _CritCanDropItemsOnDead( cr ) )
    {
        // Drop all, exclude armor
        Item@[] items;
        cr.GetItems( SLOT_INV, items );
        cr.GetItems( SLOT_HAND1, items );
        cr.GetItems( SLOT_HAND2, items );
		cr.GetItems( SLOT_MISC, items );
		cr.GetItems( SLOT_HEAD, items );
		cr.GetItems( SLOT_BACK, items );
        // Disable drop of hidden items
        for( uint i = 0, j = items.length(); i < j; i++ )
        {
            if( FLAG( items[ i ].Flags, ITEM_GAG ) )
            {
                Item@ item = items[ i ];
                if( valid( item ) )
				{
                    DeleteItem( item );
                }
				@items[ i ] = null;
            }
            else if( FLAG( items[ i ].Flags, ITEM_HIDDEN ) )
			{
                @items[ i ] = null;
			}
		}
        MoveItems( items, map, cr.HexX, cr.HexY );
    }
}

uint e_deleteBody( uint[] @ values )
{
    Map@  map = GetMap( values[ 0 ] );
    Item@ item = GetItem( values[ 1 ] );
    Item@[] items;
    if( valid( item ) )
    {
        item.GetItems( uint( -1 ), items );

        if( valid( map ) )
		{
            MoveItems( items, map, item.HexX, item.HexY );
        }
		else
		{
            DeleteItems( items );
		}
        DeleteItem( item );
    }
    return 0;
}

void CheckDragging( Critter& cr )
{
	if( cr.ParamBase[ CR_YOU_DRAG ] != 0 )
	{
		Critter@ target = GetCritter( cr.ParamBase[ CR_YOU_DRAG ] );
		if( !valid( target ) )
		{
			cr.ParamBase[ CR_YOU_DRAG ] = 0;
		}
		
		Map@ cMap = cr.GetMap();
		if( !valid( cMap) )
		{
			DropCr( cr, target );
			return;
		}
	
		Map@ tMap = target.GetMap();
		if( valid( tMap ) && ( tMap.Id == MAP_HEAVEN || tMap.Id == MAP_HELL || tMap.Id == MAP_UTILITY_START || tMap.Id == MAP_DREAM ) )
		{ 
			DropCr( cr, target ); 
			return; 
		}
		
		if( cr.IsDead() || cr.IsKnockout() || ( !target.IsDead() && !target.IsKnockout() ) )
		{ 	
			DropCr( cr, target );
			return;
		}
		
		if( GetDistantion( cr.HexX, cr.HexY, target.HexX, target.HexY ) > 2 && ( tMap.Id == cMap.Id ) )
		{ 
			DropCr( cr, target );
			return;
		}
		TeleportIt( cr, target );
	}
}

void TeleportIt( Critter& cr, Critter& target )
{	
	if( !valid( target ) )
	{
		cr.ParamBase[ CR_YOU_DRAG ] = 0;
	}
	
	uint16 hx = 0, hy = 0;
	
	Map@ cMap = cr.GetMap();
	if( !valid( cMap) )
	{
		DropCr( cr, target );
		return;
	}
	
	Map@ tMap = target.GetMap();
	if( !valid( tMap ) )
	{
		DropCr( cr, target );
		return;
	}
	
	if( tMap.Id != cMap.Id )
	{ 
		Location@ loc = cMap.GetLocation();
		if( !valid( loc ) )
		{
			return;
		}
		
		if( valid( tMap ) && ( tMap.Id == MAP_HEAVEN || tMap.Id == MAP_HELL || tMap.Id == MAP_UTILITY_START || tMap.Id == MAP_DREAM ) )
		{
			DropCr( cr, target );
			return;
		}
		
		hx = cr.HexX;
		hy = cr.HexY;
		cMap.MoveHexByDir( hx, hy, cr.Dir, 1 );

		target.TransitToMap( cMap.Id, hx, hy, cr.Dir, 0 ); 
		target.SetWorldPos( loc.WorldX, loc.WorldY );
		DropCr( cr, target ); //Temporary solution. The problem is that PreviousHex( cr, hx, hy ) takes into account the old coordinates of cr from previous map
		return; 
	}
	
	PreviousHex( cr, hx, hy );

	if( GetDistantion( hx, hy, cr.HexX, cr.HexY ) > 1 )
	{
		hx = cr.HexX;
		hy = cr.HexY;
		cMap.MoveHexByDir( hx, hy, _RevDir( cr.Dir ), 1 );
	}
	
	if( tMap.IsHexPassed( hx, hy ) )
	{
		Critter@[] dragged = {target};
		Critter@[] seeTarget;
		tMap.GetCrittersSeeing( dragged, true, FIND_LIFE_AND_KO, seeTarget );
		target.TransitToHex( hx, hy, cr.Dir, 0 );

		if( seeTarget.length() > 0 )
		{
			for( uint i = 0; i < seeTarget.length(); i ++ )
			{
				Critter@ seeTargetCr = seeTarget[i];
				seeTargetCr.RefreshVisible();
			}
		}
	}
	else
	{
		if( hx != target.HexX && hy != target.HexY )
		{
			DropCr( cr, target );
		}
	}
	ChangeCritterSpeed( cr );
}

void unsafe_DragAndDrop( Critter& player, int id, int itemId, int param2, string@ param3, int[] @ param4 )
{
    if( player.IsDead() || player.IsKnockout() )
	{
		player.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_CANT_DO_THAT );
		return;
	}
	
	Critter @ target = GetCritter( id );
	
	if( !valid( target ) )
	{
		player.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_MISS_KNOCK );
		return;
	}

	DragAndDrop( player, target );
	player.TimeoutBase[ TO_SK_STEAL ] = __FullSecond + REAL_SECOND( 3 );
}

void DragAndDrop( Critter& cr, Critter@ target )
{
	if( !valid( target ) )
	{ 
		cr.ParamBase[ CR_YOU_DRAG ] = 0;
		return;
	}
	
	if( target.Mode[ MODE_NO_PUSH ] != 0 )
	{
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_YOU_CANT_DO_THAT );
		return;
	}
	
	if( !target.IsDead() && !target.IsKnockout() )
	{ 
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_BODY_MUST_LIE );
		return; 
	}	
	
	if( GetDistantion( cr.HexX, cr.HexY, target.HexX, target.HexY ) > 1 )
	{
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_BODY_TOO_FAR );
		return;
	}
	
	if( target.ParamBase[ CR_DRAGS_YOU ] == int( cr.Id ) )
	{
		DropCr( cr, target );
		return;
	}
	
	if( target.Param[ CR_DRAGS_YOU ] != int( cr.Id ) && target.Param[ CR_DRAGS_YOU ] != 0 )
	{	
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_BODDY_BEING_DRAGGED );
		return;	
	}
	
	if( cr.Param[ CR_YOU_DRAG ] != int( target.Id ) && cr.Param[ CR_YOU_DRAG ] != 0 )
	{	
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_DRAG_ONLY_ONE );
		return;	
	}
	
	if( !isGM(cr) && tooMuchWeight( cr, target ) )
	{
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_TOO_HEAVY );
		return;
	}	
	
	if( !isGM(cr) && tooMuchVolume( cr, target ) )
	{
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_CANT_DO_THAT );
		return;
	}
	
	target.ParamBase[ CR_DRAGS_YOU] = cr.Id;
	cr.ParamBase[ CR_YOU_DRAG ] = target.Id;
	cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_START_DRAGGING );
	ChangeCritterSpeed( cr );
	cr.AddTimeEvent( "cte_dragging", 0, CTE_DRAGGING, 0 );
}

bool tooMuchWeight( Critter& cr, Critter& target )
{
	float max = cr.Stat[ ST_CARRY_WEIGHT ] * DRAGGING_CARRY_WEIGHT_MULTIPLIER;
	float sum = ItemsWeight( cr ) + ItemsWeight( target );
	return( sum > max );
}

bool tooMuchVolume( Critter& cr, Critter& target )
{
	float max = CRITTER_MAX_VOLUME * DRAGGING_CARRY_VOLUME_MULTIPLIER;
	float sum = cr.ItemsVolume() + target.ItemsVolume();
	return( sum > max );
}

uint cte_dragging( Critter& cr, int identifier, uint& rate )
{
	if( cr.Param[ CR_YOU_DRAG ] > 0 )
	{
		uint movementState = MovementState( cr );
		switch( movementState )
		{
			case( 0 ):
			case( 2 ):
			{
				CheckDragging( cr );
				return REAL_MS( cr.Param[ ST_WALK_TIME ] );
			}
			default:
			{
				Critter@ target = GetCritter( cr.ParamBase[ CR_YOU_DRAG ] );
				if( valid( target ) )
				{
					DropCr( cr, target );
				}
				return 0;
			}
		}
	}
	return 0;	
}

void DropCr( Critter@ cr, Critter@ target )
{
	cr.ParamBase[ CR_YOU_DRAG ] = 0;
	target.ParamBase[ CR_DRAGS_YOU ] = 0;
	cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_STOPED_DRAGGING );
	ChangeCritterSpeed( cr );
}

#endif //GAMEPLAY