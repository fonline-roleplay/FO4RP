#include "_utils.fos"
#include "craft_recipes.fosh"

#define COEFFICIENT				(1)

class ItemPart
{
    uint16 Pid = 0;
    uint Count = 0;

    ItemPart(uint16 pid, uint count)
    {
        if( pid == 0 || count == 0 )
        {
            return;
        }
        
        this.Pid = pid;
        this.Count = count;
    }
}

class ItemPartArray
{
    ItemPart@[] ItemParts;
    dictionary pidAssosiateList;
    uint Tier = 1;
    uint Tools = 0;
    int CalcCost = 0;
    int ProtoCost = 0;
    
    void Add(uint16 pid, uint count)
    {
        if( pid == 0 || count == 0 )
        {
            return;
        }
        
        uint index;
        if( pidAssosiateList.get( ""+pid, index ) )
        {
            ItemParts[index].Count += count;
            return;
        }

        ItemPart@ newElem = ItemPart(pid, count);
        Add(newElem);
        pidAssosiateList.set(""+pid, this.length() - 1);
    }

    void Add(ItemPart@ newElem)
    {
        ItemParts.insertLast(newElem);
    }

    uint length()
    {
        return ItemParts.length();
    }

    ItemPart@ get_opIndex(int index) 
    {
        if( index >= int( this.length() ) )
        {
            return null;
        }
        
        return ItemParts[index];
    }
}

dictionary EconomyList;

ItemPartArray@ ProcessDecraftRecipe(CraftRecipe@ recipe)
{
	if( !valid( recipe ) )
	{
		return null;
	}
	
	ItemPartArray@ Decraft;

	if( EconomyList.exists( ""+recipe.Output[0] ) )
	{
		EconomyList.get(""+recipe.Output[0], @Decraft);
		return Decraft;
	}
	
	@Decraft = ItemPartArray();
	Decraft.Tier = recipe.Tier;
	Decraft.Tools = recipe.Tools;

	ProtoItem@ proto = GetProtoItem(recipe.Output[0]);

    if( valid( proto ) )
    {
        Decraft.ProtoCost = MAX(proto.Cost, 1);
        if( ( proto.Type == ITEM_TYPE_ARMOR || proto.Type == ITEM_TYPE_WEAPON ) )
        {
            SETFLAG(Decraft.Tools, TOOL_MULTITOOL);
        }
    }

	for(uint i = 0, len = recipe.Resources.length(); i < len; i++)
	{
		uint16 pid = recipe.Resources[i];
		uint count = recipe.ResourcesCount[i];
		ProtoItem@ ResourceProto = GetProtoItem(pid);
		if(!valid(ResourceProto))
		{
			continue;
		}

		CraftRecipe@ ResourceRecipe = GetCraftByItemPID(pid);
		
		CraftRecipe@ ComponentRecipe = GetCraftByItemPID( pid );
		if( !valid( ComponentRecipe ) ) 
		{
            Decraft.CalcCost += MAX(ResourceProto.Cost, 1) * count;
			Decraft.Add(pid, count);
			continue;
		}

		ItemPartArray@ ComponentDecraft = ProcessDecraftRecipe(ComponentRecipe);
		if(!valid(ComponentDecraft))
		{
			continue;
		}

        Decraft.CalcCost += ComponentDecraft.CalcCost;

		for(uint j = 0, jlen = ComponentDecraft.length(); j < jlen; j++)
		{
			ItemPart@ currCompDecraftRes = ComponentDecraft[j];
			if(!valid(currCompDecraftRes))
			{
				continue;
			}
			
			Decraft.Add(currCompDecraftRes.Pid, currCompDecraftRes.Count * count);
		}
	}

	Decraft.CalcCost = Decraft.CalcCost * COEFFICIENT;
	Decraft.CalcCost = Decraft.CalcCost / recipe.OutputCount[0];

	if(Decraft.length() > 0)
	{
		EconomyList.set( ""+recipe.Output[0], Decraft );
	}
	
	return Decraft;
}

void InitEconomy()
{
	Log("Generating economy list...");
	for(uint i = 0, len = GetMaxCraftsCount(); i < len; i++)
	{
		CraftRecipe@ currRecipe = GetCraftByPID(i);
		if(!valid(currRecipe)) continue;
		ProcessDecraftRecipe(currRecipe);
	}
	Log("Generating economy list complete.");
}

void CreateEconomyList()
{
	file file_data;
	if( file_data.open( "economy.csv", "w" ) != 0 )
	{
		return;
	}

	file_data.writeString( "OUTPUT0_NAME,OUTPUT0_PID,OUTPUT0_AMOUNT,OUTPUT0_PID_COST,OUTPUT0_CALC_COST,OUTPUT1_NAME,OUTPUT1_PID,OUTPUT1_AMOUNT,OUTPUT1_PID_COST,OUTPUT1_CALC_COST\n" );

	for(uint i = 0, len = GetMaxCraftsCount(); i < len; i++)
    {
        CraftRecipe@ currRecipe = GetCraftByPID(i);
        if(!valid(currRecipe)) continue;
		uint16 pid = currRecipe.Output[0];

		ItemPartArray@ currParts = GetItemParts(pid);
		if(!valid(currParts)) continue;
		
		int calcCost = currParts.CalcCost;

		for(uint j = 1, jlen = currRecipe.Output.length(); j < jlen; j++)
		{
			int count = currRecipe.OutputCount[j] > 1 ? currRecipe.OutputCount[j] : 1;
			ItemPartArray@ currSecondOutput = GetItemParts(currRecipe.Output[j]);
			if(!valid(currSecondOutput)) continue;
			calcCost = calcCost - (currSecondOutput.CalcCost * count);
		}

		string result = "";
		
		for(uint j = 0; j < 2; j++)
		{
			if(j > 0)
				result += ",";
			if(currRecipe.Output.length() - 1 < j)
			{
				result += ",,,,";
				continue;
			}

			uint16 outputPid = currRecipe.Output[j];
			ItemPartArray@ outputParts = GetItemParts(outputPid);
			if(!valid(outputParts)) continue;

			string name = ReplaceText(GetMsgStr( TEXTMSG_ITEM, outputPid * 100 ), ",", "");
			name = ReplaceText(name, ",", "");
			result += name + "," + outputPid + "," + currRecipe.OutputCount[j] + "," + outputParts.ProtoCost + "," + (j == 0 ? calcCost : outputParts.CalcCost);
		}

		string formula = "";

		for(uint j = 0, jlen = currParts.length(); j < jlen; j++)
		{
			ItemPart@ currPart = currParts[j];
			if(!valid(currPart)) continue;
			ProtoItem@ protoItem = GetProtoItem(currPart.Pid);
			if(!valid(protoItem)) continue;

			formula += ",";
			
			string name = ReplaceText(GetMsgStr( TEXTMSG_ITEM, currPart.Pid * 100 ), ",", "");
			name = ReplaceText(name, ",", "");
			formula += name + "," + currPart.Pid + "," + MAX(protoItem.Cost, 1) + "," + currPart.Count + "," + COEFFICIENT;
		}

        file_data.writeString( result + formula + "\n" );
	}

	file_data.close();
}

ItemPartArray@ GetItemParts(uint16 pid)
{
	ItemPartArray@ Decraft;
	if( EconomyList.get(""+pid, @Decraft) )
	{
		return Decraft;
	}
	return null;
}