#ifndef SEX_MODULE
#define SEX_MODULE

#include "_utils.fos"
#include "sex_h.fos"
#include "_ltp.fos"
#include "gathering_h.fos"
#include "critter_skills_h.fos"
#include "effects_h.fos"
#include "npc_planes_h.fos"
#include "drugs_h.fos"

void StartMenuSexPanel( Critter& cr, Critter& targetCr )
{
	cr.ParamBase[ CR_INIT_SEX ] = 0;
	resetVals( cr );
    Map@ map = cr.GetMap();
    if( map is null )
	{
        return;
    }
	
	if( !valid( targetCr ) )
	{
		return;
	}
	
	if( !targetCr.IsPlayer() )
	{
		return;
	}
	
    iMenuHandler@ handler = MenuSexPanel( map, targetCr );
    iDialogBox@ menu = OpenMenu( cr, "SexPanel", handler );
}

class MenuSexPanel: CenteredMenuHandler
{
    uint map_id;
	uint targetCr_id;
	int selectorPos;
	uint arrayPos;
	
    MenuSexPanel( Map& map, Critter& targetCr )
	{
        map_id = map.Id;
		targetCr_id = targetCr.Id;
		selectorPos = 0;
		arrayPos = 0;
    }

    bool MenuUpdate( Critter& cr, iDialogBox& menu )
	{
        Map@ map = GetMap( map_id );
		Critter@ targetCr = GetCritter( targetCr_id );

		if( cr.Param[ SEX_PARTNER_STATE ] == SEX_PARTNER_DECLINE )
		{
			cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_SEX_REJECT );
			resetVals( cr );
			return false;
		}
		else if( cr.Param[ SEX_PARTNER_STATE ] == SEX_PARTNER_AGREE )
		{
			return false;
		}
		
		if( MovementState( cr ) > 0 || MovementState( targetCr ) > 0 )
		{
			resetVals( cr );
			return false;
		}
		
		if( cr.Param[ SEX_OFFER_STATE ] != 1 )
		{
			if( hasItems( cr, Condoms, -1 ) )
			{
				string condom = cr.Param[ SEX_PROTECTION_STATE ] == SEX_PROTECTION_NONE ? "None" : "Condom";
				if( menu.ButtonExt( "Protection: ", condom ) )
				{
					cr.ParamBase[ SEX_PROTECTION_STATE ] = cr.Param[ SEX_PROTECTION_STATE ] == SEX_PROTECTION_NONE ? SEX_PROTECTION_CONDOM : SEX_PROTECTION_NONE;
					return true;
				}
			}
			
			string sexMode = cr.Param[ SEX_ANIMATE_STATE ] == SEX_STATE_BLACK ? "Black Screen" : "Animate";
			if( menu.ButtonExt( "Mode: ", sexMode ) )
			{
				cr.ParamBase[ SEX_ANIMATE_STATE ] = cr.Param[ SEX_ANIMATE_STATE ] == SEX_STATE_BLACK ? SEX_STATE_ANIM : SEX_STATE_BLACK;
				if( cr.Param[ SEX_ANIMATE_STATE ] == SEX_STATE_ANIM )
				{
					cr.ParamBase[ SEX_POSITION ] = SEX_POSITION_CLASSIC;
				}
				return true;
			}
			
			if( cr.ParamBase[ SEX_ANIMATE_STATE ] == SEX_STATE_ANIM )
			{
				string position = SexPositions[ cr.Param[ SEX_POSITION ] ];
				if( menu.ButtonExt( "Position: ", position ) )
				{
					selectorPos++;
					if( arrayPos == SexPositions.length() -1 )
					{
						arrayPos = 0;
						selectorPos = arrayPos;
					}
					else
					{
						arrayPos = selectorPos;
					}
					cr.ParamBase[ SEX_POSITION ] = arrayPos;
					return true;
				}
			}

			if( menu.Button( "Make offer" ) )
			{
				StartMenuAnswerSexPanel( targetCr, cr );
				cr.ParamBase[ SEX_OFFER_STATE ] = SEX_OFFER_MADE;
				return true;
			}
		}
		
		if( menu.Button( "Decline" ) )
		{
			cr.ParamBase[ SEX_PARTNER_STATE ] = SEX_PARTNER_DECLINE;
			resetVals( cr );
			return false;
		}
 		return true;
    }
	
    string@ Description( Critter& cr )
	{
		string info = cr.Param[ SEX_OFFER_STATE ] != SEX_OFFER_MADE ? "You are about to make an offer to have sex to your partner:\n" : "You have made an offer to have sex to your partner.\nAnd are now waiting for the response.\n";
		string mode = cr.ParamBase[ SEX_ANIMATE_STATE ] == SEX_STATE_BLACK ? "Black Screen" : "Animate";
		info += "Sex mode: |0xFFFF00 " + mode;
		string protection = cr.ParamBase[ SEX_PROTECTION_STATE ] == SEX_PROTECTION_NONE ? "None" : "Condom";
		info += "\n|0x3CF800 Protection: |0xFFFF00 " + protection;
		string position = SexPositions[ cr.ParamBase[ SEX_POSITION ] ];
		info += "\n|0x3CF800 Position: |0xFFFF00 " + position;
		return info;
    }
	
    string@ ButtonCancel()
	{
        return ButtonDecorator( " ", null );
    }

	bool ShouldRedraw( Critter& cr )
	{
		return true;
    }
}

void StartMenuAnswerSexPanel( Critter& targetCr, Critter& cr )
{
    Map@ map = targetCr.GetMap();
    if( map is null )
	{
        return;
    }

    iMenuHandler@ handler = MenuAnswerSexPanel( map, cr );
    iDialogBox@ menu = OpenMenu( targetCr, "SexAnswerPanel", handler );
}

class MenuAnswerSexPanel: CenteredMenuHandler
{
    uint map_id;
	uint cr_id;
	int selectorPos;
	uint arrayPos;
	
    MenuAnswerSexPanel( Map& map, Critter& cr )
	{
        map_id = map.Id;
		cr_id = cr.Id;
		selectorPos = 0;
		arrayPos = 0;
    }

    bool MenuUpdate( Critter& targetCr, iDialogBox& menu )
	{
        Map@ map = GetMap( map_id );
		Critter@ cr = GetCritter( cr_id );
		
		if( MovementState( cr ) > 0 || MovementState( targetCr ) > 0 )
		{
			return false;
		}
		
		if( cr.Param[ SEX_PARTNER_STATE ] == SEX_PARTNER_DECLINE )
		{
			targetCr.Say( SAY_NETMSG, "Your partner decided not to have the intercourse" );
			return false;
		}
		
		if( cr.Param[ SEX_PROTECTION_STATE ] == SEX_PROTECTION_NONE )
		{
			if( hasItems( cr, Condoms, -1 ) || hasItems( targetCr, Condoms, -1 ) )
			{
				string condom = cr.Param[ SEX_PROTECTION_STATE ] == 0 ? "None" : "Condom";
				if( menu.ButtonExt( "Protection: ", condom ) )
				{
					cr.ParamBase[ SEX_PROTECTION_STATE ] = cr.Param[ SEX_PROTECTION_STATE ] == 0 ? 1 : 0;
					return true;
				}
			}
		}
		
		if( cr.ParamBase[ SEX_ANIMATE_STATE ] != SEX_STATE_BLACK )
		{
			string sexMode = cr.Param[ SEX_ANIMATE_STATE ] == SEX_STATE_BLACK ? "Black Screen" : "Animate";
			if( menu.ButtonExt( "Mode: ", sexMode ) )
			{
				cr.ParamBase[ SEX_ANIMATE_STATE ] = cr.Param[ SEX_ANIMATE_STATE ] == 0 ? 1 : 0;
				if( cr.Param[ SEX_ANIMATE_STATE ] == 1 )
				{
					cr.ParamBase[ SEX_POSITION ] = 1;
				}
				return true;
			}
			
			if( cr.ParamBase[ SEX_ANIMATE_STATE ] == SEX_STATE_ANIM )
			{
				string position = SexPositions[ cr.Param[ SEX_POSITION ] ];
				if( menu.ButtonExt( "Position: ", position ) )
				{
					selectorPos++;
					if( arrayPos == SexPositions.length() -1 )
					{
						arrayPos = 0;
						selectorPos = arrayPos;
					}
					else
					{
						arrayPos = selectorPos;
					}
					cr.ParamBase[ SEX_POSITION ] = arrayPos;
					return true;
				}
			}
		}
	
		if( menu.Button( "Confirm" ) )
		{
			cr.ParamBase[ SEX_PARTNER_STATE ] = SEX_PARTNER_AGREE;
			HaveSex( cr, targetCr );
			return false;
		}
		
		if( menu.Button( "Decline" ) )
		{
			cr.ParamBase[ SEX_PARTNER_STATE ] = SEX_PARTNER_DECLINE;
			return false;
		}

 		return true;
    }
	
    string@ Description( Critter& targetCr )
	{
		Critter@ cr = GetCritter( cr_id );
		string info = "You are being offered to have sex!\nIt's up to you now to decide on the way to do it:\n";
		string mode = cr.ParamBase[ SEX_ANIMATE_STATE ] == SEX_STATE_BLACK ? "Black Screen" : "Animate";
		info += "Sex mode: |0xFFFF00 " + mode;
		string protection = cr.ParamBase[ SEX_PROTECTION_STATE ] == SEX_PROTECTION_NONE ? "None" : "Condom";
		info += "\n|0x3CF800 Protection: |0xFFFF00 " + protection;
		string position = SexPositions[ cr.ParamBase[ SEX_POSITION ] ];
		info += "\n|0x3CF800 Position: |0xFFFF00 " + position;
		return info;
    }
	
    string@ ButtonCancel()
	{
        return ButtonDecorator( " ", null );
    }

	bool ShouldRedraw( Critter& targetCr )
	{
		return true;
    }
}

void HaveSex( Critter& cr, Critter& targetCr )
{
	if( cr.IsInjured() )
	{
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_INJURED_FOR_SEX );
		targetCr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_PARTNER_INJURED_FOR_SEX );
		return; 
	}
	
	if( targetCr.IsInjured() )
	{ 
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_PARTNER_INJURED_FOR_SEX );
		targetCr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_INJURED_FOR_SEX );
		return; 
	}
	
	if( IsTired( cr ) )
	{
		targetCr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_PARTNER_TIRED_FOR_SEX );
		return;
    }
		
	if( IsTired( targetCr ) )
	{
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_PARTNER_TIRED_FOR_SEX );
        return;
    }
	
	//This will be added later, when animal taming is in place
	/*if( targetCr.StatBase[ ST_BODY_TYPE ] == 5 )
	{
		cr.ParamBase[ SEX_ANIMATE_STATE ] = targetCr.Id;
		start_bramin_sex( cr );
		return;
	}*/
	
	string protection = cr.Param[ SEX_PROTECTION_STATE ] == 0 ? "n unprotected" : "safe";
    CrimeLog( cr, crInfo( cr ) + " and " + crInfo( targetCr ) + " are having a" + protection + " sex." );
	
	start_cr_sex( cr, targetCr );
	start_targetCr_sex( targetCr, cr );
	
	if( cr.Param[ SEX_ANIMATE_STATE ] == SEX_STATE_BLACK )
	{
	    FlushScreen( cr, COLOR_BLACK, COLOR_BLACK, 7500 );
		FlushScreen( targetCr, COLOR_BLACK, COLOR_BLACK, 7500 );
	}
	
	if( cr.Param[ SEX_PROTECTION_STATE ] == SEX_PROTECTION_CONDOM )
	{
		Item@[] condoms = getItems( cr, Condoms, -1 );
		Item@ condom = null;
		if( condoms.length() > 0 )
		{
			@condom = condoms[0];
			useCondom( condom );
		}
		else
		{
			condoms = getItems( targetCr, Condoms, -1 );
			if( condoms.length() > 0 )
			{
				@condom = condoms[0];
				useCondom( condom );
			}
		}
		if( !valid( condom ) )
		{
			//desease chance go here
		}
	}
	return;
}

void useCondom( Item@ condom )
{
	if( valid( condom ) )
	{
		_SubItem( condom, 1 );
	}
}

bool cr_ltp_inited = false;
void cr_ltp_init()
{
	LTPREG( LTP_SEX, process_cr_sex )
	cr_ltp_inited = true;
}

bool start_cr_sex( Critter& cr, Critter& targetCr )
{
	if( !valid( targetCr ) )
	{
		return false;
	}
	
	if( !cr_ltp_inited )
	{
		cr_ltp_init();
	}
	
	uint8  revDir;
	revDir = cr.Dir + 3;
	if( revDir > 5 )
	{
		revDir = revDir - 6;
	}
	
	switch( cr.Param[ SEX_POSITION ] )
	{
		case( SEX_POSITION_DOGGIE ):
		{
			if( cr.Stat[ ST_GENDER ] == GENDER_FEMALE && targetCr.Stat[ ST_GENDER ] == GENDER_MALE )
			{
				cr.SetDir( revDir );
				targetCr.Animate( 0, ANIM2_DAMAGE_FRONT, null, true, true );
			}
			else
			{
				targetCr.SetDir( cr.Dir );
				targetCr.Animate( 0, ANIM2_DAMAGE_BACK, null, true, true );
			}
			break;
		}
		case( SEX_POSITION_ORAL ):
			cr.SetAnims( COND_LIFE, 0, ANIM2_ORAL );
			break;
		default:
			cr.Animate( 0, ANIM2_DAMAGE_FRONT, null, true, true );
			break;
	}
	StartProcess( cr, LTP_SEX, 0, targetCr.Id, 0, 0 );
	return true;
}

uint process_cr_sex( Critter@ cr, int& param0, int& param1, int& param2 )
{
	LTPROCESS( cr, param0, LTP_SEX )
	Critter@ targetCr = GetCritter( param1 );
	param0++;
	
	switch( cr.Param[ SEX_POSITION ] )
	{
		case( SEX_POSITION_ORAL ):
			break;
		default:
			cr.Animate( 0, ANIM2_DAMAGE_FRONT, null, true, true );
			break;
	}

	if( cr.ParamBase[ ST_LTP_TIME ] == -1 )
	{
		if( cr.Param[ SEX_POSITION ] == SEX_POSITION_ORAL )
		{
			cr.SetAnims( COND_LIFE, 0, ANIM2_IDLE );
		}
		return 0;
	}
	
	if( targetCr.ParamBase[ ST_LTP_TIME ] == -1 )
	{
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_CUM_TOO_FAST );
		targetCr.SayMsg( SAY_EMOTE, TEXTMSG_TEXT, STR_EMOTE_TREMBLES );
		
		if( cr.Param[ SEX_POSITION ] == SEX_POSITION_ORAL )
		{
			cr.SetAnims( COND_LIFE, 0, ANIM2_IDLE );
		}
		
		resetVals( cr );
		return 0;
	}
	
	if( param0 > 10 )
	{
		targetCr.ParamBase[ ST_LTP_TIME ] = -1;
		
		if( cr.Param[ SEX_POSITION ] == SEX_POSITION_ORAL )
		{
			cr.SetAnims( COND_LIFE, 0, ANIM2_IDLE );
		}
		finish_sex( cr, targetCr );		
		return 0;
	}
	
	return 700;
}

bool targetCr_ltp_inited = false;
void targetCr_ltp_init()
{
	LTPREG( LTP_SEX_RECIEVE, targetCr_process_sex )
	targetCr_ltp_inited = true;
}

bool start_targetCr_sex( Critter& targetCr, Critter& cr )
{
	if( !valid( cr ) )
	{
		return false;
	}
	
	if( !targetCr_ltp_inited )
	{
		targetCr_ltp_init();
	}

	uint8  revDir;
	revDir = cr.Dir + 3;
	if( revDir > 5 )
	{
		revDir = revDir - 6;
	}

	if( !targetCr.IsKnockout() )
	{
		switch( cr.Param[ SEX_POSITION ] )
		{
			case( SEX_POSITION_CLASSIC ):
				targetCr.SetDir( revDir );
				targetCr.Animate( 0, ANIM2_DAMAGE_FRONT, null, true, true );
				break;
			case( SEX_POSITION_DOGGIE ):
			{
				//if( targetCr.Stat[ ST_GENDER ] == GENDER_MALE  && cr.Stat[ ST_GENDER ] == GENDER_FEMALE )
				//{
			//		targetCr.SetDir( revDir );
			//	}
				//else
			//	{
					targetCr.SetDir( cr.Dir );
			//	}

				targetCr.Animate( 0, ANIM2_DAMAGE_BACK, null, true, true );
				break;
			}
			case( SEX_POSITION_ORAL ):
				targetCr.SetDir( revDir );
				targetCr.SetAnims( COND_LIFE, 0, ANIM2_ORAL_RECIEVE );
				break;
		}
	}
	
	StartProcess( targetCr, LTP_SEX_RECIEVE, 0, cr.Id, 0, 0 );
	return true;
}

uint targetCr_process_sex( Critter@ targetCr, int& param0, int& param1, int& param2 )
{
	LTPROCESS( targetCr, param0, LTP_SEX_RECIEVE )
	Critter@ cr = GetCritter( param1 );
	param0++;
	
	if( !targetCr.IsKnockout() )
	{
		switch( cr.Param[ SEX_POSITION ] )
		{
			case( SEX_POSITION_CLASSIC ):
				targetCr.Animate( 0, ANIM2_DAMAGE_FRONT, null, true, true );
				break;
			case( SEX_POSITION_DOGGIE ):
				targetCr.Animate( 0, ANIM2_DAMAGE_BACK, null, true, true );
				break;
			case( SEX_POSITION_ORAL ):
				break;
		}
	}
	
	if( targetCr.ParamBase[ ST_LTP_TIME ] == -1 )
	{
		targetCr.SetAnims( COND_LIFE, 0, ANIM2_IDLE );
		return 0;
	}
	
	if( cr.ParamBase[ ST_LTP_TIME ] == -1 )
	{
		targetCr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_CUM_TOO_FAST );
		cr.SayMsg( SAY_EMOTE, TEXTMSG_TEXT, STR_EMOTE_TREMBLES );
		targetCr.SetAnims( COND_LIFE, 0, ANIM2_IDLE );
		return 0;
	}
	
	if( param0 > 10 )
	{
		targetCr.ParamBase[ ST_LTP_TIME ] = -1;
		targetCr.SetAnims( COND_LIFE, 0, ANIM2_IDLE );
		return 0;
	}
	
	return 700;
}

void finish_sex( Critter& cr, Critter& targetCr )
{
	cr.TimeoutBase[ TO_TIREDNESS ] = __FullSecond + cr.Timeout[ TO_TIREDNESS ] + REAL_SECOND( 300 );
	targetCr.TimeoutBase[ TO_TIREDNESS ]= __FullSecond + targetCr.Timeout[ TO_TIREDNESS ] + REAL_SECOND( 300 );
	
	cr.StatBase[ ST_HUNGER ] -= Random( 15, 20 );
	targetCr.StatBase[ ST_HUNGER ] -= Random( 15, 20 );
	
	cr.StatBase[ ST_THIRST ] -= Random( 20, 30 );
	targetCr.StatBase[ ST_THIRST ] -= Random( 20, 30 );
	
	cr.ParamBase[ CR_DIRTINESS ] += Random( 20, 30 );
	targetCr.ParamBase[ CR_DIRTINESS ] += Random( 20, 30 );

	targetCr.SayMsg( SAY_EMOTE, TEXTMSG_TEXT, STR_EMOTE_GETS_LAID );
	cr.SayMsg( SAY_EMOTE, TEXTMSG_TEXT, STR_EMOTE_GETS_LAID );
	int skillNum = SK_SEX;
	raiseSkill( cr, skillNum );
	raiseSkill( targetCr, skillNum );
	resetVals( cr );
	
	StopProcess( cr );
	StopProcess( targetCr );
	
	if( cr.GetTimeEvents( CTE_HAD_SEX, null, null, null ) == 0 )
	{
		uint rate = 0;
		uint skill = cr.Skill[ SK_SEX ] > targetCr.Skill[ SK_SEX ] ? cr.Skill[ SK_SEX ] : targetCr.Skill[ SK_SEX ];
		rate = skill * 0.3f;
		cr.StatBase[ ST_APREGEN_EXT ] += rate;
		targetCr.StatBase[ ST_APREGEN_EXT ] += rate;
		if( cr.KarmaBase[ KARMA_SEXPERT ] >= 1 || targetCr.KarmaBase[ KARMA_SEXPERT ] >= 1 )
		{
			cr.StatBase[ ST_APREGEN_EXT ] += 60;
			targetCr.StatBase[ ST_APREGEN_EXT ] += 60;
			rate += 60;
		}
		cr.AddTimeEvent( "cte_had_sex", REAL_HOUR( 4 ), CTE_HAD_SEX, rate );
		targetCr.AddTimeEvent( "cte_had_sex", REAL_HOUR( 4 ), CTE_HAD_SEX, rate );
	}
}

void resetVals( Critter& cr )
{
	cr.ParamBase[ SEX_ANIMATE_STATE ] = 0;
	cr.ParamBase[ SEX_PROTECTION_STATE ] = 0;
	cr.ParamBase[ SEX_POSITION ] = 0;
	cr.ParamBase[ SEX_OFFER_STATE ] = 0;
	cr.ParamBase[ SEX_PARTNER_STATE ] = 0;
}

uint cte_had_sex( Critter& cr, int identifier, uint& rate )
{
	cr.StatBase[ ST_APREGEN_EXT ] -= rate;
	cr.EraseTimeEvents( CTE_HAD_SEX );
	return 0;
}

/* This will be added later, when animal taming is in place
bool ltp_bramin_inited = false;
void ltp_bramin_init()
{
	LTPREG( LTP_BRAMIN_SEX, process_bramin_sex )
	ltp_bramin_inited = true;
}

bool start_bramin_sex( Critter& cr )
{
	if(!ltp_bramin_inited) 
		ltp_bramin_init();
	
		Critter@ target = GetCritter( cr.ParamBase[SEX_ANIMATE_STATE] );
		target.SetDir( cr.Dir );
		target.SetHomePos( target.HexX, target.HexY, target.Dir);
		target.Animate( 0, ANIM2_DAMAGE_BACK, null, true, true );
		cr.Animate( 0, ANIM2_DAMAGE_FRONT, null, true, true );
		StartProcess( cr, LTP_BRAMIN_SEX, 0, 0, 0, 0 );
		return true;
}

uint process_bramin_sex( Critter@ cr, int& param0, int& param1, int& param2 )
{
	LTPROCESS( cr, param0, LTP_BRAMIN_SEX )
	Critter@ targetCr = GetCritter( cr.ParamBase[SEX_ANIMATE_STATE] );
	
	param0++;
	
	targetCr.Animate( 0, ANIM2_DAMAGE_BACK, null, true, true );
	cr.Animate( 0, ANIM2_DAMAGE_FRONT, null, true, true );
	
	if( param0 > 10 )
	{
		cr.Say( SAY_EMOTE, "Ублажает брамина" );
		targetCr.Say( SAY_EMOTE, "Нежно и протяжно мычит" );
		if( cr.ParamBase[ SK_SEX ] <= 200 ) {
			
			int skillNum = SK_SEX;
			raiseSkill( cr, skillNum );
		}
		return 0;
	}
	return 700;
}*/

#endif // SEX_MODULE
