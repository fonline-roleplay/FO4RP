#ifndef LOCKERS
#define LOCKERS

#include "_utils.fos"
#include "lockers_h.fos"
#include "gateway_h.fos"

bool UseItemOnLocker( Critter& cr, Item& locker, Item& item )
{
    if( item.GetType() == ITEM_TYPE_KEY )
    {
        if( item.LockerId == locker.LockerId )
        {
            if( !FLAG( locker.LockerCondition, LOCKER_ISOPEN ) )
            {
				Map@ map = cr.GetMap();
				if( hasKey( locker ) ) 
				{
					bool thisSide = hasKeyAround( cr, locker );
					uint keySide = thisSide ? STR_KEY_THIS_SIDE : STR_KEY_OTHER_SIDE;
					cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, keySide );
					return false;
				}

                if( FLAG( locker.LockerCondition, LOCKER_LOCKED ) )
                {
                    UNSETFLAG( locker.LockerCondition, LOCKER_LOCKED );
                    cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_UNLOCK_DOOR );
					cr.SayMsg( SAY_EMOTE_ON_HEAD, TEXTMSG_TEXT, STR_EMOTE_UNLOCKS );
                }
                else
                {
                    SETFLAG( locker.LockerCondition, LOCKER_LOCKED );
                    cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_LOCK_DOOR );
					cr.SayMsg( SAY_EMOTE_ON_HEAD, TEXTMSG_TEXT, STR_EMOTE_LOCKS );
                }
				PlayGenericSound( map, cr.HexX, cr.HexY, "ui_lockpicking_unlock.wav", 4 );
                return true;
            }
            cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_CLOSE_DOOR );
            return true;
        }
        else
        {
            cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_WRONG_KEY );
            return true;
        }
    }
    return true;
}

bool hasKeyAround( Critter& cr, Item& locker )
{
	Item@[] list = getItemsAround( cr );
	for( uint i = 0, len = list.length(); i < len; i++ )
	{
		if( list[i].GetType() == ITEM_TYPE_KEY && list[i].LockerId == locker.LockerId )
		{
			return true;
		}
	}
	return false;
}

bool hasKey( Item& locker, int dir = NO_DIR )
{
	Item@[] list = getItemsAround( locker );
	
	for( uint i = 0, len = list.length(); i < len; i++ )
	{
		if( list[i].GetType() == ITEM_TYPE_KEY && list[i].LockerId == locker.LockerId )
		{
			return true;
		}
	}
	return false;
}

bool hasKey( Critter& cr, Item& locker )
{
	Item@[] keys;
	cr.GetItemsByType( ITEM_TYPE_KEY, keys );
	uint len = keys.length();
	if( len > 0 )
	{
		for( uint i = 0; i < len; i ++ )
		{
			if( keys[i].LockerId != locker.LockerId )
			{
				continue;
			}
			else
			{
				return true;
			}
		}
	}
	return false;
}

bool LockpickMenu( Critter& cr, Item& locker )
{
	cr.StatBase[ ST_VAR0 ] = locker.Id;
	
	string[] data = { "lockers@answer_LOCKPICK", "Choose action:", "knock", "bang", "begin lockpick" };
	
	if( locker.Proto.Item_Subtype != ITEM_SUBTYPE_BRIEFCASE )
	{
		string action = " key";
		if( hasKey( locker ) && hasKeyAround( cr, locker ) )
		{
			data.insertLast( "extract" + action );
			data.insertLast( ( FLAG( locker.LockerCondition, LOCKER_LOCKED ) ? "Unlock" : "lock" ) + " the lock" );
		}
		else if( hasKey( cr, locker ) ) 
		{
			data.insertLast( "insert" + action );
			data.insertLast( ( FLAG( locker.LockerCondition, LOCKER_LOCKED ) ? "Unlock" : "lock" ) + " the lock" );
		}
	}
	
	DIALOG_MENU( cr, data );
	
	return true;
}

bool UseSkillOnLocker( Critter& cr, Item& locker, int skill )
{
	if( FLAG( locker.LockerCondition, LOCKER_NOOPEN ) )
	{
		LogCrimeMovement( cr, locker, "cant open NOOPEN" ); 
		cr.SayMsg( SAY_NETMSG, TEXTMSG_GAME, STR_USE_NOTHING );
		return true;
	}
	
    Map@ map = cr.GetMap();
    if( !valid( map ) )
    {
        cr.SayMsg( SAY_NETMSG, TEXTMSG_GAME, STR_USE_NOTHING );
        return true;
    }

	if( skill == SKILL_PICK_ON_GROUND )
    {
		if( FLAG( locker.LockerCondition, LOCKER_ELECTRO ) && !FLAG( locker.LockerCondition, LOCKER_ISOPEN ) && ( locker.GetType() == ITEM_TYPE_DOOR || locker.GetType() == ITEM_TYPE_CONTAINER ) )
        {
            cr.RunClientScript( "client_screen_codedoor@ShowScreen", locker.Id, locker.CODEDOOR_CODE, 0, "Enter password", null );
			LogCrimeMovement( cr, locker, "enters password" ); 
            return true;
        }
		
        if( locker.GetType() == ITEM_TYPE_DOOR )
        {
            if( FLAG( locker.LockerCondition, LOCKER_ISOPEN ) && valid( map.GetCritter( locker.HexX, locker.HexY ) ) )
            {
                cr.SayMsg( SAY_NETMSG, TEXTMSG_GAME, STR_USE_NOTHING );
				LogItemMovement( cr, locker, "can't close the door because someone is in the way" ); 
                return true;
            }
            // Check key
            if( !FLAG( locker.LockerCondition, LOCKER_ISOPEN ) && locker.LockerId != 0 &&
                !FLAG( locker.LockerCondition, LOCKER_BROKEN ) && FLAG( locker.LockerCondition, LOCKER_LOCKED ) )
            {
                cr.SayMsg( SAY_NETMSG, TEXTMSG_GAME, STR_ITEM_LOCKER_NEED_KEY );
				LockerBudge( locker );
				
                if( !isGM(cr) )
				{
					LogCrimeMovement( cr, locker, "can't open locked because has no key" ); 
					PlayDoorSound( map, locker.HexX, locker.HexY, locker.GetProtoId(), locker.LockerCondition, 15 );
					return true;
				}
				else
				{
					LogCrimeMovement( cr, locker, "opens locked door by GM force" ); 
				}
			}
            // Change state
            if( FLAG( locker.LockerCondition, LOCKER_ISOPEN ) )
			{
				Map@ mapl = GetMap( locker.MapId );
				if( !valid( mapl ) )
				{
					return true;
				}
				
				if( mapl.IsHexPassed( locker.HexX, locker.HexY) )
				{
					LogItemMovement( cr, locker, "closes" ); 
					locker.LockerClose();
					ToggleDoorBlocker( mapl, locker, true );
					PlayDoorSound( map, locker.HexX, locker.HexY, locker.GetProtoId(), locker.LockerCondition, 15 );
				}
				else
				{
					LogItemMovement( cr, locker, "can't close door because of blocked hex" ); 
					cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_HEX_BLOCKED );
					PlayDoorSound( map, locker.HexX, locker.HexY, locker.GetProtoId(), 2, 15 );
				}
				return true;
			}
			else
			{
				LogItemMovement( cr, locker, "opens" ); 
				ToggleDoorBlocker( map, locker, false );
				locker.LockerOpen();
				PlayDoorSound( map, locker.HexX, locker.HexY, locker.GetProtoId(), locker.LockerCondition, 15 );
				return true;
			}
        }
        else if( locker.GetType() == ITEM_TYPE_CONTAINER )
        {
            if( locker.CritSlot != SLOT_INV && FLAG( locker.Flags, ITEM_CAN_PICKUP ) )
            {
                int freeWeight = cr.Stat[ ST_CARRY_WEIGHT ] - ItemsWeight( cr );
                int contWeight = 0;
                Item@[] items;
                uint len = locker.GetItems( uint( -1 ), items );
				
                for( uint i = 0; i < len; i++ )
                {
                    if( items[ i ] is null )
					{
                        continue;
					}
                    contWeight += int( items[ i ].Proto.Weight * items[ i ].GetCount() );
                }

                if( freeWeight >= ( contWeight + int(locker.Proto.Weight) ) )
                {
					LogItemMovement( cr, locker, "picks up" ); 
                    MoveItem( locker, 0, cr );
                    return true;
                }
            }
            // Check key
            if( !FLAG( locker.LockerCondition, LOCKER_ISOPEN ) && locker.LockerId != 0 &&
                !FLAG( locker.LockerCondition, LOCKER_BROKEN ) && FLAG( locker.LockerCondition, LOCKER_LOCKED ) )
            {
                cr.SayMsg( SAY_NETMSG, TEXTMSG_GAME, STR_ITEM_LOCKER_NEED_KEY );
				LockerBudge( locker );
				
                if( !isGM(cr) )
				{
					LogCrimeMovement( cr, locker, "cant open locked because has no key" );
					PlayDoorSound( map, locker.HexX, locker.HexY, locker.GetProtoId(), locker.LockerCondition, 15 );
					return true;
				}
				else
				{
					LogCrimeMovement( cr, locker, "opens with GM power" );
				}
			}
            // Is changeble
            if( locker.Proto.Container_Changeble )
            {
                // Change state
                if( FLAG( locker.LockerCondition, LOCKER_ISOPEN ) )
				{
					LogItemMovement( cr, locker, "closes" ); 
					PlayDoorSound( map, locker.HexX, locker.HexY, locker.GetProtoId(), locker.LockerCondition, 15 );
                    locker.LockerClose();
				}
                else
                {
                    ShowContainer( cr, locker, locker.Proto.GroundLevel ? TRANSFER_HEX_CONT_DOWN : TRANSFER_HEX_CONT_UP );
					LogItemMovement( cr, locker, "opens" );
					PlayDoorSound( map, locker.HexX, locker.HexY, locker.GetProtoId(), locker.LockerCondition, 15 );
                    locker.LockerOpen();
                }
				return true;
            }
            else
            {
                ShowContainer( cr, locker, locker.Proto.GroundLevel ? TRANSFER_HEX_CONT_DOWN : TRANSFER_HEX_CONT_UP );
				LogItemMovement( cr, locker, "looks into" );
				return true;
            }
        }
    }
    else if( skill == SK_STEAL )
    {
        if( locker.GetType() == ITEM_TYPE_CONTAINER )
        {
			if( ( !locker.Proto.Container_Changeble || FLAG( locker.LockerCondition, LOCKER_ISOPEN ) ) && !FLAG( locker.LockerCondition, LOCKER_LOCKED ) )
			{
				ShowContainer( cr, locker, locker.Proto.GroundLevel ? TRANSFER_HEX_CONT_DOWN : TRANSFER_HEX_CONT_UP );
				LogItemMovement( cr, locker, "looks using SK_STEAL into" );
				return true;
			}
			else if( FLAG( locker.LockerCondition, LOCKER_LOCKED ) )
			{
				cr.Say( SAY_NETMSG, "|0xFFFF00 The container is locked" );
			}
			else
			{
				cr.Say( SAY_NETMSG, "|0xFFFF00 The door is closed" );
			}
        }		
		return false;
	}
    else if( skill == SK_LOCKPICK )
    {
        if( cr.Timeout[ TO_SK_LOCKPICK ] > 0 && !isGM( cr ) )
        {
            cr.SayMsg( SAY_NETMSG, TEXTMSG_GAME, STR_SK_COOLDOWN );
			LogItemMovement( cr, locker, "can't lockpick because of tiredness" );
            return true;
        }

        int base = cr.Skill[ SK_LOCKPICK ] - locker.LockerComplexity;
        Item@lockpick = cr.GetItem( 0, SLOT_HAND1 );
        if( locker.LOCK_TYPE == 4 && valid( lockpick ) && lockpick.GetProtoId() != PID_ELECTRONIC_LOCKPICKS )
        {
			LogItemMovement( cr, locker, "can't lockpick without electronic lockpick" );
            cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_NEED_E_LOCKPICK );
            return true;
        }

        // Todo: PID_ELECTRONIC_LOCKPICKS +, PID_ELEC_LOCKPICK_MKII

        if( cr.GetAccess() != ACCESS_ADMIN )
		{
            base = CLAMP( base, 0, 95 );
		}
		
        if( locker.LockerComplexity == 500 && !isGM( cr ) )
		{
            cr.SayMsg( SAY_NETMSG, TEXTMSG_GAME, STR_SKILL_LOCKPICK_FAIL );
			LogItemMovement( cr, locker, "can't lockpick GM lock" );
			return true;
		}
        else if( locker.LockerCondition == LOCKER_ELECTRO )
        {
            uint16 complex = locker.LockerComplexity;
            uint16 exp = complex * 2;

            string tempCode = locker.Val4;
            uint8  codeLength = tempCode.length();
            uint16 skillLock = cr.Skill[ SK_LOCKPICK ];
            uint8  count = complex / codeLength;

            bool changeCode = false;

            for( uint8 i = 0; i < codeLength; i++ )
            {
                skillLock = ( skillLock >= count ? ( skillLock - count ) : 0 );
                if( complex >= skillLock )
                {
                    tempCode[ i ] = '*';
                    exp = ( exp >= count * 4 ? exp - ( count * 4 ) : 0 );

                    changeCode = false;
                }
            }
			cr.Say( SAY_NETMSG, "Code: |0xFFFF00 " + tempCode );
			LogItemMovement( cr, locker, "discovers code: " + tempCode );
			PlayGenericSound( map, cr.HexX, cr.HexY, "door_vault1_lock.mp3", 6 );
			return true;
        }
        else
        {
			LogItemMovement( cr, locker, "interacts with a lock" );
            return LockpickMenu( cr, locker );
        }
    }
    else if( skill == SKILL_TAKE_ALL_CONT )
    {
        return false;
    }

    return false;
}

void answer_LOCKPICK( Critter& player, uint answerI, string& answerS )
{ 
    Item@ lock = GetItem( player.Stat[ ST_VAR0 ] );
    if( lock is null )
	{
        return;
    }
	Item@lockpick = player.GetItem( 0, SLOT_HAND1 );
	Map@ map = player.GetMap();
	
	if( GetDistantion( lock.HexX, lock.HexY, player.HexX, player.HexY ) > 2 )
	{
		player.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_TOO_FAR_FROM_DOOR );
		return;
	}

    if( answerI == 0 )
    {
        map.SetTextMsg( lock.HexX, lock.HexY, COLOR_GRAY, TEXTMSG_TEXT, STR_MAPMSG_KNOCK );
        player.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_STR_YOU_KNOCK );
		LogItemMovement( player, lock, "knocks on the" ); 
    }
    if( answerI == 1 )
    {
		map.SetTextMsg( lock.HexX, lock.HexY, COLOR_RED, TEXTMSG_TEXT, STR_MAPMSG_LOUD_BANG );
		LogItemMovement( player, lock, "bangs on the" ); 
	
		return;
	}
    if( answerI == 2 )
    {
		if( hasKey( lock ) ) 
        {
			bool thisSide = hasKeyAround( player, lock );
			uint keySide = thisSide ? STR_KEY_THIS_SIDE : STR_KEY_OTHER_SIDE;
			player.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, keySide );
			LogCrimeMovement( player, lock, "can't lockpick because there is a key in the door" ); 
            return;
        }

		if( FLAG( lock.LockerCondition, LOCKER_ISOPEN ) && InteractContainer.find( lock.GetProtoId() ) == -1 )
        {
            player.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_DOOR_OPEN );
			LogCrimeMovement( player, lock, "can't lockpick - the door is open" ); 
            return;
        }
		
        if( lock.LOCK_TYPE == 0 )
        {
            player.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_NO_LOCK );
			LogCrimeMovement( player, lock, "cant lockpick - no lock" ); 
            return;
        }

		if( !valid( lockpick ) || ( lockpick.GetProtoId() != PID_FLocks && lockpick.GetProtoId() != PID_EXP_LOCKPICK_SET && lockpick.GetProtoId() != PID_LOCKPICKS ) )
        {
            player.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_NO_LOCKPICKING_TOOL );
			LogCrimeMovement( player, lock, "can't lockpick - no lockpicks" ); 
            return;
        }

		if( lock.LOCK_TYPE == 1 )
		{
            lock.STORED_CODE = Random( 1, 9 ) * 10 + Random( 1, 9 );
            lock.ENTERED_CODE = 11;
        }
        if( lock.LOCK_TYPE == 2 )
        {
            lock.STORED_CODE = Random( 1, 9 ) * 100 + Random( 1, 9 ) * 10 + Random( 1, 9 );
            lock.ENTERED_CODE = 111;
        }
        if( lock.LOCK_TYPE == 3 )
        {
            lock.STORED_CODE = Random( 1, 9 ) * 1000 + Random( 1, 9 ) * 100 + Random( 1, 9 ) * 10 + Random( 1, 9 );
            lock.ENTERED_CODE = 1111;
        }

        if( valid( lockpick ) && lockpick.LOCK_TYPE != 0 && ( lockpick.GetProtoId() == PID_FLocks || lockpick.GetProtoId() == PID_EXP_LOCKPICK_SET || lockpick.GetProtoId() == PID_LOCKPICKS) )
        {
			lockpick.LOCK_TYPE = 0;
            _SubItem( lockpick, 1 );
            player.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_LOCKPICKING_BROKEN );
			map.PlaySound( LockpickBreak[ Random( 0, LockpickBreak.length() -1 ) ], player.HexX, player.HexY, 2 );
			LogCrimeMovement( player, lock, "breaks the lockpick" ); 
            return;
        }
		else if( valid( lockpick ) && Random( 0, 1 ) == 0 )
		{
            lockpick.LOCK_TYPE = 10;
		}
		
        player.StatBase[ ST_VAR1 ] = 1;     // lockpick position
        player.StatBase[ ST_VAR2 ] = 0;     // remaining attempts

        player.ShowScreen( SCREEN_DIALOGBOX, 8, "answer_LOCKPICK_lock" );
        player.Say( SAY_DIALOGBOX_TEXT, "State of lock is unknown" );
        player.Say( SAY_DIALOGBOX_BUTTON( 0 ), "Examine the lock" );
        player.Say( SAY_DIALOGBOX_BUTTON( 1 ), "Lockpick further" );
        player.Say( SAY_DIALOGBOX_BUTTON( 2 ), "Lockpick backward" );
        player.Say( SAY_DIALOGBOX_BUTTON( 3 ), "Lockpick up" );
        player.Say( SAY_DIALOGBOX_BUTTON( 4 ), "Lockpick down" );
        player.Say( SAY_DIALOGBOX_BUTTON( 5 ), "Turn" );
        player.Say( SAY_DIALOGBOX_BUTTON( 6 ), "Remove lockpick" );
		player.Say( SAY_DIALOGBOX_BUTTON( 7 ), "Lock the lock" );
		
        player.TimeoutBase[ TO_SK_LOCKPICK ] = LOCKPICK_TIMEOUT( player );
		
		map.PlaySound( "ui_lockpicking_enter.wav", player.HexX, player.HexY, 2 );
		LogCrimeMovement( player, lock, "begins to break the lock, code is: " + lock.STORED_CODE ); 
    }

	if( answerI == 3 )
	{
		if( hasKey( lock ) )
		{
			if( hasKeyAround( player, lock ) )
			{
				LogItemMovement( player, lock, "takes out key from the lock" ); 
				takeKey( player, lock );
			}
			else
			{
				LogItemMovement( player, lock, "can't put in the key, there is one already from the other side" ); 
				player.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_KEY_OTHER_SIDE );
			}
		}
		else if( hasKey( player, lock ) ) 
		{
			LogItemMovement( player, lock, "inserts key in the lock" ); 
			putKey( player, lock );
		}
	}
	if( answerI == 4 )
	{
		if( !hasKey( lock ) )
		{
			if( FLAG( lock.LockerCondition, LOCKER_LOCKED ) )
			{
				UNSETFLAG( lock.LockerCondition, LOCKER_LOCKED );
				player.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_UNLOCK_DOOR );
				LogItemMovement( player, lock, "unlocks" ); 
				player.SayMsg( SAY_EMOTE_ON_HEAD, TEXTMSG_TEXT, STR_EMOTE_UNLOCKS );
				PlayGenericSound( map, player.HexX, player.HexY, "ui_lockpicking_unlock.wav", 6 );
			}
			else
			{
				SETFLAG( lock.LockerCondition, LOCKER_LOCKED );
				player.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_LOCK_DOOR );
				LogItemMovement( player, lock, "locks" ); 
				player.SayMsg( SAY_EMOTE_ON_HEAD, TEXTMSG_TEXT, STR_EMOTE_LOCKS );
				PlayGenericSound( map, player.HexX, player.HexY, "ui_lockpicking_unlock.wav", 6 );
			}
		}
		else
		{
			bool thisSide = hasKeyAround( player, lock );
			if( thisSide )
			{
				if( FLAG( lock.LockerCondition, LOCKER_LOCKED ) )
				{
					UNSETFLAG( lock.LockerCondition, LOCKER_LOCKED );
					player.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_UNLOCK_DOOR );
					player.SayMsg( SAY_EMOTE_ON_HEAD, TEXTMSG_TEXT, STR_EMOTE_UNLOCKS );
					LogItemMovement( player, lock, "unlocks" );
					PlayGenericSound( map, player.HexX, player.HexY, "ui_lockpicking_unlock.wav", 6 );
				}
				else
				{
					SETFLAG( lock.LockerCondition, LOCKER_LOCKED );
					player.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_LOCK_DOOR );
					player.SayMsg( SAY_EMOTE_ON_HEAD, TEXTMSG_TEXT, STR_EMOTE_LOCKS );
					LogItemMovement( player, lock, "locks" );
					PlayGenericSound( map, player.HexX, player.HexY, "ui_lockpicking_unlock.wav", 6 );
				}
			}
			else 
			{ 
				player.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_KEY_OTHER_SIDE ); 
				LogCrimeMovement( player, lock, "can't lockpick - key on the other side" ); 
			}
		}

		LockpickMenu( player, lock );
	}
}

bool takeKey( Critter& cr, Item& locker )
{
	Map@ map = cr.GetMap();
	if( !valid( map ) )
	{
		return false;
	}
	
	Item@[] list = getItemsAround( locker );
	for( uint i = 0, len = list.length(); i < len; i++ )
	{
		if( list[i].GetType() == ITEM_TYPE_KEY && list[i].LockerId == locker.LockerId )
		{
			if( list[i].KEY_EVENT != 0 )
			{
				EraseTimeEvent( list[i].KEY_EVENT );
				list[i].KEY_EVENT = 0;
			}
			
			MoveItem( list[i], 1, cr );
			UNSETFLAG( list[i].Flags, ITEM_HIDDEN );
			list[i].Update();
			PlayGenericSound( map, cr.HexX, cr.HexY, "ui_lockpicking_enter.wav", 6 );
			cr.SayMsg( SAY_EMOTE, TEXTMSG_TEXT, STR_EMOTE_EJECT_KEY );
			return true;
		}
	}
	return false;
}

bool putKey( Critter& cr, Item& locker )
{
	Map@ map = cr.GetMap();
	if( !valid( map ) )
	{
		return false;
	}
	
	Item@[] keys;
	Item@ key = null;
	cr.GetItemsByType( ITEM_TYPE_KEY, keys );
	uint len = keys.length();
	if( len > 0 )
	{
		for( uint i = 0; i < len; i ++ )
		{
			if( keys[i].LockerId != locker.LockerId )
			{
				continue;
			}
			else
			{
				@key = keys[i];
			}
		}
	}
	
	if( !valid( key ) )
	{
		return false;
	}
	
	MoveItem( key, 1, map, cr.HexX, cr.HexY );
	SETFLAG( key.Flags, ITEM_HIDDEN );
	uint[] vals = { key.Id, locker.Id };
	key.KEY_EVENT = CreateTimeEvent( AFTER( REAL_DAY( 10 ) ), "e_KeyRust", vals, true );

	key.Update();
	PlayGenericSound( map, cr.HexX, cr.HexY, "ui_lockpicking_enter.wav", 6 );
	cr.SayMsg( SAY_EMOTE, TEXTMSG_TEXT, STR_EMOTE_INJECTS_KEY );
	return true;
}

uint e_KeyRust( uint[]@ values )
{
	if( !valid( values ) || values.length() != 2 )
	{
		return 0;
	}
	
	Item@ key = GetItem( values[0] );
	Item@ door = GetItem( values[1] );
	if( !valid( key ) || !valid( door ) )
	{
		return 0;
	}
	
	Map@ map = GetMap( key.MapId );
	
	if( !valid( map ) || !FLAG( key.Flags, ITEM_HIDDEN ) )
	{
		return 0;
	}
	
	UNSETFLAG( key.Flags, ITEM_HIDDEN );
	
	uint16 x = key.HexX, y = key.HexY;
	map.MoveHexByDir( x, y, GetDirection( door.HexX, door.HexY, key.HexX, key.HexY ), 1 );
	
	MoveItem( key, 1, map, x, y ); 
	
	return 0;
}

void answer_LOCKPICK_lock( Critter& player, uint answerI, string& answerS )
{
    Item@ lock = GetItem( player.Stat[ ST_VAR0 ] );
    if( !valid( lock ) )
	{
        return;
    }
	
	Map@ map = player.GetMap();
	if( !valid( map ) )
	{
		return;
	}
	
    int base = player.Skill[ SK_LOCKPICK ] - lock.LockerComplexity;
    base = CLAMP( base, 50, 300 );
	
    Item@ lockpick = player.GetItem( 0, SLOT_HAND1 );
	uint lockpickPid = 0;
	if( valid( lockpick ) )
	{
		lockpickPid = lockpick.GetProtoId();
		switch( lockpickPid )
		{
			case( PID_LOCKPICKS ):
				base += 25;
				break;
			case( PID_EXP_LOCKPICK_SET ):
				base += 50;
				break;
			default:
				break;
		}
	}
	
	if( GetDistantion( lock.HexX, lock.HexY, player.HexX, player.HexY ) > 2 )
	{
		player.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_TOO_FAR_BREAK_LOCKPICKS );
		map.PlaySound( LockpickBreak[ Random( 0, LockpickBreak.length() -1 ) ], player.HexX, player.HexY, 2 );
		_SubItem( lockpick, 1 );
		return;
	}

    string state = "";
    int curComb = 0;
	
	int curPos = int( ( lock.ENTERED_CODE % pow( 10, player.StatBase[ ST_VAR1 ] ) ) / pow( 10, player.StatBase[ ST_VAR1 ] - 1 ) );

    if( answerI == 0 )
    {
        if( lock.LOCK_TYPE < 2 )
		{
            state = "Padlock, ";
        }
		else if( lock.LOCK_TYPE == 2 )
		{
            state = "Door lock, ";
        }
		else if( lock.LOCK_TYPE == 3 )
		{
            state = "Safe lock, ";
		}
	}
	
    if( answerI == 1 )
    {
        if( player.Timeout[ TO_SK_LOCKPICK ] > int( REAL_MINUTE( 2 ) ) )
		{
            state = "The lockpick is stuck, you need to release it a bit and try again."; //"Щуп застрял, нужно чуть ослабить зажим и попробовать еще раз."
        }
		else if( player.StatBase[ ST_VAR1 ] < lock.LOCK_TYPE + 1 )
		{
            player.StatBase[ ST_VAR1 ]++;
			curPos = int( ( lock.ENTERED_CODE % pow( 10, player.StatBase[ ST_VAR1 ] ) ) / pow( 10, player.StatBase[ ST_VAR1 ] - 1 ) );
			
            state = "You have moved the lockpick further.";
			
            if( Random( 0, 2 ) == 0 )
			{
                player.TimeoutBase[ TO_SK_LOCKPICK ] = __FullSecond + REAL_SECOND( 2 );
			}
        }
		else
		{
            state = "The lockpick reached the end.";
		}
		PlayGenericSound( map, player.HexX, player.HexY, _GetOneRandom( LockpickMove ), 4 );
    }
	
    if( answerI == 2 )
    {
        if( player.Timeout[ TO_SK_LOCKPICK ] > int( REAL_MINUTE( 2 ) ) )
		{
            state = "The lockpick is stuck, you need to release it a bit and try again.";
		}
		else if( player.StatBase[ ST_VAR1 ] > 1 )
		{
            player.StatBase[ ST_VAR1 ]--;
			curPos = int( ( lock.ENTERED_CODE % pow( 10, player.StatBase[ ST_VAR1 ] ) ) / pow( 10, player.StatBase[ ST_VAR1 ] - 1 ) );
			
            state = "You moved the lockpick backwards.";
            if( Random( 0, 2 ) == 0 )
			{
                player.TimeoutBase[ TO_SK_LOCKPICK ] = __FullSecond + REAL_SECOND( 2 );
			}
		}
		else 
		{
            state = "The lockpick is in the closest position, you only have to remove it now."; //"Щуп уже в крайнем положении, остается только вытащить его."
		}
		PlayGenericSound( map, player.HexX, player.HexY, _GetOneRandom( LockpickMove ), 4 );
	}
	
    if( answerI == 3 )
    {
        curPos = int( ( lock.ENTERED_CODE % pow( 10, player.StatBase[ ST_VAR1 ] ) ) / pow( 10, player.StatBase[ ST_VAR1 ] - 1 ) );
        if( curPos < 9 )
        {
            curComb = int( ( lock.STORED_CODE % pow( 10, player.StatBase[ ST_VAR1 ] ) ) / pow( 10, player.StatBase[ ST_VAR1 ] - 1 ) );
            lock.ENTERED_CODE += int( pow( 10, player.StatBase[ ST_VAR1 ] - 1 ) );
            curPos++;
            state = "You have moved the lock toung up with a lockpick."; //"Вы подвинули щупом язычек замка вверх."
            if( player.Timeout[ TO_SK_LOCKPICK ] > 0 && Random( 0, 2 ) == 0 )
			{
				PlayGenericSound( map, player.HexX, player.HexY, "ui_lockpicking_forcefail.wav", 10 );
                state += "\nYou were not carefull and made a bad move."; //"\nВы поторопились и неловко дернули щупом."
                if( Random( 0, 1 ) == 0 )
				{
                    int bar = Random( 1, lock.LOCK_TYPE + 1 );
                    curComb = int( ( lock.STORED_CODE % pow( 10, bar ) ) / pow( 10, bar - 1 ) );
                    lock.STORED_CODE += int( ( Random( 1, 9 ) - curComb ) * pow( 10, bar - 1 ) );
                }
            }
			else if( curComb == curPos && base > Random( 0, 100 ) )
			{
                if( player.Skill[ SK_LOCKPICK ] < Random( 0, 200 ) )
				{
                    PlayGenericSound( map, player.HexX, player.HexY, "ui_lockpicking_forcefail.wav", 10 );
                }
				state += "\nYou hear a click.";
            }
			else if( ( curComb == ( curPos - 1 ) || curComb == ( curPos + 1 ) ) && base > Random( 0, 100 ) && Random( 0, 1 ) == 0 )
			{
                if( player.Skill[ SK_LOCKPICK ] < Random( 0, 200 ) )
				{
                   PlayGenericSound( map, player.HexX, player.HexY, "ui_lockpicking_forcefail.wav", 10 );
                }
				state += "\nYou hear a click.";
            }
			else if( ( curComb == ( curPos - 2 ) || curComb == ( curPos + 2 ) ) && base > Random( 0, 100 ) && Random( 0, 3 ) == 0 )
			{
                if( player.Skill[ SK_LOCKPICK ] < Random( 0, 200 ) )
				{
                    PlayGenericSound( map, player.HexX, player.HexY, "ui_lockpicking_forcefail.wav", 10 );
				}
                state += "\nYou hear a click.";
            }
			else if( base < Random( 0, 100 ) && Random( 0, 9 ) == 0 )
			{
                if( player.Skill[ SK_LOCKPICK ] < Random( 0, 200 ) )
				{
                   PlayGenericSound( map, player.HexX, player.HexY, "ui_lockpicking_forcefail.wav", 10 );
				}
                state += "\nYou hear a click, ";
            }
            player.TimeoutBase[ TO_SK_LOCKPICK ] = __FullSecond + REAL_SECOND( 10 - player.Skill[ SK_LOCKPICK ] * 30 / 100 );
        }
        else
		{
            state = "The lockpick has pushed the spring to it's maximim"; // "Щуп уперся, пружина язычка уже максимально зажата."
		}
		map.PlaySound( LockpickMove[ Random( 0, LockpickMove.length() -1 ) ], player.HexX, player.HexY, 2 );
    }
	
    if( answerI == 4 )
    {
        curPos = int( ( lock.ENTERED_CODE % pow( 10, player.StatBase[ ST_VAR1 ] ) ) / pow( 10, player.StatBase[ ST_VAR1 ] - 1 ) );
        if( curPos > 1 )
        {
            curComb = int( ( lock.STORED_CODE % pow( 10, player.StatBase[ ST_VAR1 ] ) ) / pow( 10, player.StatBase[ ST_VAR1 ] - 1 ) );
            lock.ENTERED_CODE -= int( pow( 10, player.StatBase[ ST_VAR1 ] - 1 ) );
            curPos--;
            state = "You have moved the lock toung down with a lockpick.";
            if( player.Timeout[ TO_SK_LOCKPICK ] > int( REAL_MINUTE( 2 ) ) )
			{
                PlayGenericSound( map, player.HexX, player.HexY, "ui_lockpicking_forcefail.wav", 10 );
                state += "\nYou were not carefull and made a bad move.";
                if( Random( 0, 1 ) == 0 )
				{
                    int bar = Random( 1, lock.LOCK_TYPE + 1 );
                    curComb = int( ( lock.STORED_CODE % pow( 10, bar ) ) / pow( 10, bar - 1 ) );
                    lock.STORED_CODE += int( ( Random( 1, 9 ) - curComb ) * pow( 10, bar - 1 ) );
                }
            }
			else if( curComb == curPos && base > Random( 0, 100 ) )
			{
                if( player.Skill[ SK_LOCKPICK ] < Random( 0, 200 ) )
				{
                    PlayGenericSound( map, player.HexX, player.HexY, "ui_lockpicking_forcefail.wav", 10 );
				}
                state += "\nYou hear a click.";
            }
			else if( ( curComb == ( curPos - 1 ) || curComb == ( curPos + 1 ) ) && base > Random( 0, 100 ) && Random( 0, 1 ) == 0 )
			{
                if( player.Skill[ SK_LOCKPICK ] < Random( 0, 200 ) )
				{
                    PlayGenericSound( map, player.HexX, player.HexY, "ui_lockpicking_forcefail.wav", 10 );
				}
                state += "\nYou hear a click.";
            }
			else if( ( curComb == ( curPos - 2 ) || curComb == ( curPos + 2 ) ) && base > Random( 0, 100 ) && Random( 0, 3 ) == 0 )
			{
                if( player.Skill[ SK_LOCKPICK ] < Random( 0, 200 ) )
				{
                    PlayGenericSound( map, player.HexX, player.HexY, "ui_lockpicking_forcefail.wav", 10 );
                }
				state += "\nYou hear a click.";
            }
			else if( base < Random( 0, 100 ) && Random( 0, 9 ) == 0 )
			{
                if( player.Skill[ SK_LOCKPICK ] < Random( 0, 200 ) )
				{
                    PlayGenericSound( map, player.HexX, player.HexY, "ui_lockpicking_forcefail.wav", 10 );
				}
                state += "\nYou hear a click.";
            }
            player.TimeoutBase[ TO_SK_LOCKPICK ] = __FullSecond + REAL_SECOND( 10 - player.Skill[ SK_LOCKPICK ] * 30 / 100 );
        }
		else
		{
            state = "The lockpick has pushed the spring to it's maximim.";
		}
		map.PlaySound( LockpickMove[ Random( 0, LockpickMove.length() -1 ) ], player.HexX, player.HexY, 2 );
	}
	
    if( answerI == 5 )
    {
        if( lock.STORED_CODE == lock.ENTERED_CODE )
        {
            if( lock.GetType() == ITEM_TYPE_CONTAINER )
			{
				ShowContainer( player, lock, lock.Proto.GroundLevel ? TRANSFER_HEX_CONT_DOWN : TRANSFER_HEX_CONT_UP );
            }
			else
			{
				lock.LockerOpen();
                player.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_LOCKPICKING_SUCCESS );
			}
			lock.LockerCondition = LOCKER_ISOPEN;
            lock.Update();
            
			if( valid( lockpick ) )
			{
                lockpick.LOCK_TYPE = 0;
            }
			PlayGenericSound( map, player.HexX, player.HexY, "ui_lockpicking_unlock.wav", 6 );
			
			LogCrimeMovement( player, lock, "successfully lockpicks" ); 
            return;
        }
		else if( player.StatBase[ ST_VAR2 ] < Random( 0, player.Skill[ SK_LOCKPICK ] / 2 ) )
		{
            state = "The lock won't budge."; //Замок не поворачивается.
            player.StatBase[ ST_VAR2 ]++;
			PlayGenericSound( map, player.HexX, player.HexY, "ui_lockpicking_picktension_01_lpm.wav", 6 );
        }
		else
		{
            player.SayMsg( SAY_NETMSG, TEXTMSG_GAME, STR_SKILL_LOCKPICK_FAIL );
            if( valid( lockpick ) && Random( 0, 1 ) == 0 )
			{
                lockpick.LOCK_TYPE = 0;
			}
			map.PlaySound( "ui_lockpicking_forcefail.wav", player.HexX, player.HexY, 5 );
            return;
        }
    }
    if( answerI == 6 )
    {
        if( player.StatBase[ ST_VAR1 ] > 1 )
		{
            state = "You have to move the lockpick to the first position.";
        }
		else
		{
            lockpick.LOCK_TYPE = 0;
            player.SayMsg( SAY_NETMSG, TEXTMSG_GAME, STR_LOCKPICK_EXTRACTED );
			PlayGenericSound( map, player.HexX, player.HexY, "ui_lockpicking_enter.wav", 6 );
            return;
        }
    }
	
    if( answerI == 7 )
    {
		lockpick.LOCK_TYPE = 0;
		int realskill = player.Skill[ SK_LOCKPICK ];
		switch( lockpickPid )
		{
			case( PID_LOCKPICKS ):
				realskill += 25;
				break;
			case( PID_EXP_LOCKPICK_SET ):
				realskill += 50;
				break;
			default:
				break;
		}
		
        if( ( realskill - lock.LockerComplexity ) < 0 )
		{
            player.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_SKILL_TOO_LOW_TO_LOCK );
			LogCrimeMovement( player, lock, "did not lock the door with a lockpick because of low skill" ); 
			return;
		}
		else if( FLAG( lock.LockerCondition, LOCKER_LOCKED ) )
		{
			player.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_DOOR_ALREADY_LOCKED );
			LogCrimeMovement( player, lock, "did not lock the door with a lockpick because the door is locked already" ); 
			return;
		}
		else if( FLAG( lock.LockerCondition, LOCKER_ISOPEN ) && InteractContainer.find( lock.GetProtoId() ) == -1 )
		{
			player.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_CLOSE_DOOR );
			LogCrimeMovement( player, lock, "did not lock the door with a lockpick because the door is open" ); 
			return;
        }
		else
		{
            player.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_LOCKED_DOOR );
			lock.LockerCondition = LOCKER_LOCKED;
			PlayGenericSound( map, player.HexX, player.HexY, "ui_lockpicking_unlock.wav", 6 );
			LogCrimeMovement( player, lock, "locks the door with a lockpick" ); 
            return;
		}
    }
	_CritAnimateUse( player );
    state += "\n\nToung position - " + curPos + "\nLockpick position - " + player.StatBase[ ST_VAR1 ];

	if( player.Param[ QST_GAMEMODE ] == GAME_TEST )
	{
		state += "\n Door code: " + lock.ENTERED_CODE + ", inputed code: " + lock.STORED_CODE;
	}
	
    player.ShowScreen( SCREEN_DIALOGBOX, 8, "answer_LOCKPICK_lock" );
    player.Say( SAY_DIALOGBOX_TEXT, state );
    player.Say( SAY_DIALOGBOX_BUTTON( 0 ), "Examine lock" );
    player.Say( SAY_DIALOGBOX_BUTTON( 1 ), "Lockpick further" );
    player.Say( SAY_DIALOGBOX_BUTTON( 2 ), "Lockpick backwards" );
    player.Say( SAY_DIALOGBOX_BUTTON( 3 ), "Lockpick up" );
    player.Say( SAY_DIALOGBOX_BUTTON( 4 ), "Lockpick down" );
    player.Say( SAY_DIALOGBOX_BUTTON( 5 ), "Turn" );
    player.Say( SAY_DIALOGBOX_BUTTON( 6 ), "Extract the lockpick" );
	player.Say( SAY_DIALOGBOX_BUTTON( 7 ), "Lock the lock" );
}

bool IsKeyAviable( Critter& cr, uint lockerId )
{
    if( lockerId == 0 )
	{
        return true;
	}
	
	Map@ map = cr.GetMap();
	if( !valid( map ) )
	{
		return false;
	}

    Item@[] keys;
    for( uint i = 0; i < cr.GetItems( -1, keys ); i++ )
	{
        if( keys[ i ].LockerId == lockerId )
		{
            return true;
		}
		
		if( keys[ i ].GetProtoId() == PID_KEY_BUNCH || keys[ i ].GetProtoId() == PID_FNKEYBT )
		{
			PlayGenericSound( map, cr.HexX, cr.HexY, "keys.ogg", 6 );
			
			Item@[] sub_keys;
			uint count = keys[ i ].GetItems( 0, sub_keys );

			for( uint j = 0; j < count; j++ )
			{
				if( sub_keys[j].LockerId == lockerId )
				{
					return true;
				}
			}
		}
	}
    return false;
}

uint GetKeyId( Critter& cr, uint lockerId )
{
    if( lockerId == 0 || _CritFingersDoorId( cr.Id ) == lockerId || _CritEyesDoorId( cr.Id ) == lockerId )
	{
        return 0;
    }
	Item@[] keys;
    for( uint i = 0, j = cr.GetItemsByType( ITEM_TYPE_KEY, keys ); i < j; i++ )
	{
        if( keys[ i ].LockerId == lockerId )
		{
            return keys[ i ].Id;
		}
	}
	return 0;
}

void SwitchState( Item& locker )
{
    if( locker.GetType() == ITEM_TYPE_DOOR || locker.GetType() == ITEM_TYPE_CONTAINER )
    {
        if( !FLAG( locker.LockerCondition, LOCKER_NOOPEN ) )
        {
            if( FLAG( locker.LockerCondition, LOCKER_ISOPEN ) )
			{
                locker.LockerClose();
            }
			else
			{
                locker.LockerOpen();
			}
		}
    }
}

void LockerBudge( Item& item )
{
    Map@ map = GetMap( item.MapId );
    if( !valid( map ) )
	{
        return;
	}
	string sound = item.GetType() == ITEM_TYPE_DOOR ? "SLDOORSC.mp3" : "ILCNTNRB.mp3"; // fix to sound database
    map.PlaySound( sound, item.HexX, item.HexY, 8 );
}

bool LockerRemoval( Critter& cr, Item@ item, Item@ targetItem )
{
	uint8 itemType = targetItem.GetType();
	if( ( ( itemType == ITEM_TYPE_DOOR || itemType == ITEM_TYPE_CONTAINER ) && targetItem.LOCK_TYPE != 0 && _LockerIsOpen( targetItem ) && !FLAG( targetItem.LockerCondition, LOCKER_LOCKED ) ) 
		|| ( itemType == ITEM_TYPE_CONTAINER && InteractContainer.find( targetItem.GetProtoId() ) != -1 && targetItem.LOCK_TYPE != 0 && !FLAG( targetItem.LockerCondition, LOCKER_LOCKED  ) ) )
	{
		uint16 pid_locker = 0;
		switch( targetItem.LOCK_TYPE )
		{
			case( 1 ):
				pid_locker = PID_LOCKER_LOW;
				break;
			case( 2 ):
				pid_locker = PID_LOCKER_MED;
				break;
			case( 3 ):
				pid_locker = PID_LOCKER_HARD;
				break;
			default:
				break;
		}
		
		if( pid_locker != 0 )
		{
			if( item.GetProtoId() == PID_MULTI_TOOL )
			{
				Item@ locker = cr.AddItem( pid_locker, 1 );
				locker.LOCK_TYPE = ( targetItem.LockerComplexity > 50 ? ( targetItem.LockerComplexity - 50 ) : 0 );
				locker.Update();
			}

			if( item.GetProtoId() == PID_CROWBAR )
			{
				switch( pid_locker )
				{
					case( PID_LOCKER_LOW ):
						pid_locker = 3;
						break;
					case( PID_LOCKER_MED ):
						pid_locker = 6;
						break;
					case( PID_LOCKER_HARD ):
						pid_locker = 10;
						break;
					default:
						pid_locker = 1;
						break;
				}
				cr.AddItem( PID_CRAFT_M_BARS, Random( 1, pid_locker ) );
			}
		}
		targetItem.LOCK_TYPE = 0;
		targetItem.CODEDOOR_CODE = 0;
		targetItem.LockerComplexity = 0;
		targetItem.LockerId = 0;
		targetItem.LockerCondition = LOCKER_ISOPEN;
		targetItem.Update();
		
		Map@ map = cr.GetMap();
		PlayGenericSound( map, cr.HexX, cr.HexY, "ILCNTNRC.mp3", 20 );
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_REMOVED_LOCK );
	}
	return true;
}

bool KeyBunchUse( Critter& cr, Item@ item, Item@ targetItem )
{
	Item@[] keys;
	uint count = item.GetItems( uint( -1 ), keys );
	Map@ map = cr.GetMap();
	if( valid( map ) )
	{
		PlayGenericSound( map, cr.HexX, cr.HexY, "keys.ogg", 6 );
	}

	for( uint i = 0; i < count; i++ )
	{
		if( keys[i].LockerId == targetItem.LockerId )
		{
			UseItemOnLocker( cr, targetItem,  keys[i] );
			return true;
		}
	}
	return false;
}

#endif //LOCKERS