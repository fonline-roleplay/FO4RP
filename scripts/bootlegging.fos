#ifndef BOOTLEGGING
#define BOOTLEGGING

#include "_utils.fos"
#include "bootlegging_h.fos"
#include "_ltp.fos"
#include "gathering_h.fos"
#include "firestarter_h.fos"
#include "explode_h.fos"
#include "speed_h.fos"

bool IsStill( Item& still )
{
	return Stills.find( still.GetProtoId() ) != -1;
}

bool UseItemOnStill( Critter& cr, Item@ item, Item@ still )
{
	if( still is null || !IsStill(still) ) {
		return false;
	}
	
    Map@ map = cr.GetMap();
 	if( map is null || item is null ) {
		return true;
	}
	
	if( hasItem( cr, PID_HOMEBREW ) || hasItem( cr, PID_HOMEBREW_PRT ) ) {
		TransferToStill( cr, item, still );
		
	} else if( hasItems( cr, FireFuel ) ) {
		FeedStillFire( cr, map, item, still );
		
	} else if( hasItem( cr, PID_BOTTLE_GLASS, SLOT_HAND1 ) || hasItem( cr, PID_SPIRIT_PRT, SLOT_HAND1 ) ) {
		start_spirit_out( cr, still );
		
	} else if( hasItems( cr, Igniters ) ) {
		IgniteStill( cr, item, still );
	}
	return true;
}

bool HasFuel( Item& still ) {
	return still.CURRENT_FUEL > 0;
}

bool IgniteStill( Critter& cr, Item& item, Item& still )
{	
	if( !HasFuel(still) )
	{
		cr.Say( SAY_NETMSG, "|0xFFFF00 Тут нечего разжигать, найдите подходящее топливо." );
		return false;
	}
	if( still.EVENT == 0 )
	{
		if( item.GetProtoId() == PID_FLINT && Random( 1, 300 ) > cr.Skill[SK_OUTDOORSMAN] )
		{
			_SubItem( item, 1 );
			cr.Say( SAY_NETMSG, "|0xFFFF00 Вы сточили поджиг." );
		}
		still.STATE = STATE_WORKING_100;
		_CritAnimateUse( cr );
		cr.Say( SAY_EMOTE, "Разжигает огонь" );
		still.PicMap = PIC_STILL_ANIM_ON;
		setAnimState( still );
		uint[] values = { still.Id };
		still.STORED_HEAT = FloatToBits( 1 );
		still.CURRENT_PRESSURE = FloatToBits( 1 );
		still.EVENT = CreateTimeEvent( AFTER( REAL_SECOND( 0 ) ), "e_StillBurn", values, true );
	}
	return true;
}

float Mass( Item& still )
{
    float result = 0.09f; // масса воздуха в пустом баке на 15л // air mass in empty still
    if( still.CURRENT_RAW_MATERIAL > 0 ) {
        result = still.CURRENT_RAW_MATERIAL / 1000.0f;
    }
    return result;
}

float last_temp = 0;

uint e_StillBurn( uint[]@ values )
{
	uint itemId = values[0];
	Item@ still = GetItem( itemId );
	
	if( valid( still ) ) {
		float System_heat = FloatFromBits( still.STORED_HEAT );
		float M = Mass(still);
		float boiling_Pressure_Mod = ( still.IMPURITIES / 5000.0f );
		
		if( still.STATE > STATE_PREPARE && System_heat >= 0.5f ) {
			
			uint16 hexX = 0, hexY = 0;
			Map@ map = still.GetMapPosition( hexX, hexY );
			map.PlaySound( "CHEMICAL.ACM", still.HexX, still.HexY, 5 );

			//Valve level effects burning time
			float valve_level = 0.0f;
			switch( still.STATE ) {
				case( STATE_WORKING_0 ):
					valve_level = 0.0f;
					break;
				case( STATE_WORKING_25 ):
					valve_level = 0.25f;
					break;
				case( STATE_WORKING_50 ):
					valve_level = 0.50f;
					break;
				case( STATE_WORKING_75 ):
					valve_level = 0.75f;
					break;
				case( STATE_WORKING_100 ):
					valve_level = 1.0f;
					break;
			}

			float Q_burning = FUEL_BURN_AMOUNT * valve_level; //const 45
			still.CURRENT_FUEL -= ( Q_burning * FUEL_MODIFIER ); // wastes fuel 50% slower than all other fires
			if( still.CURRENT_FUEL < 0 ) {
				still.CURRENT_FUEL = 0;
			}
			
			if( still.CURRENT_FUEL <= 0.0f ) {
				Q_burning = 0.0f;
			}
			
			System_heat += Q_burning;
			
			float TSP = ( THERM_WATER_TSP * M );
			
			float HOTTNESS = ( System_heat / TSP ) + ENVIROMENT_TEMPRATURE_K;
	
			float Q_loss = ( HOTTNESS / L_RESIST ) * 3.14f * THERM_STILL_HIGHT;
			
			System_heat -= Q_loss;
			
			still.STORED_HEAT = FloatToBits( System_heat );
			
			float T2 = ( System_heat / TSP ) + ENVIROMENT_TEMPRATURE_K;
			
			if( T2 >= MAX_TEMPRATURE ) {
				still.IMPURITIES++;
			}
			
			if( ( T2 >= MAX_TEMPRATURE + 100 ) && still.GetProtoId() != PID_STILL_S_BROKEN ) {
				for( uint i = 0; i < 6; i++ ) {
					uint16 tx = still.HexX, ty = still.HexY;
					map.MoveHexByDir( tx, ty, i, 1 );
					Critter@ victim = map.GetCritter( tx, ty );
					if( !map.IsHexPassed( tx, ty ) && !valid( victim ) ) {
						continue;
					}
					GenerateFire( map, tx, ty, 0, 2, 0 );
					still.ChangeProto( PID_STILL_S_BROKEN );
					still.PicMap = still.Proto.PicMap;
					still.STILL_SPIRIT_LEVEL = 0.0f;
					still.CURRENT_PRODUCT = 0.0f;
					still.STILL_SPIRIT_LEVEL = 0.0f;
					still.IMPURITIES = 100.0f;
					setAnimState( still );
				}
			}
			
			still.CURRENT_TEMPRATURE = FloatToBits( T2 );
			
			float Sys_ST = FloatFromBits( still.STORED_HEAT );
			float Cur_temp = FloatFromBits( still.CURRENT_TEMPRATURE ) - 273.15f;
			
			//Log( "TSP: "+ TSP + ", Горение: " + Q_burning + ", Потери: " + Q_loss + ", Накопл Тепло: " + Sys_ST + ", Т-текущая: " + Cur_temp + " dT: " + ( Cur_temp - last_temp ) );
			last_temp = Cur_temp;
			
			float CurrentTemp = FloatFromBits( still.CURRENT_TEMPRATURE );
			float boiling_temp = Boiling_Temperature_Calc( still );
			
			if(  CurrentTemp >= boiling_temp ) {
				Item@ steamFX = map.AddItem( still.HexX, still.HexY, PID_STILL_STEAM, 1 );
				uint[] vals = { steamFX.Id };
				steamFX.EVENT = CreateTimeEvent( AFTER( REAL_SECOND( 1 ) ), "e_steamFX", vals, true );
				
				if( still.CURRENT_RAW_MATERIAL > 0.0f ) {
					
					still.CURRENT_RAW_MATERIAL -= RAW_MATERIAL_LOSS;
					
					if( still.CURRENT_RAW_MATERIAL <= 0.0f ) {
						still.CURRENT_RAW_MATERIAL = 0.0f;
					}
					
					if( still.STILL_SPIRIT_LEVEL > 0.0f ) {
						if( still.STILL_SPIRIT_LEVEL > ALCOHOL_GAIN ) {
							still.STILL_SPIRIT_LEVEL -= ALCOHOL_GAIN;
							still.CURRENT_PRODUCT += ALCOHOL_GAIN;
						} else {
							still.CURRENT_PRODUCT += still.STILL_SPIRIT_LEVEL;
							still.STILL_SPIRIT_LEVEL = 0.0f;
						}					
					}
					
					float CurrentPressure = FloatFromBits( still.CURRENT_PRESSURE );
					CurrentPressure += boiling_Pressure_Mod;
					still.CURRENT_PRESSURE = FloatToBits( CurrentPressure );
						
					if( FloatFromBits( still.CURRENT_PRESSURE ) >= MAX_PRESSURE ) {
						CreateTimeEvent( AFTER( REAL_SECOND( Random( 5, 20 ) ) ), "e_Explode", values, true );
					}
				}
			}
			return FIRE_BURNING_TIME;
		}
		
		still.STATE = STATE_PREPARE; // заменить на STATE_FINISH как только будет выделение спирта
		if( still.GetProtoId() == PID_STILL_S ) {
			still.PicMap = still.Proto.PicMap;
			setAnimState( still );
		}
		still.EVENT = 0.0f;
		still.CURRENT_TEMPRATURE = 0.0f;
		still.STORED_HEAT = 0.0f;
		
		uint16 hexX=0, hexY=0;
		Map@ map = still.GetMapPosition( hexX, hexY );
		Item@ fireFX = map.AddItem( still.HexX, still.HexY, PID_STEAM, 1 );
		uint[] val = { fireFX.Id };
		fireFX.EVENT = CreateTimeEvent( AFTER( REAL_SECOND( 2 ) ), "e_fireFX", val, true );
		EraseTimeEvent( still.EVENT );
	}
	return 0;
}

uint Boiling_Temperature_Calc( Item& still ) 
{
	float BaseBoilingTemperature = 273.15f + 100.0f; // 100% C
	
	float currentAlcoholPercentage = 0;
	
	if( still.CURRENT_RAW_MATERIAL > 0 ) {
		currentAlcoholPercentage = still.STILL_SPIRIT_LEVEL * 100.0f / still.CURRENT_RAW_MATERIAL;
	}
	
	float stillBoilingTemperature = BaseBoilingTemperature - currentAlcoholPercentage;
	
	return stillBoilingTemperature;
}

bool FeedStillFire( Critter& cr, Map& map, Item& item, Item& still )
{
	if( still.CURRENT_FUEL == STILL_MAX_FUEL )
	{	
		cr.Say( SAY_NETMSG, "|0xFFFF00 Горючее уже некуда подкладывать, дайте ему прогореть!" );
		return false;
	}
	
	_SubItem( item, 1 );
	_CritAnimateUse(cr);
	if( item.GetProtoId() == PID_COAL ) {
		still.CURRENT_FUEL = MIN( still.CURRENT_FUEL + 5400, STILL_MAX_FUEL );
		cr.Say( SAY_EMOTE, "Подкладывает уголь" );
		map.PlaySound( "ROCK.ACM", still.HexX, still.HexY, 3 );
	} else {	
		still.CURRENT_FUEL = MIN( still.CURRENT_FUEL + 1800, STILL_MAX_FUEL );
		cr.Say( SAY_EMOTE, "Подкладывает топливо" );
		map.PlaySound( "BRDSWELL.ACM", still.HexX, still.HexY, 3 );
	}

	int occupancy_rate = ( still.CURRENT_FUEL * 100 / STILL_MAX_FUEL );
	cr.Say( SAY_NETMSG, "|0xFFFF00 Аппарат заполнен горючими материалами на " +occupancy_rate+ " %" );
	
	return true;
}

bool TransferToStill( Critter& cr, Item& homebrew, Item& still ) //export to main
{
	Map@ map = cr.GetMap();
    if( !valid( map ) ) { return false;
	}
	if( !valid( still ) ) { Log( "Потеря указателя на контейнер!" ); return false; 
	}
	if( still.STATE > STATE_PREPARE ) { cr.Say( SAY_NETMSG, "|0xFF0000 Аппарат не готов к загрузу браги." ); return false; 
	}
	if( homebrew.GetProtoId() != PID_HOMEBREW && homebrew.GetProtoId() != PID_HOMEBREW_PRT	) { cr.Say( SAY_NETMSG, "|0xFF0000 Вам кажется, что из этого спирт не выделить." ); return false; 
	}
	
	map.PlaySound( "homebrew_pouring.ogg", cr.HexX, cr.HexY, 5 );
	
	if( homebrew.GetProtoId() == PID_HOMEBREW ) {
		
		still.CURRENT_RAW_MATERIAL += 500;
		still.STILL_SPIRIT_LEVEL += 500 * homebrew.HB_SPIRIT_LEVEL / 100;
		still.IMPURITIES += homebrew.HB_POLUTION_LEVEL;
		
	} else if( homebrew.GetProtoId() == PID_HOMEBREW_PRT ) {
		
		still.CURRENT_RAW_MATERIAL += homebrew.REMAINING_USES * 100;
		still.STILL_SPIRIT_LEVEL += homebrew.REMAINING_USES * 100 * homebrew.HB_SPIRIT_LEVEL / 100;
		still.IMPURITIES += homebrew.REMAINING_USES * 100 * homebrew.HB_POLUTION_LEVEL / 100;
	}
		
	_SubItem( homebrew, 1 );
	cr.AddItem( PID_BOTTLE_GLASS, 1 );

	if( still.CURRENT_RAW_MATERIAL > MAX_RAW_MATERIAL ) {
		still.CURRENT_RAW_MATERIAL = MAX_RAW_MATERIAL;
		map.SetText( still.HexX, still.HexY, COLOR_LGRAY, ":брага переливается через край:" );
	}
	
	ShowContainer( cr, still, TRANSFER_HEX_CONT_UP );
	return true;
}

uint e_Explode( uint[]@ values )
{
	uint itemId = values[0];
	Item@ still = GetItem( itemId );
	if( valid( still ) )    
	{
		uint16 hexX=0, hexY=0;
		Map @map = still.GetMapPosition( hexX, hexY );
		ExplodeEx( map, hexX, hexY, PID_EXPLODE_MISSILE2, 0, Random( 70, 150 ), DAMAGE_EXPLODE, 3, 0 );
		map.AddItem( hexX, hexY, Ashes[ Random( 0, Ashes.length() - 1 ) ], 1 );
		Item@ fireFX = map.AddItem( hexX, hexY, PID_SMOKE, 1 );
		
		uint[] values = {fireFX.Id};
		fireFX.EVENT = CreateTimeEvent( AFTER( REAL_SECOND( 2 ) ),"e_fireFX", values, true );
		uint randomDebree = Random( 2, 5 );
		for( uint i = 0; i < randomDebree; i++ )
		{
			uint16 hx=still.HexX, hy=still.HexY;
			map.MoveHexByDir( hx, hy, Random( 0, 5 ), Random( 1, 3) );
			if( map.IsHexPassed( hx, hy ) )
			{
				map.AddItem( hx, hy, Wreckage[ Random( 0, Wreckage.length() -1 ) ] , 1 );
			}
		}
		still.EVENT = 0.0f;
		EraseTimeEvent( still.EVENT );
		uint16 tx=still.HexX, ty=still.HexY;
		DeleteItem( still );
		GenerateFire( map, tx, ty, 0, 2, 0 );
	}
	return 0;
}

uint e_fireFX( uint[]@ val )
{
	uint itemId = val[0];
	Item@ e_fireFX = GetItem( itemId );
	if( valid( e_fireFX ) )  
	{
		EraseTimeEvent( e_fireFX.EVENT );
		e_fireFX.EVENT = 0;
		DeleteItem( e_fireFX );
	}
	return 0;
}

uint e_steamFX( uint[]@ vals )
{
	uint itemId = vals[0];
	Item@ e_steamFX = GetItem( itemId );
	if( valid( e_steamFX ) )  
	{
		if( e_steamFX.CURRENT_TEMPRATURE >= 37500 ) {
			uint16 hexX=0, hexY=0;
			Map@ map = e_steamFX.GetMapPosition( hexX, hexY );
			map.PlaySound( "still_steam.ogg", hexX, hexY, 5 );
			return FIRE_BURNING_TIME;
		}
		
		EraseTimeEvent( e_steamFX.EVENT );
		e_steamFX.EVENT = 0;
		DeleteItem( e_steamFX );
	}
	return 0;
}

//========== TECHNICAL_ANIMATION_STATE_CHANGER ===============//
void setAnimState( Item& still )
{
	if( still.STATE > STATE_PREPARE ) {	
		UNSETFLAG( still.Flags, ITEM_SHOW_ANIM_EXT );
		SETFLAG( still.Flags, ITEM_LIGHT );
		SETFLAG( still.Flags, ITEM_CAN_USE );
		UNSETFLAG( still.Flags, ITEM_CAN_PICKUP );
	} else {
		SETFLAG( still.Flags, ITEM_SHOW_ANIM_EXT );
		UNSETFLAG( still.Flags, ITEM_LIGHT );
	}
	still.Update();
}

//=============== STILL STATE DISCRIPTION ====================//

string@ GenerateDescription(Item& still) {
	string@ info;
	bool is_working = false;
	switch( still.STATE ) {
		case STATE_PREPARE:
			@info = "Аппарат не работает.";
			break;
		default:
			@info = "Аппарат работает, заслонка: ";
			is_working = true;
	}
	if( is_working ) {
		switch( still.STATE ) {
			case STATE_WORKING_0:
				info += "Закрыта.";
				break;
			case STATE_WORKING_25:
				info += "Открыта на четверть.";
				break;
			case STATE_WORKING_50:
				info += "Открыта на половину.";
				break;
			case STATE_WORKING_75:
				info += "Открыта на три четверти.";
				break;
			case STATE_WORKING_100:
				info += "Полностью открыта.";
				break;
		}
		
		float temprature = FloatFromBits( still.CURRENT_TEMPRATURE ) - 273.15f;
		info += "\n" "Температура: " + temprature + " градусов C.";

		float pressure = FloatFromBits( still.CURRENT_PRESSURE );
		info += "\n" "Давление: " + pressure + " бар.";
	}

	float raw_material = still.CURRENT_RAW_MATERIAL / 1000.0f;
	float max_raw_material = MAX_RAW_MATERIAL / 1000.0f;
	info += "\nСырье: " + raw_material + " из " + max_raw_material + " литров.";

	string occupancy_level = ( still.CURRENT_FUEL * 100.0f / STILL_MAX_FUEL );
	info += "\nТопливо: " + occupancy_level + " %";

	float product = still.CURRENT_PRODUCT / 1000.0f;
	info += "\nВыделено " + product + " литров спирта.";

	return info;
}

//=============== MAIN MENU ====================//

void ShowMenuStill( Critter& cr, Item& still ) //exported
{
	Map@ map = cr.GetMap();
    if( still is null || map is null ) {
        return;
    }

    iMenuHandler@ handler = MenuStill(still, map);
    iDialogBox@ menu = OpenMenu(cr, "Дестилятор", handler);
}

class MenuStill: CenteredMenuHandler {
	uint m_still_id;
	uint m_map_id;
	int m_last_state;
	int difficulty;
	
	MenuStill( Item& still, Map& map ) {
		m_still_id = still.Id;
		m_map_id = map.Id;
	}
	bool MenuUpdate( Critter& cr, iDialogBox& menu ) {
		Item@ still = GetItem(m_still_id);
		Map@ map = GetMap(m_map_id);
		if( still is null || map is null ) {
			return false;
		}

		bool locked_in_place = !FLAG( still.Flags, ITEM_CAN_PICKUP );

		// Все действия которые можно выполнить с закреплённым аппаратом
		if( locked_in_place ) {
			if ( still.STATE == STATE_PREPARE ) { // Который не начал работу
				if( menu.Button( "Демонтировать" ) ) {
					LockInPlace( cr, still );
					return true;
				}
				
				if( still.GetProtoId() != PID_STILL_S_BROKEN ) {
					if( menu.Button( "Добавить брагу" ) ) {
						ShowContainer( cr, still, TRANSFER_HEX_CONT_UP );
						return false; // чтобы не перебивать окно контейнера
					}
					
					if ( still.STATE < STATE_FINISH ) { // Который не начал работу или работает
						if( menu.Button( "Подкинуть горючее" ) ) {
							Item@ fuel = FindStillFuel(cr);
							if( valid( fuel ) ) {
								FeedStillFire( cr, map, fuel, still ); 
							} else {
								cr.Say( SAY_NETMSG, "|0xFF0000 У Вас нет подходящего топлива." ); 
							}				
							return true;
						}
					}
										
					if( HasFuel( still ) && menu.Button( "Разжечь огонь" ) ) {
						Item@ lighter = cr.GetItem( PID_LIGHTER, -1 );
						if( lighter is null ) {
							@lighter = cr.GetItem( PID_FLINT, -1 );
						}
						if( lighter is null ) {
							cr.Say( SAY_NETMSG, "|0xFFFF00 Вам нечем разжечь огонь." );
						} else {
							IgniteStill( cr, lighter, still );
						}
						return true;
					}
				
					if( menu.Button( "Провести очиситку" ) ) { // Чистка аппарата
						
						string fail = "|0xFFFF00 Вам нечем почистить самогонный аппарат!\n";
						if( !checkConsumables( cr ) ) { 
							string consumable = "|0x3CF800 Необходимые инструменты:\n";
							for( uint i = 0; i < Consumables.length; i++ ) {
								consumable += " - ";
								consumable += "|0xFFFF00 " + _GetProtoName( Consumables[i] );
								consumable += "|0x3CF800 ,\n";
							}	
							consumable[ consumable.length() - 2 ] = '.';
							consumable[ consumable.length() - 1 ] = ' ';
							cr.Say( SAY_NETMSG, fail + consumable );
							return true;
						}
						difficulty = still.IMPURITIES * 50;
						start_stillRepair( cr, still, difficulty );
						return false;
					}
					
					if( still.CURRENT_PRODUCT > 0 && menu.Button( "Разлить спирт" ) ) {
						start_spirit_out( cr, still );
						return false;
					}
									
				} else {
					if( menu.Button( "Отремонтировать" ) ) {
						
						string fail = "|0xFFFF00 Вы не смогли починить самогонный аппарат!\n";
						
						if( !checkSkill( cr ) ) {
							string skill = "|0x3CF800 Необходимо иметь навыки: \n";
							skill += " - ";
							skill += "|0xFFFF00 Наука";
							skill += "|0x3CF800  - ";
							skill += "|0xFFFF00 150";
							skill += "|0x3CF800 ,\n - ";
							skill += "|0xFFFF00 Ремонт";
							skill += "|0x3CF800  - ";
							skill += "|0xFFFF00 200";
							skill += "|0x3CF800 .";
							cr.Say( SAY_NETMSG, fail + skill ); 
							return true;
						}
						
						if( !checkTools( cr ) ) { 
							string tool = "|0x3CF800 Необходимые инструменты:\n";
							for( uint i = 0; i < Tools.length; i++ ) {
								tool += " - ";
								tool += "|0xFFFF00 " + _GetProtoName( Tools[i] );
								tool += "|0x3CF800 ,\n";
							}	
							tool[ tool.length() - 2 ] = '.';
							tool[ tool.length() - 1 ] = ' ';
							cr.Say( SAY_NETMSG, fail + tool );
							return true;
						}
						
						if( !checkComponents( cr ) ) {
							string component = "|0x3CF800 Необходимые материалы:\n";
							for( uint i = 0; i < Components.length; i++ ) {
								component += " - ";
								component += "|0xFFFF00 " + _GetProtoName( Components[i] );
								component += "|0x3CF800  - ";
								component += "|0xFFFF00 " + ComponentsAmount[ i ];
								component += "|0x3CF800  шт.,\n";
							}			
							component[ component.length() - 2 ] = ' ';
							component[ component.length() - 1 ] = ' ';
							cr.Say( SAY_NETMSG, fail + component );
							return true;
						}
						difficulty = 10000;
						start_stillRepair( cr, still, difficulty );
						return false;
					}
					
				}
				
			} else if( still.STATE < STATE_FINISH ) { // Который начал, но не закончил работу
				if ( menu.Button( "Закрыть заслонку" ) ) {
					still.STATE = STATE_WORKING_0;
					return true;
				}
				if ( menu.Button( "На четверть" ) ) {
					still.STATE = STATE_WORKING_25;
					return true;
				}
				if ( menu.Button( "На половину" ) ) {
					still.STATE = STATE_WORKING_50;
					return true;
				}
				if ( menu.Button( "На три четверти" ) ) {
					still.STATE = STATE_WORKING_75;
					return true;
				}
				if ( menu.Button( "Открыть полностью" ) ) {
					still.STATE = STATE_WORKING_100;
					return true;
				}
			}

		} else {
			if( menu.Button( "Установить" ) ) {
				LockInPlace( cr, still );
				return true;
			}
			if( menu.Button( "Поднять" ) ) {
				PickItem( cr, still );
				ChangeCritterSpeed( cr );
				return false; // предмет уже не на земле
			}
		}
		if( menu.Button( "Ничего не делать" ) ) {
			return false;
		}
		return true;
	}

	string@ Description( Critter& cr ) {
		Item@ still = GetItem(m_still_id);
		m_last_state = still.STATE;
		return GenerateDescription(still);
	}

	bool ShouldRedraw( Critter& cr ) {
		Item@ still = GetItem( m_still_id );
		return still is null || IsStillWorking(still) || still.STATE != m_last_state;
    }
}

bool IsStillWorking( Item& still ) {
	return still.STATE > STATE_PREPARE && still.STATE < STATE_FINISH;
}

Item@ FindStillFuel( Critter& cr ) {
	Item@ fuel = null;
	for( uint i = 0, j = 9; i < j; i++ )
	{
		@fuel = @cr.GetItem( FireFuel[i], -1 );
		if( fuel !is null ) {
			break;
		}
	}
	return fuel;
}

bool ltp_inited = false;
void ltp_init()
{
	LTPREG( LTP_SPIRIT_OUT, process_spirit_out )
	ltp_inited = true;
}

bool start_spirit_out( Critter& cr, Item& still )
{
	if( !ltp_inited ) { 
		ltp_init();
	}
	Map@ map = cr.GetMap();		

	if( valid( still ) ) {
		if( ( still.CURRENT_PRODUCT / 100 ) < 1 ) { cr.Say( SAY_NETMSG, "|0xFFFF00 Недостаточно жидкости для перелива." ); return false;
		}
		
		if( !hasItem( cr, PID_BOTTLE_GLASS ) && !hasItem( cr, PID_SPIRIT_PRT ) ) { cr.Say( SAY_NETMSG, "|0xFFFF00 У вас нет подходящей тары." ); return false;
		}
		
		uint hit_pause = ACTION_PAUSE_BASE - ( cr.Stat[ST_AGILITY] * ACTION_PAUSE_BONUS_RATE );
		uint action_pause = CLAMP( hit_pause, ACTION_PAUSE_MIN, ACTION_PAUSE_MAX );
		
		map.PlaySound( "homebrew_pouring.ogg", cr.HexX, cr.HexY, 5 );
		
		cr.SetAnims( COND_LIFE, 0, ANIM2_CROUCH );
		
		CreateTimeEvent( AFTER( REAL_MS( 1000 ) ), "gathering@e_TiredenessTimeout", cr.Id, true);	
		
		StartProcess( cr, LTP_SPIRIT_OUT, 0, 0, still.Id, action_pause );
		return true;
	}
	return false;
}

uint process_spirit_out( Critter@ cr, int& param0, int& param1, int& param2 )
{
    LTPROCESS( cr, param0, LTP_SPIRIT_OUT )
	uint hit_pause = ACTION_PAUSE_BASE - ( cr.Stat[ST_AGILITY] * ACTION_PAUSE_BONUS_RATE );
	uint action_pause = CLAMP( hit_pause, ACTION_PAUSE_MIN, ACTION_PAUSE_MAX );
  	Item@ still = GetItem( param2 );
	
  	if( valid( still ) ) {
		
      	if( ( !hasItem( cr, PID_BOTTLE_GLASS ) && !hasItem( cr, PID_SPIRIT_PRT ) ) || ( still.CURRENT_PRODUCT / 100 ) < 1 ) {
			string TXT = ( ( still.CURRENT_PRODUCT / 100 ) < 1 ) ? " Вы разлили весь спирт по бутылкам." :  " У Вас закончилась тара.";
			cr.Say( SAY_NETMSG, "|0xFFFF00 " + TXT );
			cr.SetAnims( COND_LIFE, 0, ANIM2_IDLE );
			return 0;
		}
		
		uint autoemote = Random( 1, 10 );
		if( autoemote == 10 ) { cr.Say( SAY_EMOTE_ON_HEAD, "Наполняет бутылку" );
		}
		
		if( hasItem( cr, PID_SPIRIT_PRT ) ) {
			
			Item@ spiritPrt = getItem( cr, PID_SPIRIT_PRT );
			if( spiritPrt.REMAINING_USES < spiritPrt.Proto.Partial_Item ) {
				
				still.CURRENT_PRODUCT -= 100;
				spiritPrt.REMAINING_USES ++;
				spiritPrt.EXTRA_WEIGHT = spiritPrt.REMAINING_USES * 100;

				Map@ map = cr.GetMap();
				map.PlaySound( "homebrew_pouring.ogg", cr.HexX, cr.HexY, 5 );
				spiritPrt.Update();
				
				if( spiritPrt.REMAINING_USES == spiritPrt.Proto.Partial_Item ) {
			
					Item@ spirit = cr.AddItem( PID_SPIRIT, 1 );
					_SubItem( spiritPrt , 1 );
					cr.Say( SAY_NETMSG, "|0xFFFF00 Вы полностью наполнили бутылку спиртом" );
					spirit.Update();
				}
			}

			CreateTimeEvent( AFTER( REAL_MS( 1000 ) ), "gathering@e_TiredenessTimeout", cr.Id, true );
			
			return action_pause;
			
		} else {
		
			Item@ bottle = cr.GetItem( PID_BOTTLE_GLASS, -1 );
			_SubItem( bottle , 1 );
		
			Item@ spiritPrt = cr.AddItem( PID_SPIRIT_PRT, 1 );
		
			still.CURRENT_PRODUCT -= 100;
			spiritPrt.REMAINING_USES ++;
			spiritPrt.EXTRA_WEIGHT = spiritPrt.REMAINING_USES * 100;

			Map@ map = cr.GetMap();
			map.PlaySound( "homebrew_pouring.ogg", cr.HexX, cr.HexY, 5 );
				
			spiritPrt.Update();
		}
		
		CreateTimeEvent( AFTER( REAL_MS( 1000 ) ), "gathering@e_TiredenessTimeout", cr.Id, true);
		
		return action_pause;
    }
	cr.SetAnims(COND_LIFE, 0, ANIM2_IDLE);
	return 0;
}

bool ltp_stillRepair_inited = false;
void ltp_stillRepair_init()
{
	LTPREG( LTP_STILL_REPAIR, process_stillRepair )
	ltp_stillRepair_inited = true;
}

bool start_stillRepair( Critter& cr, Item& still, uint difficulty )
{
	if(!ltp_stillRepair_inited) 
		ltp_stillRepair_init();
	
	uint hit_pause = ACTION_PAUSE_BASE - (cr.Stat[ST_AGILITY] * ACTION_PAUSE_BONUS_RATE );
	uint action_pause = CLAMP( hit_pause, ACTION_PAUSE_MIN, ACTION_PAUSE_MAX );
	uint[] values = { cr.Id };
	
	CreateTimeEvent( AFTER( REAL_MS( 700 ) ), "repair@e_RepairSound", values, true);
	CreateTimeEvent( AFTER( REAL_MS( 1000 ) ), "gathering@e_TiredenessTimeout", values, true);	

	_CritAnimateUse( cr );
	
	StartProcess( cr, LTP_STILL_REPAIR, 0, still.Id, difficulty, action_pause );
	return true;
}

uint process_stillRepair( Critter@ cr, int& param0, int& param1, int& param2 )
{
	LTPROCESS( cr, param0, LTP_STILL_REPAIR )
	
	uint hit_pause = ACTION_PAUSE_BASE - (cr.Stat[ST_AGILITY] * ACTION_PAUSE_BONUS_RATE );
	uint action_pause = CLAMP( hit_pause, ACTION_PAUSE_MIN, ACTION_PAUSE_MAX );
  	Item@ still = GetItem( param1 );
	int difficulty = param2 ;
	if( valid( still ) )
    {
		param0++;
		
		uint install_rate = ( cr.Skill[SK_REPAIR] ) + ( cr.Stat[ST_AGILITY] * 10 ) + ( cr.Stat[ST_INTELLECT] * 10 ) + OBJECT_DPA_BASE; 
		int sequence_length = ceil( float( difficulty / ( CLAMP( install_rate, OBJECT_DPA_MIN, OBJECT_DPA_MAX ) ) ) );
		
		if( param0 > sequence_length )
		{
			
			if( difficulty >= 10000 ) {
				for( uint i = 0; i < Components.length; i++ ) {
					Item@ component = cr.GetItem( Components[i], -1 );
					uint required_amount = ComponentsAmount[i];
					if( !valid( component ) || cr.CountItem( Components[i] ) < required_amount ) {
						return 0;
					}
					_SubItem( component, required_amount);
				}
				cr.Say( SAY_EMOTE, "завершает ремонт самогонного аппрата" );
				still.ChangeProto( PID_STILL_S );
				still.PicMap = still.Proto.PicMap;
				still.Update();
			} else {
				for( uint i = 0; i < Consumables.length; i++ ) {
					Item@ consumable = cr.GetItem( Consumables[i], -1 );
					if( !valid( consumable ) ) {
						return 0;
					}
					_SubItem( consumable, 1 );
				}
				cr.AddItem( PID_PHIAL, 1 );
				cr.AddItem( PID_BOTTLE_GLASS, 1 );
				cr.Say( SAY_EMOTE, "завершает обслуживание самогонного аппрата" );
				still.IMPURITIES = 0;
				still.CURRENT_RAW_MATERIAL = 0;
				still.STILL_SPIRIT_LEVEL = 0;
				still.CURRENT_PRODUCT = 0;
				still.Update();
			}
			return 0;
		}
		
		uint[] values = { cr.Id };
		
		CreateTimeEvent( AFTER( REAL_MS( 700 ) ), "repair@e_RepairSound", values, true);
		CreateTimeEvent( AFTER( REAL_MS( 1000 ) ), "gathering@e_TiredenessTimeout", values, true);

		_CritAnimateUse( cr );
		
		return action_pause;
	}
	return 0;
}

bool checkSkill( Critter& cr ) {
	if( cr.ParamBase[  SK_REPAIR ] < 200 || cr.ParamBase[ SK_SCIENCE ] < 150 ) { return false; 
	}
	return true;
}

bool checkTools( Critter& cr ) {
	for( uint i = 0; i < Tools.length; i++ ) {
		Item@ tool = cr.GetItem( Tools[i], -1 );
		if( !valid( tool ) ) {
			return false;
		}
	}
	return true;
}

bool checkComponents( Critter& cr ) {
	for( uint i = 0; i < Components.length; i++ ) {
		Item@ component = cr.GetItem( Components[i], -1 );
		uint required_amount = ComponentsAmount[i];
		if( !valid( component ) || cr.CountItem( Components[i] ) < required_amount ) {
			return false;
		}
	}
	return true;
}

bool checkConsumables( Critter& cr ) {
	for( uint i = 0; i < Consumables.length; i++ ) {
		Item@ tool = cr.GetItem( Consumables[i], -1 );
		if( !valid( tool ) ) {
			return false;
		}
	}
	return true;
}

#endif //BOOTLEGGING