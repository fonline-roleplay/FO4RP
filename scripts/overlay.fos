#include "_utils.fos"

bool overlay_hide = false;

bool want_connect = false;
bool want_disconnect = false;

// import void overlay_connect() from "overlay";

import string@ UrlPrefixOverlay() from "link";
void overlay_connect() {
    string@ web = UrlPrefixOverlay();
    if (web !is null) {
        OverlayConnect("127.0.0.1:33741", web);
    }
}

// import void overlay_finish() from "overlay";
void overlay_disconnect() {
    OverlayDisconnect(true);
}

// import void overlay_visibility() from "overlay";
void overlay_visibility() {
    overlay_hide = !overlay_hide;
    OverlayHide(overlay_hide);
}

// import void overlay_loop(bool show) from "overlay";
void overlay_loop( bool show ) 
{
    CritterCl@[] critters;
    if( show ) {
        CritterCl@[] all_critters;
        CritterCl@ crbearing;
        uint len = GetCritters( 0, FIND_ALL, all_critters );
        for( uint i = 0; i < len; i++ ) {
            @ crbearing = all_critters[ i ];
            if( crbearing !is null && should_send_critter(crbearing) ) { 
				critters.insertLast( crbearing ); 
			}
        }
    }
    UpdateAvatars( critters );
}

bool should_send_critter( CritterCl& cr ) 
{
	// Check if cr knows and sees name 
	if( ABS( cr.Param[ QST_MEDIUM ] ) == 2 )
	{
		return false;
	}
	// Hideface item check
	int param = cr.ParamBase[ CR_HIDEFACE_ITM ];
	if( FLAG( param, ITM_FACE_MASK ) )
	{
		return false;
	}
	else if( FLAG( cr.Param[ PLAYER_FLAGS ], PLAYER_FLAG_HIDE_FACE ) )
	{
		if( FLAG( param, ITM_ARMOR_HOOD ) || FLAG( param, ITM_ARMOR_SUIT ) )
		{
			return false;
		}
	}
  return true;
}

// import void overlay_message(string& message, int& sayType, uint& critterId, uint& delay) from "overlay";

void overlay_message(string& message, int& sayType, uint& critterId, uint& delay) {
    string@ name = null;
    bool masked = false;
    if( sayType == SAY_RADIO ) {
        @name = "Radio";
    } else if ( sayType > SAY_WHISP_ON_HEAD ) {
        return;
    } else {
        CritterCl@ cr = null;
        if( critterId != 0 ) {
            @name = GetName(critterId);
            @cr = GetCritter(critterId);
        }
        if( critterId >= 5000000) {
            if ( name is null ) {
                if( cr !is null && cr.IsNpc()) {
                    @name = cr.Name;
                }
            }
        } else {
            if ( critterId != 0 && ( __sinf & SINF_LOGIN ) != 0 ) {
                if( cr !is null && cr.IsPlayer() ) {
                    if( name is null ) {
                        @name = "("+cr.Name+")";
                    } else {
                        @name = name + " ("+cr.Name+")";
                    }
                }
            }
        }
        if( cr !is null) {
            CritterCl@ self = GetChosen();
            bool cr_is_self = (self !is null) && (self.Id == cr.Id);
            masked = !cr_is_self && !should_send_critter(cr);
        }
    }
    OverlayMessage(message, sayType, critterId, delay, name, masked);
}
