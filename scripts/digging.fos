#ifndef DIGGING
#define DIGGING

#include "_utils.fos"
#include "_ltp.fos"
#include "gathering_h.fos"
#include "firestarter_h.fos"
#include "combat_h.fos"

#define STASH_OWNER = Val2

import uint8 checkSand( Map& map, uint16 tx, uint16 ty ) from "qmap_sand";

void TryGetSand( Critter& cr, Item& shovel, int hexX, int hexY ) 
{
	Map@ map = cr.GetMap();
	if( !valid( map ) )
	{
		return;
	}
	
	if( GetDistantion( cr.HexX, cr.HexY, hexX, hexY ) > 1 )
	{
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_DIG_CLOSER );
		return;
	}
	
	uint8 sand_flags = 0; 	
	sand_flags = checkSand( map, H2T( hexX ), H2T( hexY ) );
	
	if( sand_flags == 0 )
	{
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_NO_SAND_FOUND );
		return;
	}
	
	PrepareDigging( cr, shovel );
}

bool ltp_digging_inited = false;
void ltp_digging_init()
{
	LTPREG( LTP_DIGGING, process_digging )
	ltp_digging_inited = true;
}

bool ltp_dig_stash_inited = false;
void ltp_dig_stash_init()
{
	LTPREG( LTP_DIG_STASH, process_dig_stash )
	ltp_dig_stash_inited = true;
}

bool PrepareDigging( Critter& cr, Item@ tool ) 
{
	Map@ map = cr.GetMap();
    if( !valid( map ) )
	{
		return false;
	}
	
	if( !valid( tool ) )
	{
		return false;
	}

   	uint toolPid = tool.GetProtoId();
	if( toolPid != PID_SHOVEL && toolPid != PID_SPADE )
	{
		return false;
	}

	if( tool.CritSlot != SLOT_HAND1 )
	{
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_EQUIP_TOOL );
		return false;
	}

	if( IsTired( cr ) )
	{
		return false;
	}
	
	if( cr.IsDmgTwoArm() || cr.IsDmgTwoLeg() )
	{ 
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_INJURED_CANT_WORK );
		return false; 
	}
	
	start_digging( cr, tool );
	return true;
}

bool start_digging( Critter& cr, Item& tool )
{
	if( !ltp_digging_inited )
	{
		ltp_digging_init();
	}
	
	if( valid( tool ) )
	{	
		uint hit_pause = ACTION_PAUSE_BASE - (cr.Stat[ST_AGILITY] * ACTION_PAUSE_BONUS_RATE );
		cr.ParamBase[ ST_LTP_SEQUENCE_WAIT ] = CLAMP( hit_pause, ACTION_PAUSE_MIN, ACTION_PAUSE_MAX );
		uint digging_rage = cr.Skill[ SK_OUTDOORSMAN ] + ( cr.Stat[ ST_STRENGTH ] * 10 ) + OBJECT_DPA_BASE; 
		cr.ParamBase[ ST_LTP_SEQUENCE ] = uint( ceil( float( ( OBJECT_HP_BASE ) / ( CLAMP( digging_rage, OBJECT_DPA_MIN, OBJECT_DPA_MAX ) ) ) ) );
		
		uint[] values = { cr.Id, tool.Id };
		CreateTimeEvent( AFTER( REAL_MS( 700 ) ), "e_diggingSound", values, false);
		CreateTimeEvent( AFTER( REAL_MS( 1000 ) ), "gathering@e_TiredenessTimeout", cr.Id, true);	
		_CritAnimateSwing(cr);
		StartProcess( cr, LTP_DIGGING, 0, tool.Id, 0, cr.ParamBase[ ST_LTP_SEQUENCE_WAIT ] );
		return true;
	}
	return false;
}

uint process_digging( Critter@ cr, int& param0, int& param1, int& param2 )
{
    LTPROCESS( cr, param0, LTP_DIGGING )

    Item@ tool = GetItem( param1 );
  	if( valid( tool ) )
    {
      	param0++;
		
		if( param0 > cr.ParamBase[ ST_LTP_SEQUENCE ] )
		{
			uint16 lootPid = PID_SAND;
			uint amount = 1;
			
			if( cr.Skill[ SK_OUTDOORSMAN ] > 50 )
			{
				uint len = cr.Skill[ SK_OUTDOORSMAN ] / 50;
				for( uint i = 0; i < len; i++ )
				{
					if( Random( 1, 100 ) > 70 )
					{
						amount++;
					}
				}				
			}
			
			cr.AddItem( lootPid, amount );
			
			cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_YOU_GATHERED, "$proto@msg item " + ( lootPid * 100 ) + "@$amount" + amount );
		}
		else
		{
			_CritAnimateSwing(cr);
			uint[] values = { cr.Id };
			CreateTimeEvent( AFTER( REAL_MS( 700 ) ), "e_diggingSound", values, false );
			CreateTimeEvent( AFTER( REAL_MS( 1000 ) ), "gathering@e_TiredenessTimeout", cr.Id, true );
			
			if( cr.IsInjured() )
			{
				uint damage = INJURED_HP_DAMAGE - cr.Stat[ ST_ENDURANCE ];
				InjureCritter( cr, damage, DAMAGE_UNCALLED, cr.Dir, cr.Id, HIT_LOCATION_NONE, false );
			}
			
			return cr.ParamBase[ ST_LTP_SEQUENCE_WAIT ];
		}
    }
	return 0;
}

bool IsContainerCanStash( Item& stash )
{
	if(	stash.Proto.Item_Subtype == ITEM_SUBTYPE_FURNITURE ||
		stash.Proto.Item_Subtype == ITEM_SUBTYPE_PERSONAL_BAG ||
		stash.Proto.Item_Subtype == ITEM_SUBTYPE_BRIEFCASE )
	{
		return true;
	}
	return false;
}

bool TryDigStash( Critter& cr, Item& tool, Item& stash ) 
{
	Map@ map = cr.GetMap();
    if( !valid( map ) )
	{
		return false;
	}
	
	if( !valid( tool ) )
	{
		return false;
	}
	
	if( !valid( stash ) )
	{
		return false;
	}

   	uint toolPid = tool.GetProtoId();
	if( toolPid != PID_SHOVEL )
	{
		return false;
	}
	
	uint stashPid = stash.GetProtoId();
	if( !IsContainerCanStash( stash ) && stashPid != PID_STASH_BOX )
	{
		return false;
	}
	
	if(checkSand(map, H2T(stash.HexX), H2T(stash.HexY)) == 0)
	{
		return false;
	}

	if( tool.CritSlot != SLOT_HAND1 )
	{
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_EQUIP_TOOL );
		return false;
	}

	if( IsTired( cr ) )
	{
		return false;
	}
	
	if( cr.IsDmgTwoArm() || cr.IsDmgTwoLeg() )
	{ 
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_INJURED_CANT_WORK );
		return false; 
	}
	
	start_dig_stash( cr, tool, stash );
	return true;
}

bool start_dig_stash( Critter& cr, Item& tool, Item& stash)
{
	if( !ltp_dig_stash_inited )
	{
		ltp_dig_stash_init();
	}
	
	if( valid( tool ) && valid( stash ) )
	{	
		uint hit_pause = ACTION_PAUSE_BASE - (int(cr.Stat[ST_AGILITY] / 2) * ACTION_PAUSE_BONUS_RATE );
		cr.ParamBase[ ST_LTP_SEQUENCE_WAIT ] = CLAMP( hit_pause, ACTION_PAUSE_MIN, ACTION_PAUSE_MAX );
		uint digging_rage = cr.Skill[ SK_OUTDOORSMAN ] + ( int(cr.Stat[ ST_STRENGTH ] / 2) * 10 ) + OBJECT_DPA_BASE; 
		cr.ParamBase[ ST_LTP_SEQUENCE ] = uint( ceil( float( ( OBJECT_HP_BASE ) / ( CLAMP( digging_rage, OBJECT_DPA_MIN, OBJECT_DPA_MAX ) ) ) ) );
		
		uint[] values = { cr.Id, tool.Id };
		CreateTimeEvent( AFTER( REAL_MS( 700 ) ), "e_diggingSound", values, false);
		CreateTimeEvent( AFTER( REAL_MS( 1000 ) ), "gathering@e_TiredenessTimeout", cr.Id, true);	
		_CritAnimateSwing(cr);
		StartProcess( cr, LTP_DIG_STASH, 0, tool.Id, stash.Id, cr.ParamBase[ ST_LTP_SEQUENCE_WAIT ] );
		return true;
	}
	return false;
}

uint process_dig_stash( Critter@ cr, int& param0, int& param1, int& param2 )
{
    LTPROCESS( cr, param0, LTP_DIG_STASH )

    Item@ tool = GetItem( param1 );
	Item@ stash = GetItem( param2 );
  	if( valid( tool ) && valid( stash ) )
    {
      	param0++;
		Map@ currMap = cr.GetMap();
		
		if( param0 > cr.ParamBase[ ST_LTP_SEQUENCE ] )
		{
			int pid = stash.GetProtoId();
			
			if( pid == PID_STASH_BOX )
			{
				LogCrimeMovement( cr, stash, "Digged out stash at " + currMap.GetProtoId() + ", " + stash.HexX + " " + stash.HexY);
				Item@[] contents;
				stash.GetItems( 0, contents );
				
				uint16 tx = stash.HexX, ty = stash.HexY;
				uint8 rand = Random(0, 5);
				currMap.MoveHexByDir( tx, ty, rand, 1);
				MoveItems( contents, currMap, tx, ty );
				DeleteItem( stash );
			}
			else if( IsContainerCanStash(stash) )
			{
				LogCrimeMovement( cr, stash, "Buried stash at " + currMap.GetProtoId() + ", " + stash.HexX + " " + stash.HexY);
				Item@ stashPit = currMap.AddItem(stash.HexX, stash.HexY, PID_STASH_BOX, 1);
				MoveItem(stash, 1, stashPit, 0);
				
				uint8 frame = Random(0, 3);
				ChangeFrame( stashPit, frame );
				stashPit.Animate( frame, frame);
				stashPit.TrapValue = cr.Skill[ SK_TRAPS ];
				stashPit.STASH_OWNER = cr.Id;
			}
		}
		else
		{
			_CritAnimateSwing(cr);
			
			if(valid( currMap ))
			{
				uint16 tx = cr.HexX, ty = cr.HexY;
				uint8 rand = Random(0, 5);
				currMap.MoveHexByDir( tx, ty, rand, 1);
				currMap.AddItem( tx, ty, PID_SAND, 1 );
			}
			
			uint[] values = { cr.Id };
			CreateTimeEvent( AFTER( REAL_MS( 700 ) ), "e_diggingSound", values, false );
			CreateTimeEvent( AFTER( REAL_MS( 1000 ) ), "gathering@e_TiredenessTimeout", cr.Id, true );
			
			if( cr.IsInjured() )
			{
				uint damage = INJURED_HP_DAMAGE - cr.Stat[ ST_ENDURANCE ];
				InjureCritter( cr, damage, DAMAGE_UNCALLED, cr.Dir, cr.Id, HIT_LOCATION_NONE, false );
			}

			return cr.ParamBase[ ST_LTP_SEQUENCE_WAIT ];
		}
    }
	return 0;
}

bool CheckRemainsRemoval( Critter& cr, Item@ targetItem )
{
	Map@ map = cr.GetMap();
	PlayGenericSound( map, cr.HexX, cr.HexY, _GetOneRandom( PickaxeSounds ), 30 );
	
	if( valid( targetItem ) )
	{
		uint16 pid = targetItem.GetProtoId();
		if( Ashes.find( targetItem.GetProtoId() ) != -1 )
		{
			cr.SayMsg( SAY_EMOTE_ON_HEAD, TEXTMSG_TEXT, STR_EMOTE_REMOVE_ASH );
			DeleteItem( targetItem );
		}
		else if( pid >= PID_OLD_BLOOD_BONES_1 && targetItem.GetProtoId() <= PID_OLD_BLOOD_BONES_3 )
		{
			
			cr.Animate( 0, ANIM2_PICKUP, null, false, true );
			targetItem.Val0 += Random( 1, 3 );
			cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_REMOVE_SOME_REMAINS );
			
			if( targetItem.Val0 > 10 )
			{
				cr.SayMsg( SAY_EMOTE_ON_HEAD, TEXTMSG_TEXT, STR_EMOTE_REMOVE_REMAINS );
				DeleteItem( targetItem );
			}
		}
		else if( pid == PID_VAULT_DWELLER_BONES )
		{
			cr.Animate( 0, ANIM2_PICKUP, null, false, true );
			targetItem.Val0 += Random( 1, 3 );
			cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_REMOVE_SOME_REMAINS );
			
			if( targetItem.Val0 > 10 )
			{
				Item@ skeleton = cr.AddItem( PID_BONES, 1 );
				skeleton.Val0 = targetItem.Val0;
				skeleton.Val1 = targetItem.Val1;
				skeleton.Val2 = targetItem.Val2;
				skeleton.Val3 = targetItem.Val3;
				skeleton.Val4 = targetItem.Val4;
				skeleton.Val5 = targetItem.Val5;
				skeleton.Val6 = targetItem.Val6;
				skeleton.Val7 = targetItem.Val7;
				skeleton.Val8 = targetItem.Val8;
				skeleton.Val9 = targetItem.Val9;
				
				string @itemLex = Item_GetLexems( targetItem );
				if( valid( itemLex ) ) 
				{
					setLexDesc( skeleton, itemLex );
					setOverridenDesc( skeleton, true );
				}
				
				DropContainerItems( map, targetItem );
				DeleteItem( targetItem );
				cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_COLLECT_REMAINS );
			}
		}
		else
		{
			cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_DID_NOTHING );
			return false;
		}
		return true;
	}
	return false;
}

uint e_diggingSound( uint[]@ values )
{
	Critter@ cr = GetCritter(values[0]);
	Map@ map = cr.GetMap();
	PlayGenericSound( map, cr.HexX, cr.HexY, _GetOneRandom( PickaxeSounds ), 30 );
	return 0;
}

#endif //DIGGING